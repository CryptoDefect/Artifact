// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@( (@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   @  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(  @@%@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   @@%%%%@@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@(  @@&%%%%%%%&@( (@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   @@%%%%%   %%%%@@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@( (@@@@@@@@@@@@@@@@@(  (@%%%* *%%%%%* *%%@@(. @@@@@@@@@@@@@@@@@@( (@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@      @@@@@@@@@@@@@   @@%%%  %%%     %%%  %%@@   @@@@@@@@@@@@@@      @@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@(          (@@@@@@@@(  (&%%%%%* *%%%***%%%* *%%%&@@( (@@@@@@@@@(          (@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   @@%%%%%%%%%%         %%%%%%%%%@@   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@                @@   (@&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&@@@ .(@@@                @@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@,           ,  ,@                                ,,           @                @@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@    ..          @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@..              @@@@@@@@@@@@@@@@@@@@
// @@@@@@@@@@@@@  *   *              *   *                  *    *   *              *   *    *             *   @@@@@@@@@@@@
// @@@@@@@@@@@@@@@%                                                                                         @@@@@@@@@@@@@@@
// @@@@@@@@@@@@@@@%  @@@@@@@@ @@@@@@@@@ @@@@@@@@ @@@@@@@@@%@@@@@@@@ @@@@@@@@ %@@@@@@@@ @@@@@@@@ %@@@@@@@@%  @@@@@@@@@@@@@@@
// @@@@@@@@@@@@@     @@(((((@ @@(((((@@ @@((((@@ @@(((((@@%@@((((@@ @@(((((@ %@#((((@@ @@((((@@ %@(((((@@%     @@@@@@@@@@@@
// @@@@@@@@@@@@@   . @@(@@@(@ @@(@@@(@@ @@@@@@@@ @@(@@@(@@%@@@@@(@@ @@(@@@(@ %@(@@@(@@ @@@@@@@@ %@(@@@(@@%/   @@@@@@@@@@@@@
// @@@@@@@@@@@@@*  ( @  */  @ @@ */.  @ @ **.  @ @@ */  @@%@ **.  @ @  */  @ %@ */.  @ @ **.  @ %@ */  @@%.   @@@@@@@@@@@@@
// @@@@@@@@@@@@@     @  @&    @   &&  @    @&    @  @&   @%   @&  @ @  @&    %   &&  @    @&    %  @&   @%    @@@@@@@@@@@@@
// @@@@@@@@@@@@@ **. @@((((#@*@@((((#@@*@@((((@@*@@((((#@@%@@(((#@@*@@((((#@*%@((((#@@*@@((((@@*%@((((#@@%#  *@@@@@@@@@@@@@
// @@@@@@@@@@@@@     @@@@@@@@ @@@@@@@@@ @@@@@@@@ @@@@@@@@@%@@@@@@@@ @@@@@@@@ %@@@@@@@@ @@@@@@@@ %@@@@@@@@%    @@@@@@@@@@@@@
// @@@@@@@@@@@@@ . . &&&&&&&&*&&&&&&&&&*********/************///****//***((/*******/**%&&&&&&&&*%&&&&&&&&%*  *@@@@@@@@@@@@@
// @@@@@@@@@@@@@   / @@@@@@@@ @@@@@@@@@   %%%%%%%%%.   ,   %           ,   %%%%%%%%   %@@@@@@@@ %@@@@@@@@%,   @@@@@@@@@@@@@
// @@@@@@@@@@@@@     @@ (@([email protected] @@ #@@[email protected]@ . %%%%%%.   .     .((((((((((       . %%%%%   %@@(@@(@@ %@ @@([email protected]@%.   @@@@@@@@@@@@@
// @@@@@@@@@@@@@     @   (  @ @@ *(   @   %%%%       [email protected]@@@@@@@@@@@@@@@@@      ,.%%%   %@ *    @ %@  (  @@%%  %@@@@@@@@@@@@@
// @@@@@@@@@@@@@*  * @  ((   [email protected]   (/  @.  %#      (@@@@@@@@@@@@@@@@@@@@@@@((    ,.%   %   (/   .%  ((   @%    @@@@@@@@@@@@@
// @@@@@@@@@@@@@   , @@ **  @*@@  ** @@*    ,    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@   .    %@@ ** @@*%@ **  @@%    @@@@@@@@@@@@@
// @@@@@@@@@@@@@ . , @@@@@@@@*@@@@@@@@@*     / (@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ .      %@@@@@@@@*%@@@@@@@@%* [email protected]@@@@@@@@@@@@
// @@@@@@@@@@@@@     %%%%%%%%%%%%%%%%%%%    % [email protected]@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@       %%%%%%%%%%%%%%%%%%%%     @@@@@@@@@@@@
// @@@@@@@@@@@@@**** @@@@@@@@ @@@@@@@@@ ****%  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  *****%@@@@@@@@ %@@@@@@@@%*****@@@@@@@@@@@@
// @@@@@@@@@@@@@   / @@*   (@ @@     @@ ,   %  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   %   %@@    @@ %@*   (@@%%  %@@@@@@@@@@@@@
// @@@@@@@@@@@@@   . @(/(%(/@ @@/(%(/#@     %  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@       %@(((((#@ %@/(%(/@@%.   @@@@@@@@@@@@@
// @@@@@@@@@@@@@%  % @   (  @ @@ *(   @ %   %  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@       %@ *    @ %@  (  @@%,   @@@@@@@@@@@@@
// @@@@@@@@@@@@@     @  @&  ([email protected](  &&  @.    %  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   *   %(  @&  (.%( @&  (@%*  *@@@@@@@@@@@@@
// @@@@@@@@@@@@@ %%  @@@@@@@@*@@@@@@@@@*  % %  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@       %@@@@@@@@*%@@@@@@@@%    @@@@@@@@@@@@@
// @@@@@@@@@@@@@   , &&&&&&&&*&&&&&&&&&*    %  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   **  %&&@@@&&&*%&&&&&&&&%(  *@@@@@@@@@@@@@
// @@@@@@@@@@@@@   , @@@@@@@@ @@@@@@@@@     %  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@       %@@@@@@@@ %@@@@@@@@%    @@@@@@@@@@@@@
// @@@@@@@@@@@@@*  * @@@@@@@@ @@@@@@@@@ ** (%  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@       %@@((((@@ %@@(((@@@%.   @@@@@@@@@@@@@
// @@@@@@@@@@@@@   , @@ @@@*@ @@ @@@*@@     %  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   %   %@@@@@@@@ %@ @@@*@@%%  %@@@@@@@@@@@@@
// @@@@@@@@@@@@@ **. @  **  @ @@ **,  @   * %  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@       %@ **,  @ %@ **  @@%    @@@@@@@@@@@@@
// @@@@@@@@@@@@@   . @  @@    @   @&  @     %  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@    %  %   @&    %  @@   @%,   @@@@@@@@@@@@@
// @@@@@@@@@@@@@   . @@(##((@*@@((##(@@*    %  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  *   *%@@(##(@@*%@(##((@@%    @@@@@@@@@@@@@
// @@@@@@@@@@@@@     @@@@@@@@*@@@@@@@@@* % %%  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@       %@@@@@@@@*%@@@@@@@@% ,  @@@@@@@@@@@@@
// @@@@@@@@@@@@@,..( %%%%%%%%%%%%%%%%%%%    %  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@        %%%%%%%%%%%%%%%%%%%,  [email protected]@@@@@@@@@@@@
// @@@@@@@@@@@@@...( ..................   . %  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@              ....  , .....([email protected]@@@@@@@@@@@@
// @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
//
// Forgotten Runes Gate to The Seventh Realm
// https://forgottenrunes.com
// Twitter: @forgottenrunes
pragma solidity ^0.8.0;

import '../util/Ownablearama.sol';
import '../interfaces/IBookOfLore.sol';
import './interfaces/IBeastsAuctionHouse.sol';
import '@openzeppelin/contracts/security/ReentrancyGuard.sol';
import '@openzeppelin/contracts/token/ERC721/IERC721.sol';
import '@openzeppelin/contracts/utils/cryptography/ECDSA.sol';
import '@openzeppelin/contracts/utils/cryptography/draft-EIP712.sol';
import '@openzeppelin/contracts/utils/cryptography/MerkleProof.sol';

interface IMintable {
    function mint(address recipient, uint256 tokenId) external;
}

contract GateToTheSeventhRealmMinter is Ownablearama, ReentrancyGuard, EIP712 {
    /// @notice The start block for minting
    uint256 public startBlock =
        0xffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff;

    /// @notice The Merkle root for keyholders
    bytes32 public keysMerkleRoot;

    /// @notice The Merkle root for spellholders
    bytes32 public spellsMerkleRoot;

    /// @notice The number of locks on this gate
    uint256 public constant NUM_LOCKS = 21;

    /// @notice The number of locks unlocked
    uint256 public numUnlocked = 0;

    /// @notice The address to the Book of Lore
    address public bookOfLoreAddress;

    /// @notice The address of the Wizards contract
    address public wizardsAddress;

    /// @notice The address of the Souls contract
    address public soulsAddress;

    /// @notice The address to the Gate of the Seventh Realm token contract
    address public gateOfTheSeventhRealmAddress;

    /// @notice The address to the Beasts Auction House token contract
    address public auctionHouseAddress;

    /// @notice The EIP-712 typehash for the unlock struct used by the contract
    bytes32 public constant UNLOCK_TYPEHASH =
        keccak256('Unlock(address ownerA,uint256 wizardIdA,address ownerB)');

    /// @notice A mapping of the addresses of those who participated in the unlock ceremony
    mapping(address => bool) public unlockers;

    /// @notice A mapping of wizard (or soul) IDs who participated in the unlock ceremony
    mapping(uint256 => bool) public unlockerWizards;

    /// @notice The Unlock event is emitted when a lock is unlocked
    event Unlocked(
        address wizA,
        uint256 tokenIdA,
        address wizB,
        uint256 tokenIdB,
        uint256 lockIdx
    );

    /**
     * @dev Create the contract and set initial configuration
     * @param _bookOfLoreAddress address to the Book of Lore
     * @param _wizardsAddress address to the Wizards token
     * @param _soulsAddress address to the Souls token
     */
    constructor(
        address _bookOfLoreAddress,
        address _wizardsAddress,
        address _soulsAddress
    ) EIP712('GateToTheSeventhRealmMinter', '1') {
        setBookOfLoreAddress(_bookOfLoreAddress);
        setWizardsAddress(_wizardsAddress);
        setSoulsAddress(_soulsAddress);
    }

    /**
     * @dev Convenient way to initialize the post-constructor configuration
     */
    function initialize(
        uint256 newStartBlock,
        bytes32 newKeysMerkleRoot,
        bytes32 newSpellsMerkleRoot,
        address newGateOfTheSeventhRealmAddress,
        address newAuctionHouseAddress
    ) public onlyOwner {
        setStartBlock(newStartBlock);
        setKeysMerkleRoot(newKeysMerkleRoot);
        setSpellsMerkleRoot(newSpellsMerkleRoot);
        setGateOfTheSeventhRealmAddress(newGateOfTheSeventhRealmAddress);
        setAuctionHouseAddress(newAuctionHouseAddress);
    }

    /**
     * @notice Returns if the minting has started
     * @return bool if the minting has started
     */
    function started() public view returns (bool) {
        return block.number >= startBlock;
    }

    /**
     * @notice Unlock a lock from the gate
     * @dev Unlocking the gate is a collaboration between two wizards: one with a key and one with a spell. ownerA signs a message and gives that signature to ownerB. ownerB must be the msg.sender. The frontend needs to submit proper proofs matching wizard A and B with a key or spell - this means the frontend needs to know a mapping of wizard (or soul) ids to keys and spells.
     * @param ownerA address the partner's address and signer of `signatureA`
     * @param wizIdA uint256 the ID of the partner's wizard
     * @param wizIdB uint256 the ID of the sender's wizard
     * @param wizAMerkleProof bytes32 the proof that wizIdA holds a key or a spell
     * @param wizBMerkleProof bytes32 the proof that wizIdB holds a key or a spell
     * @param signatureA bytes the signature from the partner
     */
    function unlock(
        address ownerA,
        uint256 wizIdA,
        uint256 wizIdB,
        bytes32[] calldata wizAMerkleProof,
        bytes32[] calldata wizBMerkleProof,
        bytes memory signatureA
    ) public nonReentrant {
        require(started(), 'Not started');
        require(numUnlocked < NUM_LOCKS, 'Already unlocked');
        require(_ownsWizardOrSoul(ownerA, wizIdA), 'Invalid Wiz A Owner');
        require(_ownsWizardOrSoul(msg.sender, wizIdB), 'Invalid Wiz B Owner');
        require(!unlockers[ownerA], 'Partner already unlocked');
        require(!unlockers[msg.sender], 'You already unlocked');
        require(!unlockerWizards[wizIdA], 'Wizard A already unlocked');
        require(!unlockerWizards[wizIdB], 'Wizard B already unlocked');

        require(
            (_wizardHoldsKey(wizIdA, wizAMerkleProof) &&
                _wizardHoldsSpell(wizIdB, wizBMerkleProof)) ||
                (_wizardHoldsKey(wizIdB, wizBMerkleProof) &&
                    _wizardHoldsSpell(wizIdA, wizAMerkleProof)),
            'Wizard pairing failed'
        );

        // construct an expected hash, given the parameters
        bytes32 digest = _hashTypedDataV4(
            keccak256(abi.encode(UNLOCK_TYPEHASH, ownerA, wizIdA, msg.sender))
        );

        // now recover the signer from the provided signature
        address signer = ECDSA.recover(digest, signatureA);

        // make sure the recover extracted a signer, but beware, because this
        // can return non-zero for some invalid cases (apparently?)
        require(signer != address(0), 'ECDSA: invalid signature');
        require(signer == ownerA, 'unlock: signature is not the partner owner');
        require(ownerA != msg.sender, 'nice try');

        unlockers[msg.sender] = true;
        unlockers[ownerA] = true;
        unlockerWizards[wizIdA] = true;
        unlockerWizards[wizIdB] = true;

        IMintable(gateOfTheSeventhRealmAddress).mint(
            msg.sender,
            numUnlocked * 2
        );
        IMintable(gateOfTheSeventhRealmAddress).mint(
            ownerA,
            (numUnlocked * 2) + 1
        );

        // write the Lore
        if (bookOfLoreAddress != address(0)) {
            IBookOfLore(bookOfLoreAddress).addLoreWithScribe(
                _loreContractAddress(wizIdB),
                wizIdB,
                0,
                false,
                ''
            );
        }

        emit Unlocked(ownerA, wizIdA, msg.sender, wizIdB, numUnlocked);

        numUnlocked += 1;

        if (numUnlocked == NUM_LOCKS) {
            IBeastsAuctionHouse(auctionHouseAddress).openTheGate();
        }
    }

    function _wizardHoldsSpell(uint256 wizId, bytes32[] calldata _merkleProof)
        private
        view
        returns (bool)
    {
        return
            MerkleProof.verify(
                _merkleProof,
                spellsMerkleRoot,
                keccak256(abi.encodePacked(wizId))
            );
    }

    function _wizardHoldsKey(uint256 wizId, bytes32[] calldata _merkleProof)
        private
        view
        returns (bool)
    {
        return
            MerkleProof.verify(
                _merkleProof,
                keysMerkleRoot,
                keccak256(abi.encodePacked(wizId))
            );
    }

    function _loreContractAddress(uint256 wizId)
        private
        view
        returns (address)
    {
        try IERC721(wizardsAddress).ownerOf(wizId) returns (address) {
            return wizardsAddress;
        } catch Error(string memory) {
            // try a soul
        }

        try IERC721(soulsAddress).ownerOf(wizId) returns (address) {
            return soulsAddress;
        } catch Error(string memory) {
            // not soul either
        }

        return wizardsAddress;
    }

    function _ownsWizardOrSoul(address owner, uint256 wizId)
        private
        view
        returns (bool)
    {
        require(owner != address(0), 'invalid owner');

        try IERC721(wizardsAddress).ownerOf(wizId) returns (
            address wizardOwner
        ) {
            if (wizardOwner == owner) {
                return true;
            }
        } catch Error(string memory) {
            // try a soul
        }

        try IERC721(soulsAddress).ownerOf(wizId) returns (address soulOwner) {
            if (soulOwner == owner) {
                return true;
            }
        } catch Error(string memory) {
            // not soul either
        }

        return false;
    }

    function domainSeparator() external view returns (bytes32) {
        return _domainSeparatorV4();
    }

    /**
     * Owner Controls
     */

    /**
     * @dev Set the start block
     * @param newStartBlock uint256 the block to start at
     */
    function setStartBlock(uint256 newStartBlock) public onlyOwner {
        startBlock = newStartBlock;
    }

    /**
     * @dev Set the merkle root for key-holder IDs
     * @param newMerkleRoot bytes32 the merkle root
     */
    function setKeysMerkleRoot(bytes32 newMerkleRoot) public onlyOwner {
        keysMerkleRoot = newMerkleRoot;
    }

    /**
     * @dev Set the merkle root for spell-holder IDs
     * @param newMerkleRoot bytes32 the merkle root
     */
    function setSpellsMerkleRoot(bytes32 newMerkleRoot) public onlyOwner {
        spellsMerkleRoot = newMerkleRoot;
    }

    /**
     * @dev Set the address of the Gate of the Seventh Realm
     * @param newGateOfTheSeventhRealmAddress address the address to the contract
     */
    function setGateOfTheSeventhRealmAddress(
        address newGateOfTheSeventhRealmAddress
    ) public onlyOwner {
        gateOfTheSeventhRealmAddress = newGateOfTheSeventhRealmAddress;
    }

    /**
     * @dev Set the address of the Auction House
     * @param newAuctionHouseAddress address the address to the contract
     */
    function setAuctionHouseAddress(address newAuctionHouseAddress)
        public
        onlyOwner
    {
        auctionHouseAddress = newAuctionHouseAddress;
    }

    /**
     * @dev Set the address of the Book of Lore contract
     * @param newBookOfLoreAddress address the address to the contract
     */
    function setBookOfLoreAddress(address newBookOfLoreAddress)
        public
        onlyOwner
    {
        bookOfLoreAddress = newBookOfLoreAddress;
    }

    /**
     * @dev Set the address of the Forgotten Runes Wizards Cult tokens
     * @param newWizardsAddress address the address to the contract
     */
    function setWizardsAddress(address newWizardsAddress) public onlyOwner {
        wizardsAddress = newWizardsAddress;
    }

    /**
     * @dev Set the address of Forgotten Souls contract
     * @param newSoulsAddress address the address to the contract
     */
    function setSoulsAddress(address newSoulsAddress) public onlyOwner {
        soulsAddress = newSoulsAddress;
    }

    /**
     * @dev Recover ERC721's accidentally sent to this contract
     * @param token IERC721 the address of the token
     * @param to address where to send the token
     * @param tokenId uint256 the token ID
     */
    function transferFrom(
        IERC721 token,
        address to,
        uint256 tokenId
    ) public onlyOwner {
        token.transferFrom(address(this), to, tokenId);
    }

    /**
     * @dev Set approval for an operator to manage our ERC721s, again, for emergencies
     * @param token IERC721 the address of the token
     * @param operator address the operator's address
     * @param approved bool operator approval
     */
    function setApprovalForAll(
        IERC721 token,
        address operator,
        bool approved
    ) public onlyOwner {
        token.setApprovalForAll(operator, approved);
    }

    function __remove(address unlocker, uint256 tokenId) public onlyOwner {
        delete unlockers[unlocker];
        delete unlockerWizards[tokenId];
    }
}

pragma solidity ^0.8.0;

import '@openzeppelin/contracts/access/Ownable.sol';
import '@openzeppelin/contracts/token/ERC20/IERC20.sol';

/**
 * @dev This implements Ownable plus a few utilities
 */
contract Ownablearama is Ownable {
    /**
     * @dev ETH should not be sent to this contract, but in the case that it is
     * sent by accident, this function allows the owner to withdraw it.
     */
    function withdrawAll() public payable onlyOwner {
        require(payable(msg.sender).send(address(this).balance));
    }

    /**
     * @dev Again, ERC20s should not be sent to this contract, but if someone
     * does, it's nice to be able to recover them
     * @param token IERC20 the token address
     * @param amount uint256 the amount to send
     */
    function forwardERC20s(IERC20 token, uint256 amount) public onlyOwner {
        require(address(msg.sender) != address(0));
        token.transfer(msg.sender, amount);
    }

    // disable renouncing ownership
    function renounceOwnership() public override onlyOwner {}
}

interface IBookOfLore {
    function addLoreWithScribe(
        address tokenContract,
        uint256 tokenId,
        uint256 parentLoreId,
        bool nsfw,
        string memory loreMetadataURI
    ) external;

    function addLore(
        address tokenContract,
        uint256 tokenId,
        uint256 parentLoreId,
        bool nsfw,
        string memory loreMetadataURI
    ) external;
}

// SPDX-License-Identifier: GPL-3.0

/// @title Interface for Beasts Auction Houses

pragma solidity ^0.8.6;

interface IBeastsAuctionHouse {
    struct Auction {
        // ID for the Beast (ERC721 token ID)
        uint256 beastId;
        // The current highest bid amount
        uint256 amount;
        // The time that the auction started
        uint256 startTime;
        // The time that the auction is scheduled to end
        uint256 endTime;
        // The address of the current highest bid
        address payable bidder;
        // Whether or not the auction has been settled
        bool settled;
    }

    event AuctionCreated(
        uint256 indexed beastId,
        uint256 startTime,
        uint256 endTime
    );

    event AuctionBid(
        uint256 indexed beastId,
        address sender,
        uint256 value,
        bool extended
    );

    event AuctionExtended(uint256 indexed beastId, uint256 endTime);

    event AuctionSettled(
        uint256 indexed beastId,
        address winner,
        uint256 amount
    );

    event AuctionTimeBufferUpdated(uint256 timeBuffer);

    event AuctionReservePriceUpdated(uint256 reservePrice);

    event AuctionMinBidIncrementPercentageUpdated(
        uint256 minBidIncrementPercentage
    );

    function settleAuction() external;

    function settleCurrentAndCreateNewAuction() external;

    function createBid(uint256 beastId) external payable;

    function pause() external;

    function unpause() external;

    function openTheGate() external;

    function setTimeBuffer(uint256 timeBuffer) external;

    function setReservePrice(uint256 reservePrice) external;

    function setMinBidIncrementPercentage(uint8 minBidIncrementPercentage)
        external;

    function setTreasury(address newTreasury) external;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and make it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Elliptic Curve Digital Signature Algorithm (ECDSA) operations.
 *
 * These functions can be used to verify that a message was signed by the holder
 * of the private keys of a given address.
 */
library ECDSA {
    /**
     * @dev Returns the address that signed a hashed message (`hash`) with
     * `signature`. This address can then be used for verification purposes.
     *
     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:
     * this function rejects them by requiring the `s` value to be in the lower
     * half order, and the `v` value to be either 27 or 28.
     *
     * IMPORTANT: `hash` _must_ be the result of a hash operation for the
     * verification to be secure: it is possible to craft signatures that
     * recover to arbitrary addresses for non-hashed data. A safe way to ensure
     * this is by receiving a hash of the original message (which may otherwise
     * be too long), and then calling {toEthSignedMessageHash} on it.
     *
     * Documentation for signature generation:
     * - with https://web3js.readthedocs.io/en/v1.3.4/web3-eth-accounts.html#sign[Web3.js]
     * - with https://docs.ethers.io/v5/api/signer/#Signer-signMessage[ethers]
     */
    function recover(bytes32 hash, bytes memory signature) internal pure returns (address) {
        // Check the signature length
        // - case 65: r,s,v signature (standard)
        // - case 64: r,vs signature (cf https://eips.ethereum.org/EIPS/eip-2098) _Available since v4.1._
        if (signature.length == 65) {
            bytes32 r;
            bytes32 s;
            uint8 v;
            // ecrecover takes the signature parameters, and the only way to get them
            // currently is to use assembly.
            assembly {
                r := mload(add(signature, 0x20))
                s := mload(add(signature, 0x40))
                v := byte(0, mload(add(signature, 0x60)))
            }
            return recover(hash, v, r, s);
        } else if (signature.length == 64) {
            bytes32 r;
            bytes32 vs;
            // ecrecover takes the signature parameters, and the only way to get them
            // currently is to use assembly.
            assembly {
                r := mload(add(signature, 0x20))
                vs := mload(add(signature, 0x40))
            }
            return recover(hash, r, vs);
        } else {
            revert("ECDSA: invalid signature length");
        }
    }

    /**
     * @dev Overload of {ECDSA-recover} that receives the `r` and `vs` short-signature fields separately.
     *
     * See https://eips.ethereum.org/EIPS/eip-2098[EIP-2098 short signatures]
     *
     * _Available since v4.2._
     */
    function recover(
        bytes32 hash,
        bytes32 r,
        bytes32 vs
    ) internal pure returns (address) {
        bytes32 s;
        uint8 v;
        assembly {
            s := and(vs, 0x7fffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff)
            v := add(shr(255, vs), 27)
        }
        return recover(hash, v, r, s);
    }

    /**
     * @dev Overload of {ECDSA-recover} that receives the `v`, `r` and `s` signature fields separately.
     */
    function recover(
        bytes32 hash,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) internal pure returns (address) {
        // EIP-2 still allows signature malleability for ecrecover(). Remove this possibility and make the signature
        // unique. Appendix F in the Ethereum Yellow paper (https://ethereum.github.io/yellowpaper/paper.pdf), defines
        // the valid range for s in (281): 0 < s < secp256k1n ÷ 2 + 1, and for v in (282): v ∈ {27, 28}. Most
        // signatures from current libraries generate a unique signature with an s-value in the lower half order.
        //
        // If your library generates malleable signatures, such as s-values in the upper range, calculate a new s-value
        // with 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141 - s1 and flip v from 27 to 28 or
        // vice versa. If your library also generates signatures with 0/1 for v instead 27/28, add 27 to v to accept
        // these malleable signatures as well.
        require(
            uint256(s) <= 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0,
            "ECDSA: invalid signature 's' value"
        );
        require(v == 27 || v == 28, "ECDSA: invalid signature 'v' value");

        // If the signature is valid (and not malleable), return the signer address
        address signer = ecrecover(hash, v, r, s);
        require(signer != address(0), "ECDSA: invalid signature");

        return signer;
    }

    /**
     * @dev Returns an Ethereum Signed Message, created from a `hash`. This
     * produces hash corresponding to the one signed with the
     * https://eth.wiki/json-rpc/API#eth_sign[`eth_sign`]
     * JSON-RPC method as part of EIP-191.
     *
     * See {recover}.
     */
    function toEthSignedMessageHash(bytes32 hash) internal pure returns (bytes32) {
        // 32 is the length in bytes of hash,
        // enforced by the type signature above
        return keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", hash));
    }

    /**
     * @dev Returns an Ethereum Signed Typed Data, created from a
     * `domainSeparator` and a `structHash`. This produces hash corresponding
     * to the one signed with the
     * https://eips.ethereum.org/EIPS/eip-712[`eth_signTypedData`]
     * JSON-RPC method as part of EIP-712.
     *
     * See {recover}.
     */
    function toTypedDataHash(bytes32 domainSeparator, bytes32 structHash) internal pure returns (bytes32) {
        return keccak256(abi.encodePacked("\x19\x01", domainSeparator, structHash));
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./ECDSA.sol";

/**
 * @dev https://eips.ethereum.org/EIPS/eip-712[EIP 712] is a standard for hashing and signing of typed structured data.
 *
 * The encoding specified in the EIP is very generic, and such a generic implementation in Solidity is not feasible,
 * thus this contract does not implement the encoding itself. Protocols need to implement the type-specific encoding
 * they need in their contracts using a combination of `abi.encode` and `keccak256`.
 *
 * This contract implements the EIP 712 domain separator ({_domainSeparatorV4}) that is used as part of the encoding
 * scheme, and the final step of the encoding to obtain the message digest that is then signed via ECDSA
 * ({_hashTypedDataV4}).
 *
 * The implementation of the domain separator was designed to be as efficient as possible while still properly updating
 * the chain id to protect against replay attacks on an eventual fork of the chain.
 *
 * NOTE: This contract implements the version of the encoding known as "v4", as implemented by the JSON RPC method
 * https://docs.metamask.io/guide/signing-data.html[`eth_signTypedDataV4` in MetaMask].
 *
 * _Available since v3.4._
 */
abstract contract EIP712 {
    /* solhint-disable var-name-mixedcase */
    // Cache the domain separator as an immutable value, but also store the chain id that it corresponds to, in order to
    // invalidate the cached domain separator if the chain id changes.
    bytes32 private immutable _CACHED_DOMAIN_SEPARATOR;
    uint256 private immutable _CACHED_CHAIN_ID;

    bytes32 private immutable _HASHED_NAME;
    bytes32 private immutable _HASHED_VERSION;
    bytes32 private immutable _TYPE_HASH;

    /* solhint-enable var-name-mixedcase */

    /**
     * @dev Initializes the domain separator and parameter caches.
     *
     * The meaning of `name` and `version` is specified in
     * https://eips.ethereum.org/EIPS/eip-712#definition-of-domainseparator[EIP 712]:
     *
     * - `name`: the user readable name of the signing domain, i.e. the name of the DApp or the protocol.
     * - `version`: the current major version of the signing domain.
     *
     * NOTE: These parameters cannot be changed except through a xref:learn::upgrading-smart-contracts.adoc[smart
     * contract upgrade].
     */
    constructor(string memory name, string memory version) {
        bytes32 hashedName = keccak256(bytes(name));
        bytes32 hashedVersion = keccak256(bytes(version));
        bytes32 typeHash = keccak256(
            "EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)"
        );
        _HASHED_NAME = hashedName;
        _HASHED_VERSION = hashedVersion;
        _CACHED_CHAIN_ID = block.chainid;
        _CACHED_DOMAIN_SEPARATOR = _buildDomainSeparator(typeHash, hashedName, hashedVersion);
        _TYPE_HASH = typeHash;
    }

    /**
     * @dev Returns the domain separator for the current chain.
     */
    function _domainSeparatorV4() internal view returns (bytes32) {
        if (block.chainid == _CACHED_CHAIN_ID) {
            return _CACHED_DOMAIN_SEPARATOR;
        } else {
            return _buildDomainSeparator(_TYPE_HASH, _HASHED_NAME, _HASHED_VERSION);
        }
    }

    function _buildDomainSeparator(
        bytes32 typeHash,
        bytes32 nameHash,
        bytes32 versionHash
    ) private view returns (bytes32) {
        return keccak256(abi.encode(typeHash, nameHash, versionHash, block.chainid, address(this)));
    }

    /**
     * @dev Given an already https://eips.ethereum.org/EIPS/eip-712#definition-of-hashstruct[hashed struct], this
     * function returns the hash of the fully encoded EIP712 message for this domain.
     *
     * This hash can be used together with {ECDSA-recover} to obtain the signer of a message. For example:
     *
     * ```solidity
     * bytes32 digest = _hashTypedDataV4(keccak256(abi.encode(
     *     keccak256("Mail(address to,string contents)"),
     *     mailTo,
     *     keccak256(bytes(mailContents))
     * )));
     * address signer = ECDSA.recover(digest, signature);
     * ```
     */
    function _hashTypedDataV4(bytes32 structHash) internal view virtual returns (bytes32) {
        return ECDSA.toTypedDataHash(_domainSeparatorV4(), structHash);
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev These functions deal with verification of Merkle Trees proofs.
 *
 * The proofs can be generated using the JavaScript library
 * https://github.com/miguelmota/merkletreejs[merkletreejs].
 * Note: the hashing algorithm should be keccak256 and pair sorting should be enabled.
 *
 * See `test/utils/cryptography/MerkleProof.test.js` for some examples.
 */
library MerkleProof {
    /**
     * @dev Returns true if a `leaf` can be proved to be a part of a Merkle tree
     * defined by `root`. For this, a `proof` must be provided, containing
     * sibling hashes on the branch from the leaf to the root of the tree. Each
     * pair of leaves and each pair of pre-images are assumed to be sorted.
     */
    function verify(
        bytes32[] memory proof,
        bytes32 root,
        bytes32 leaf
    ) internal pure returns (bool) {
        bytes32 computedHash = leaf;

        for (uint256 i = 0; i < proof.length; i++) {
            bytes32 proofElement = proof[i];

            if (computedHash <= proofElement) {
                // Hash(current computed hash + current element of the proof)
                computedHash = keccak256(abi.encodePacked(computedHash, proofElement));
            } else {
                // Hash(current element of the proof + current computed hash)
                computedHash = keccak256(abi.encodePacked(proofElement, computedHash));
            }
        }

        // Check if the computed hash (root) is equal to the provided root
        return computedHash == root;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _setOwner(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _setOwner(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _setOwner(newOwner);
    }

    function _setOwner(address newOwner) private {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}