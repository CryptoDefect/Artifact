/**

 *Submitted for verification at Etherscan.io on 2023-12-02

*/



// File: https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/utils/Base64.sol





// OpenZeppelin Contracts (last updated v5.0.0) (utils/Base64.sol)



pragma solidity ^0.8.20;



/**

 * @dev Provides a set of functions to operate with Base64 strings.

 */

library Base64 {

    /**

     * @dev Base64 Encoding/Decoding Table

     */

    string internal constant _TABLE = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/";



    /**

     * @dev Converts a `bytes` to its Bytes64 `string` representation.

     */

    function encode(bytes memory data) internal pure returns (string memory) {

        /**

         * Inspired by Brecht Devos (Brechtpd) implementation - MIT licence

         * https://github.com/Brechtpd/base64/blob/e78d9fd951e7b0977ddca77d92dc85183770daf4/base64.sol

         */

        if (data.length == 0) return "";



        // Loads the table into memory

        string memory table = _TABLE;



        // Encoding takes 3 bytes chunks of binary data from `bytes` data parameter

        // and split into 4 numbers of 6 bits.

        // The final Base64 length should be `bytes` data length multiplied by 4/3 rounded up

        // - `data.length + 2`  -> Round up

        // - `/ 3`              -> Number of 3-bytes chunks

        // - `4 *`              -> 4 characters for each chunk

        string memory result = new string(4 * ((data.length + 2) / 3));



        /// @solidity memory-safe-assembly

        assembly {

            // Prepare the lookup table (skip the first "length" byte)

            let tablePtr := add(table, 1)



            // Prepare result pointer, jump over length

            let resultPtr := add(result, 32)



            // Run over the input, 3 bytes at a time

            for {

                let dataPtr := data

                let endPtr := add(data, mload(data))

            } lt(dataPtr, endPtr) {



            } {

                // Advance 3 bytes

                dataPtr := add(dataPtr, 3)

                let input := mload(dataPtr)



                // To write each character, shift the 3 bytes (18 bits) chunk

                // 4 times in blocks of 6 bits for each character (18, 12, 6, 0)

                // and apply logical AND with 0x3F which is the number of

                // the previous character in the ASCII table prior to the Base64 Table

                // The result is then added to the table to get the character to write,

                // and finally write it in the result pointer but with a left shift

                // of 256 (1 byte) - 8 (1 ASCII char) = 248 bits



                mstore8(resultPtr, mload(add(tablePtr, and(shr(18, input), 0x3F))))

                resultPtr := add(resultPtr, 1) // Advance



                mstore8(resultPtr, mload(add(tablePtr, and(shr(12, input), 0x3F))))

                resultPtr := add(resultPtr, 1) // Advance



                mstore8(resultPtr, mload(add(tablePtr, and(shr(6, input), 0x3F))))

                resultPtr := add(resultPtr, 1) // Advance



                mstore8(resultPtr, mload(add(tablePtr, and(input, 0x3F))))

                resultPtr := add(resultPtr, 1) // Advance

            }



            // When data `bytes` is not exactly 3 bytes long

            // it is padded with `=` characters at the end

            switch mod(mload(data), 3)

            case 1 {

                mstore8(sub(resultPtr, 1), 0x3d)

                mstore8(sub(resultPtr, 2), 0x3d)

            }

            case 2 {

                mstore8(sub(resultPtr, 1), 0x3d)

            }

        }



        return result;

    }

}



// File: @openzeppelin/contracts/utils/Context.sol





// OpenZeppelin Contracts (last updated v5.0.0) (utils/Context.sol)



pragma solidity ^0.8.20;



/**

 * @dev Provides information about the current execution context, including the

 * sender of the transaction and its data. While these are generally available

 * via msg.sender and msg.data, they should not be accessed in such a direct

 * manner, since when dealing with meta-transactions the account sending and

 * paying for execution may not be the actual sender (as far as an application

 * is concerned).

 *

 * This contract is only required for intermediate, library-like contracts.

 */

abstract contract Context {

    function _msgSender() internal view virtual returns (address) {

        return msg.sender;

    }



    function _msgData() internal view virtual returns (bytes calldata) {

        return msg.data;

    }

}



// File: @openzeppelin/contracts/access/Ownable.sol





// OpenZeppelin Contracts (last updated v5.0.0) (access/Ownable.sol)



pragma solidity ^0.8.20;





/**

 * @dev Contract module which provides a basic access control mechanism, where

 * there is an account (an owner) that can be granted exclusive access to

 * specific functions.

 *

 * The initial owner is set to the address provided by the deployer. This can

 * later be changed with {transferOwnership}.

 *

 * This module is used through inheritance. It will make available the modifier

 * `onlyOwner`, which can be applied to your functions to restrict their use to

 * the owner.

 */

abstract contract Ownable is Context {

    address private _owner;



    /**

     * @dev The caller account is not authorized to perform an operation.

     */

    error OwnableUnauthorizedAccount(address account);



    /**

     * @dev The owner is not a valid owner account. (eg. `address(0)`)

     */

    error OwnableInvalidOwner(address owner);



    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);



    /**

     * @dev Initializes the contract setting the address provided by the deployer as the initial owner.

     */

    constructor(address initialOwner) {

        if (initialOwner == address(0)) {

            revert OwnableInvalidOwner(address(0));

        }

        _transferOwnership(initialOwner);

    }



    /**

     * @dev Throws if called by any account other than the owner.

     */

    modifier onlyOwner() {

        _checkOwner();

        _;

    }



    /**

     * @dev Returns the address of the current owner.

     */

    function owner() public view virtual returns (address) {

        return _owner;

    }



    /**

     * @dev Throws if the sender is not the owner.

     */

    function _checkOwner() internal view virtual {

        if (owner() != _msgSender()) {

            revert OwnableUnauthorizedAccount(_msgSender());

        }

    }



    /**

     * @dev Leaves the contract without owner. It will not be possible to call

     * `onlyOwner` functions. Can only be called by the current owner.

     *

     * NOTE: Renouncing ownership will leave the contract without an owner,

     * thereby disabling any functionality that is only available to the owner.

     */

    function renounceOwnership() public virtual onlyOwner {

        _transferOwnership(address(0));

    }



    /**

     * @dev Transfers ownership of the contract to a new account (`newOwner`).

     * Can only be called by the current owner.

     */

    function transferOwnership(address newOwner) public virtual onlyOwner {

        if (newOwner == address(0)) {

            revert OwnableInvalidOwner(address(0));

        }

        _transferOwnership(newOwner);

    }



    /**

     * @dev Transfers ownership of the contract to a new account (`newOwner`).

     * Internal function without access restriction.

     */

    function _transferOwnership(address newOwner) internal virtual {

        address oldOwner = _owner;

        _owner = newOwner;

        emit OwnershipTransferred(oldOwner, newOwner);

    }

}



// File: @openzeppelin/contracts/utils/math/SignedMath.sol





// OpenZeppelin Contracts (last updated v5.0.0) (utils/math/SignedMath.sol)



pragma solidity ^0.8.20;



/**

 * @dev Standard signed math utilities missing in the Solidity language.

 */

library SignedMath {

    /**

     * @dev Returns the largest of two signed numbers.

     */

    function max(int256 a, int256 b) internal pure returns (int256) {

        return a > b ? a : b;

    }



    /**

     * @dev Returns the smallest of two signed numbers.

     */

    function min(int256 a, int256 b) internal pure returns (int256) {

        return a < b ? a : b;

    }



    /**

     * @dev Returns the average of two signed numbers without overflow.

     * The result is rounded towards zero.

     */

    function average(int256 a, int256 b) internal pure returns (int256) {

        // Formula from the book "Hacker's Delight"

        int256 x = (a & b) + ((a ^ b) >> 1);

        return x + (int256(uint256(x) >> 255) & (a ^ b));

    }



    /**

     * @dev Returns the absolute unsigned value of a signed value.

     */

    function abs(int256 n) internal pure returns (uint256) {

        unchecked {

            // must be unchecked in order to support `n = type(int256).min`

            return uint256(n >= 0 ? n : -n);

        }

    }

}



// File: @openzeppelin/contracts/utils/math/Math.sol





// OpenZeppelin Contracts (last updated v5.0.0) (utils/math/Math.sol)



pragma solidity ^0.8.20;



/**

 * @dev Standard math utilities missing in the Solidity language.

 */

library Math {

    /**

     * @dev Muldiv operation overflow.

     */

    error MathOverflowedMulDiv();



    enum Rounding {

        Floor, // Toward negative infinity

        Ceil, // Toward positive infinity

        Trunc, // Toward zero

        Expand // Away from zero

    }



    /**

     * @dev Returns the addition of two unsigned integers, with an overflow flag.

     */

    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {

        unchecked {

            uint256 c = a + b;

            if (c < a) return (false, 0);

            return (true, c);

        }

    }



    /**

     * @dev Returns the subtraction of two unsigned integers, with an overflow flag.

     */

    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {

        unchecked {

            if (b > a) return (false, 0);

            return (true, a - b);

        }

    }



    /**

     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.

     */

    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {

        unchecked {

            // Gas optimization: this is cheaper than requiring 'a' not being zero, but the

            // benefit is lost if 'b' is also tested.

            // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522

            if (a == 0) return (true, 0);

            uint256 c = a * b;

            if (c / a != b) return (false, 0);

            return (true, c);

        }

    }



    /**

     * @dev Returns the division of two unsigned integers, with a division by zero flag.

     */

    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {

        unchecked {

            if (b == 0) return (false, 0);

            return (true, a / b);

        }

    }



    /**

     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.

     */

    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {

        unchecked {

            if (b == 0) return (false, 0);

            return (true, a % b);

        }

    }



    /**

     * @dev Returns the largest of two numbers.

     */

    function max(uint256 a, uint256 b) internal pure returns (uint256) {

        return a > b ? a : b;

    }



    /**

     * @dev Returns the smallest of two numbers.

     */

    function min(uint256 a, uint256 b) internal pure returns (uint256) {

        return a < b ? a : b;

    }



    /**

     * @dev Returns the average of two numbers. The result is rounded towards

     * zero.

     */

    function average(uint256 a, uint256 b) internal pure returns (uint256) {

        // (a + b) / 2 can overflow.

        return (a & b) + (a ^ b) / 2;

    }



    /**

     * @dev Returns the ceiling of the division of two numbers.

     *

     * This differs from standard division with `/` in that it rounds towards infinity instead

     * of rounding towards zero.

     */

    function ceilDiv(uint256 a, uint256 b) internal pure returns (uint256) {

        if (b == 0) {

            // Guarantee the same behavior as in a regular Solidity division.

            return a / b;

        }



        // (a + b - 1) / b can overflow on addition, so we distribute.

        return a == 0 ? 0 : (a - 1) / b + 1;

    }



    /**

     * @notice Calculates floor(x * y / denominator) with full precision. Throws if result overflows a uint256 or

     * denominator == 0.

     * @dev Original credit to Remco Bloemen under MIT license (https://xn--2-umb.com/21/muldiv) with further edits by

     * Uniswap Labs also under MIT license.

     */

    function mulDiv(uint256 x, uint256 y, uint256 denominator) internal pure returns (uint256 result) {

        unchecked {

            // 512-bit multiply [prod1 prod0] = x * y. Compute the product mod 2^256 and mod 2^256 - 1, then use

            // use the Chinese Remainder Theorem to reconstruct the 512 bit result. The result is stored in two 256

            // variables such that product = prod1 * 2^256 + prod0.

            uint256 prod0 = x * y; // Least significant 256 bits of the product

            uint256 prod1; // Most significant 256 bits of the product

            assembly {

                let mm := mulmod(x, y, not(0))

                prod1 := sub(sub(mm, prod0), lt(mm, prod0))

            }



            // Handle non-overflow cases, 256 by 256 division.

            if (prod1 == 0) {

                // Solidity will revert if denominator == 0, unlike the div opcode on its own.

                // The surrounding unchecked block does not change this fact.

                // See https://docs.soliditylang.org/en/latest/control-structures.html#checked-or-unchecked-arithmetic.

                return prod0 / denominator;

            }



            // Make sure the result is less than 2^256. Also prevents denominator == 0.

            if (denominator <= prod1) {

                revert MathOverflowedMulDiv();

            }



            ///////////////////////////////////////////////

            // 512 by 256 division.

            ///////////////////////////////////////////////



            // Make division exact by subtracting the remainder from [prod1 prod0].

            uint256 remainder;

            assembly {

                // Compute remainder using mulmod.

                remainder := mulmod(x, y, denominator)



                // Subtract 256 bit number from 512 bit number.

                prod1 := sub(prod1, gt(remainder, prod0))

                prod0 := sub(prod0, remainder)

            }



            // Factor powers of two out of denominator and compute largest power of two divisor of denominator.

            // Always >= 1. See https://cs.stackexchange.com/q/138556/92363.



            uint256 twos = denominator & (0 - denominator);

            assembly {

                // Divide denominator by twos.

                denominator := div(denominator, twos)



                // Divide [prod1 prod0] by twos.

                prod0 := div(prod0, twos)



                // Flip twos such that it is 2^256 / twos. If twos is zero, then it becomes one.

                twos := add(div(sub(0, twos), twos), 1)

            }



            // Shift in bits from prod1 into prod0.

            prod0 |= prod1 * twos;



            // Invert denominator mod 2^256. Now that denominator is an odd number, it has an inverse modulo 2^256 such

            // that denominator * inv = 1 mod 2^256. Compute the inverse by starting with a seed that is correct for

            // four bits. That is, denominator * inv = 1 mod 2^4.

            uint256 inverse = (3 * denominator) ^ 2;



            // Use the Newton-Raphson iteration to improve the precision. Thanks to Hensel's lifting lemma, this also

            // works in modular arithmetic, doubling the correct bits in each step.

            inverse *= 2 - denominator * inverse; // inverse mod 2^8

            inverse *= 2 - denominator * inverse; // inverse mod 2^16

            inverse *= 2 - denominator * inverse; // inverse mod 2^32

            inverse *= 2 - denominator * inverse; // inverse mod 2^64

            inverse *= 2 - denominator * inverse; // inverse mod 2^128

            inverse *= 2 - denominator * inverse; // inverse mod 2^256



            // Because the division is now exact we can divide by multiplying with the modular inverse of denominator.

            // This will give us the correct result modulo 2^256. Since the preconditions guarantee that the outcome is

            // less than 2^256, this is the final result. We don't need to compute the high bits of the result and prod1

            // is no longer required.

            result = prod0 * inverse;

            return result;

        }

    }



    /**

     * @notice Calculates x * y / denominator with full precision, following the selected rounding direction.

     */

    function mulDiv(uint256 x, uint256 y, uint256 denominator, Rounding rounding) internal pure returns (uint256) {

        uint256 result = mulDiv(x, y, denominator);

        if (unsignedRoundsUp(rounding) && mulmod(x, y, denominator) > 0) {

            result += 1;

        }

        return result;

    }



    /**

     * @dev Returns the square root of a number. If the number is not a perfect square, the value is rounded

     * towards zero.

     *

     * Inspired by Henry S. Warren, Jr.'s "Hacker's Delight" (Chapter 11).

     */

    function sqrt(uint256 a) internal pure returns (uint256) {

        if (a == 0) {

            return 0;

        }



        // For our first guess, we get the biggest power of 2 which is smaller than the square root of the target.

        //

        // We know that the "msb" (most significant bit) of our target number `a` is a power of 2 such that we have

        // `msb(a) <= a < 2*msb(a)`. This value can be written `msb(a)=2**k` with `k=log2(a)`.

        //

        // This can be rewritten `2**log2(a) <= a < 2**(log2(a) + 1)`

        // → `sqrt(2**k) <= sqrt(a) < sqrt(2**(k+1))`

        // → `2**(k/2) <= sqrt(a) < 2**((k+1)/2) <= 2**(k/2 + 1)`

        //

        // Consequently, `2**(log2(a) / 2)` is a good first approximation of `sqrt(a)` with at least 1 correct bit.

        uint256 result = 1 << (log2(a) >> 1);



        // At this point `result` is an estimation with one bit of precision. We know the true value is a uint128,

        // since it is the square root of a uint256. Newton's method converges quadratically (precision doubles at

        // every iteration). We thus need at most 7 iteration to turn our partial result with one bit of precision

        // into the expected uint128 result.

        unchecked {

            result = (result + a / result) >> 1;

            result = (result + a / result) >> 1;

            result = (result + a / result) >> 1;

            result = (result + a / result) >> 1;

            result = (result + a / result) >> 1;

            result = (result + a / result) >> 1;

            result = (result + a / result) >> 1;

            return min(result, a / result);

        }

    }



    /**

     * @notice Calculates sqrt(a), following the selected rounding direction.

     */

    function sqrt(uint256 a, Rounding rounding) internal pure returns (uint256) {

        unchecked {

            uint256 result = sqrt(a);

            return result + (unsignedRoundsUp(rounding) && result * result < a ? 1 : 0);

        }

    }



    /**

     * @dev Return the log in base 2 of a positive value rounded towards zero.

     * Returns 0 if given 0.

     */

    function log2(uint256 value) internal pure returns (uint256) {

        uint256 result = 0;

        unchecked {

            if (value >> 128 > 0) {

                value >>= 128;

                result += 128;

            }

            if (value >> 64 > 0) {

                value >>= 64;

                result += 64;

            }

            if (value >> 32 > 0) {

                value >>= 32;

                result += 32;

            }

            if (value >> 16 > 0) {

                value >>= 16;

                result += 16;

            }

            if (value >> 8 > 0) {

                value >>= 8;

                result += 8;

            }

            if (value >> 4 > 0) {

                value >>= 4;

                result += 4;

            }

            if (value >> 2 > 0) {

                value >>= 2;

                result += 2;

            }

            if (value >> 1 > 0) {

                result += 1;

            }

        }

        return result;

    }



    /**

     * @dev Return the log in base 2, following the selected rounding direction, of a positive value.

     * Returns 0 if given 0.

     */

    function log2(uint256 value, Rounding rounding) internal pure returns (uint256) {

        unchecked {

            uint256 result = log2(value);

            return result + (unsignedRoundsUp(rounding) && 1 << result < value ? 1 : 0);

        }

    }



    /**

     * @dev Return the log in base 10 of a positive value rounded towards zero.

     * Returns 0 if given 0.

     */

    function log10(uint256 value) internal pure returns (uint256) {

        uint256 result = 0;

        unchecked {

            if (value >= 10 ** 64) {

                value /= 10 ** 64;

                result += 64;

            }

            if (value >= 10 ** 32) {

                value /= 10 ** 32;

                result += 32;

            }

            if (value >= 10 ** 16) {

                value /= 10 ** 16;

                result += 16;

            }

            if (value >= 10 ** 8) {

                value /= 10 ** 8;

                result += 8;

            }

            if (value >= 10 ** 4) {

                value /= 10 ** 4;

                result += 4;

            }

            if (value >= 10 ** 2) {

                value /= 10 ** 2;

                result += 2;

            }

            if (value >= 10 ** 1) {

                result += 1;

            }

        }

        return result;

    }



    /**

     * @dev Return the log in base 10, following the selected rounding direction, of a positive value.

     * Returns 0 if given 0.

     */

    function log10(uint256 value, Rounding rounding) internal pure returns (uint256) {

        unchecked {

            uint256 result = log10(value);

            return result + (unsignedRoundsUp(rounding) && 10 ** result < value ? 1 : 0);

        }

    }



    /**

     * @dev Return the log in base 256 of a positive value rounded towards zero.

     * Returns 0 if given 0.

     *

     * Adding one to the result gives the number of pairs of hex symbols needed to represent `value` as a hex string.

     */

    function log256(uint256 value) internal pure returns (uint256) {

        uint256 result = 0;

        unchecked {

            if (value >> 128 > 0) {

                value >>= 128;

                result += 16;

            }

            if (value >> 64 > 0) {

                value >>= 64;

                result += 8;

            }

            if (value >> 32 > 0) {

                value >>= 32;

                result += 4;

            }

            if (value >> 16 > 0) {

                value >>= 16;

                result += 2;

            }

            if (value >> 8 > 0) {

                result += 1;

            }

        }

        return result;

    }



    /**

     * @dev Return the log in base 256, following the selected rounding direction, of a positive value.

     * Returns 0 if given 0.

     */

    function log256(uint256 value, Rounding rounding) internal pure returns (uint256) {

        unchecked {

            uint256 result = log256(value);

            return result + (unsignedRoundsUp(rounding) && 1 << (result << 3) < value ? 1 : 0);

        }

    }



    /**

     * @dev Returns whether a provided rounding mode is considered rounding up for unsigned integers.

     */

    function unsignedRoundsUp(Rounding rounding) internal pure returns (bool) {

        return uint8(rounding) % 2 == 1;

    }

}



// File: @openzeppelin/contracts/utils/Strings.sol





// OpenZeppelin Contracts (last updated v5.0.0) (utils/Strings.sol)



pragma solidity ^0.8.20;







/**

 * @dev String operations.

 */

library Strings {

    bytes16 private constant HEX_DIGITS = "0123456789abcdef";

    uint8 private constant ADDRESS_LENGTH = 20;



    /**

     * @dev The `value` string doesn't fit in the specified `length`.

     */

    error StringsInsufficientHexLength(uint256 value, uint256 length);



    /**

     * @dev Converts a `uint256` to its ASCII `string` decimal representation.

     */

    function toString(uint256 value) internal pure returns (string memory) {

        unchecked {

            uint256 length = Math.log10(value) + 1;

            string memory buffer = new string(length);

            uint256 ptr;

            /// @solidity memory-safe-assembly

            assembly {

                ptr := add(buffer, add(32, length))

            }

            while (true) {

                ptr--;

                /// @solidity memory-safe-assembly

                assembly {

                    mstore8(ptr, byte(mod(value, 10), HEX_DIGITS))

                }

                value /= 10;

                if (value == 0) break;

            }

            return buffer;

        }

    }



    /**

     * @dev Converts a `int256` to its ASCII `string` decimal representation.

     */

    function toStringSigned(int256 value) internal pure returns (string memory) {

        return string.concat(value < 0 ? "-" : "", toString(SignedMath.abs(value)));

    }



    /**

     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.

     */

    function toHexString(uint256 value) internal pure returns (string memory) {

        unchecked {

            return toHexString(value, Math.log256(value) + 1);

        }

    }



    /**

     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.

     */

    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {

        uint256 localValue = value;

        bytes memory buffer = new bytes(2 * length + 2);

        buffer[0] = "0";

        buffer[1] = "x";

        for (uint256 i = 2 * length + 1; i > 1; --i) {

            buffer[i] = HEX_DIGITS[localValue & 0xf];

            localValue >>= 4;

        }

        if (localValue != 0) {

            revert StringsInsufficientHexLength(value, length);

        }

        return string(buffer);

    }



    /**

     * @dev Converts an `address` with fixed length of 20 bytes to its not checksummed ASCII `string` hexadecimal

     * representation.

     */

    function toHexString(address addr) internal pure returns (string memory) {

        return toHexString(uint256(uint160(addr)), ADDRESS_LENGTH);

    }



    /**

     * @dev Returns true if the two strings are equal.

     */

    function equal(string memory a, string memory b) internal pure returns (bool) {

        return bytes(a).length == bytes(b).length && keccak256(bytes(a)) == keccak256(bytes(b));

    }

}



// File: erc721a/contracts/IERC721A.sol





// ERC721A Contracts v4.2.3

// Creator: Chiru Labs



pragma solidity ^0.8.4;



/**

 * @dev Interface of ERC721A.

 */

interface IERC721A {

    /**

     * The caller must own the token or be an approved operator.

     */

    error ApprovalCallerNotOwnerNorApproved();



    /**

     * The token does not exist.

     */

    error ApprovalQueryForNonexistentToken();



    /**

     * Cannot query the balance for the zero address.

     */

    error BalanceQueryForZeroAddress();



    /**

     * Cannot mint to the zero address.

     */

    error MintToZeroAddress();



    /**

     * The quantity of tokens minted must be more than zero.

     */

    error MintZeroQuantity();



    /**

     * The token does not exist.

     */

    error OwnerQueryForNonexistentToken();



    /**

     * The caller must own the token or be an approved operator.

     */

    error TransferCallerNotOwnerNorApproved();



    /**

     * The token must be owned by `from`.

     */

    error TransferFromIncorrectOwner();



    /**

     * Cannot safely transfer to a contract that does not implement the

     * ERC721Receiver interface.

     */

    error TransferToNonERC721ReceiverImplementer();



    /**

     * Cannot transfer to the zero address.

     */

    error TransferToZeroAddress();



    /**

     * The token does not exist.

     */

    error URIQueryForNonexistentToken();



    /**

     * The `quantity` minted with ERC2309 exceeds the safety limit.

     */

    error MintERC2309QuantityExceedsLimit();



    /**

     * The `extraData` cannot be set on an unintialized ownership slot.

     */

    error OwnershipNotInitializedForExtraData();



    // =============================================================

    //                            STRUCTS

    // =============================================================



    struct TokenOwnership {

        // The address of the owner.

        address addr;

        // Stores the start time of ownership with minimal overhead for tokenomics.

        uint64 startTimestamp;

        // Whether the token has been burned.

        bool burned;

        // Arbitrary data similar to `startTimestamp` that can be set via {_extraData}.

        uint24 extraData;

    }



    // =============================================================

    //                         TOKEN COUNTERS

    // =============================================================



    /**

     * @dev Returns the total number of tokens in existence.

     * Burned tokens will reduce the count.

     * To get the total number of tokens minted, please see {_totalMinted}.

     */

    function totalSupply() external view returns (uint256);



    // =============================================================

    //                            IERC165

    // =============================================================



    /**

     * @dev Returns true if this contract implements the interface defined by

     * `interfaceId`. See the corresponding

     * [EIP section](https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified)

     * to learn more about how these ids are created.

     *

     * This function call must use less than 30000 gas.

     */

    function supportsInterface(bytes4 interfaceId) external view returns (bool);



    // =============================================================

    //                            IERC721

    // =============================================================



    /**

     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.

     */

    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);



    /**

     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.

     */

    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);



    /**

     * @dev Emitted when `owner` enables or disables

     * (`approved`) `operator` to manage all of its assets.

     */

    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);



    /**

     * @dev Returns the number of tokens in `owner`'s account.

     */

    function balanceOf(address owner) external view returns (uint256 balance);



    /**

     * @dev Returns the owner of the `tokenId` token.

     *

     * Requirements:

     *

     * - `tokenId` must exist.

     */

    function ownerOf(uint256 tokenId) external view returns (address owner);



    /**

     * @dev Safely transfers `tokenId` token from `from` to `to`,

     * checking first that contract recipients are aware of the ERC721 protocol

     * to prevent tokens from being forever locked.

     *

     * Requirements:

     *

     * - `from` cannot be the zero address.

     * - `to` cannot be the zero address.

     * - `tokenId` token must exist and be owned by `from`.

     * - If the caller is not `from`, it must be have been allowed to move

     * this token by either {approve} or {setApprovalForAll}.

     * - If `to` refers to a smart contract, it must implement

     * {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.

     *

     * Emits a {Transfer} event.

     */

    function safeTransferFrom(

        address from,

        address to,

        uint256 tokenId,

        bytes calldata data

    ) external payable;



    /**

     * @dev Equivalent to `safeTransferFrom(from, to, tokenId, '')`.

     */

    function safeTransferFrom(

        address from,

        address to,

        uint256 tokenId

    ) external payable;



    /**

     * @dev Transfers `tokenId` from `from` to `to`.

     *

     * WARNING: Usage of this method is discouraged, use {safeTransferFrom}

     * whenever possible.

     *

     * Requirements:

     *

     * - `from` cannot be the zero address.

     * - `to` cannot be the zero address.

     * - `tokenId` token must be owned by `from`.

     * - If the caller is not `from`, it must be approved to move this token

     * by either {approve} or {setApprovalForAll}.

     *

     * Emits a {Transfer} event.

     */

    function transferFrom(

        address from,

        address to,

        uint256 tokenId

    ) external payable;



    /**

     * @dev Gives permission to `to` to transfer `tokenId` token to another account.

     * The approval is cleared when the token is transferred.

     *

     * Only a single account can be approved at a time, so approving the

     * zero address clears previous approvals.

     *

     * Requirements:

     *

     * - The caller must own the token or be an approved operator.

     * - `tokenId` must exist.

     *

     * Emits an {Approval} event.

     */

    function approve(address to, uint256 tokenId) external payable;



    /**

     * @dev Approve or remove `operator` as an operator for the caller.

     * Operators can call {transferFrom} or {safeTransferFrom}

     * for any token owned by the caller.

     *

     * Requirements:

     *

     * - The `operator` cannot be the caller.

     *

     * Emits an {ApprovalForAll} event.

     */

    function setApprovalForAll(address operator, bool _approved) external;



    /**

     * @dev Returns the account approved for `tokenId` token.

     *

     * Requirements:

     *

     * - `tokenId` must exist.

     */

    function getApproved(uint256 tokenId) external view returns (address operator);



    /**

     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.

     *

     * See {setApprovalForAll}.

     */

    function isApprovedForAll(address owner, address operator) external view returns (bool);



    // =============================================================

    //                        IERC721Metadata

    // =============================================================



    /**

     * @dev Returns the token collection name.

     */

    function name() external view returns (string memory);



    /**

     * @dev Returns the token collection symbol.

     */

    function symbol() external view returns (string memory);



    /**

     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.

     */

    function tokenURI(uint256 tokenId) external view returns (string memory);



    // =============================================================

    //                           IERC2309

    // =============================================================



    /**

     * @dev Emitted when tokens in `fromTokenId` to `toTokenId`

     * (inclusive) is transferred from `from` to `to`, as defined in the

     * [ERC2309](https://eips.ethereum.org/EIPS/eip-2309) standard.

     *

     * See {_mintERC2309} for more details.

     */

    event ConsecutiveTransfer(uint256 indexed fromTokenId, uint256 toTokenId, address indexed from, address indexed to);

}



// File: erc721a/contracts/ERC721A.sol





// ERC721A Contracts v4.2.3

// Creator: Chiru Labs



pragma solidity ^0.8.4;





/**

 * @dev Interface of ERC721 token receiver.

 */

interface ERC721A__IERC721Receiver {

    function onERC721Received(

        address operator,

        address from,

        uint256 tokenId,

        bytes calldata data

    ) external returns (bytes4);

}



/**

 * @title ERC721A

 *

 * @dev Implementation of the [ERC721](https://eips.ethereum.org/EIPS/eip-721)

 * Non-Fungible Token Standard, including the Metadata extension.

 * Optimized for lower gas during batch mints.

 *

 * Token IDs are minted in sequential order (e.g. 0, 1, 2, 3, ...)

 * starting from `_startTokenId()`.

 *

 * Assumptions:

 *

 * - An owner cannot have more than 2**64 - 1 (max value of uint64) of supply.

 * - The maximum token ID cannot exceed 2**256 - 1 (max value of uint256).

 */

contract ERC721A is IERC721A {

    // Bypass for a `--via-ir` bug (https://github.com/chiru-labs/ERC721A/pull/364).

    struct TokenApprovalRef {

        address value;

    }



    // =============================================================

    //                           CONSTANTS

    // =============================================================



    // Mask of an entry in packed address data.

    uint256 private constant _BITMASK_ADDRESS_DATA_ENTRY = (1 << 64) - 1;



    // The bit position of `numberMinted` in packed address data.

    uint256 private constant _BITPOS_NUMBER_MINTED = 64;



    // The bit position of `numberBurned` in packed address data.

    uint256 private constant _BITPOS_NUMBER_BURNED = 128;



    // The bit position of `aux` in packed address data.

    uint256 private constant _BITPOS_AUX = 192;



    // Mask of all 256 bits in packed address data except the 64 bits for `aux`.

    uint256 private constant _BITMASK_AUX_COMPLEMENT = (1 << 192) - 1;



    // The bit position of `startTimestamp` in packed ownership.

    uint256 private constant _BITPOS_START_TIMESTAMP = 160;



    // The bit mask of the `burned` bit in packed ownership.

    uint256 private constant _BITMASK_BURNED = 1 << 224;



    // The bit position of the `nextInitialized` bit in packed ownership.

    uint256 private constant _BITPOS_NEXT_INITIALIZED = 225;



    // The bit mask of the `nextInitialized` bit in packed ownership.

    uint256 private constant _BITMASK_NEXT_INITIALIZED = 1 << 225;



    // The bit position of `extraData` in packed ownership.

    uint256 private constant _BITPOS_EXTRA_DATA = 232;



    // Mask of all 256 bits in a packed ownership except the 24 bits for `extraData`.

    uint256 private constant _BITMASK_EXTRA_DATA_COMPLEMENT = (1 << 232) - 1;



    // The mask of the lower 160 bits for addresses.

    uint256 private constant _BITMASK_ADDRESS = (1 << 160) - 1;



    // The maximum `quantity` that can be minted with {_mintERC2309}.

    // This limit is to prevent overflows on the address data entries.

    // For a limit of 5000, a total of 3.689e15 calls to {_mintERC2309}

    // is required to cause an overflow, which is unrealistic.

    uint256 private constant _MAX_MINT_ERC2309_QUANTITY_LIMIT = 5000;



    // The `Transfer` event signature is given by:

    // `keccak256(bytes("Transfer(address,address,uint256)"))`.

    bytes32 private constant _TRANSFER_EVENT_SIGNATURE =

        0xddf252ad1be2c89b69c2b068fc378daa952ba7f163c4a11628f55a4df523b3ef;



    // =============================================================

    //                            STORAGE

    // =============================================================



    // The next token ID to be minted.

    uint256 private _currentIndex;



    // The number of tokens burned.

    uint256 private _burnCounter;



    // Token name

    string private _name;



    // Token symbol

    string private _symbol;



    // Mapping from token ID to ownership details

    // An empty struct value does not necessarily mean the token is unowned.

    // See {_packedOwnershipOf} implementation for details.

    //

    // Bits Layout:

    // - [0..159]   `addr`

    // - [160..223] `startTimestamp`

    // - [224]      `burned`

    // - [225]      `nextInitialized`

    // - [232..255] `extraData`

    mapping(uint256 => uint256) private _packedOwnerships;



    // Mapping owner address to address data.

    //

    // Bits Layout:

    // - [0..63]    `balance`

    // - [64..127]  `numberMinted`

    // - [128..191] `numberBurned`

    // - [192..255] `aux`

    mapping(address => uint256) private _packedAddressData;



    // Mapping from token ID to approved address.

    mapping(uint256 => TokenApprovalRef) private _tokenApprovals;



    // Mapping from owner to operator approvals

    mapping(address => mapping(address => bool)) private _operatorApprovals;



    // =============================================================

    //                          CONSTRUCTOR

    // =============================================================



    constructor(string memory name_, string memory symbol_) {

        _name = name_;

        _symbol = symbol_;

        _currentIndex = _startTokenId();

    }



    // =============================================================

    //                   TOKEN COUNTING OPERATIONS

    // =============================================================



    /**

     * @dev Returns the starting token ID.

     * To change the starting token ID, please override this function.

     */

    function _startTokenId() internal view virtual returns (uint256) {

        return 0;

    }



    /**

     * @dev Returns the next token ID to be minted.

     */

    function _nextTokenId() internal view virtual returns (uint256) {

        return _currentIndex;

    }



    /**

     * @dev Returns the total number of tokens in existence.

     * Burned tokens will reduce the count.

     * To get the total number of tokens minted, please see {_totalMinted}.

     */

    function totalSupply() public view virtual override returns (uint256) {

        // Counter underflow is impossible as _burnCounter cannot be incremented

        // more than `_currentIndex - _startTokenId()` times.

        unchecked {

            return _currentIndex - _burnCounter - _startTokenId();

        }

    }



    /**

     * @dev Returns the total amount of tokens minted in the contract.

     */

    function _totalMinted() internal view virtual returns (uint256) {

        // Counter underflow is impossible as `_currentIndex` does not decrement,

        // and it is initialized to `_startTokenId()`.

        unchecked {

            return _currentIndex - _startTokenId();

        }

    }



    /**

     * @dev Returns the total number of tokens burned.

     */

    function _totalBurned() internal view virtual returns (uint256) {

        return _burnCounter;

    }



    // =============================================================

    //                    ADDRESS DATA OPERATIONS

    // =============================================================



    /**

     * @dev Returns the number of tokens in `owner`'s account.

     */

    function balanceOf(address owner) public view virtual override returns (uint256) {

        if (owner == address(0)) revert BalanceQueryForZeroAddress();

        return _packedAddressData[owner] & _BITMASK_ADDRESS_DATA_ENTRY;

    }



    /**

     * Returns the number of tokens minted by `owner`.

     */

    function _numberMinted(address owner) internal view returns (uint256) {

        return (_packedAddressData[owner] >> _BITPOS_NUMBER_MINTED) & _BITMASK_ADDRESS_DATA_ENTRY;

    }



    /**

     * Returns the number of tokens burned by or on behalf of `owner`.

     */

    function _numberBurned(address owner) internal view returns (uint256) {

        return (_packedAddressData[owner] >> _BITPOS_NUMBER_BURNED) & _BITMASK_ADDRESS_DATA_ENTRY;

    }



    /**

     * Returns the auxiliary data for `owner`. (e.g. number of whitelist mint slots used).

     */

    function _getAux(address owner) internal view returns (uint64) {

        return uint64(_packedAddressData[owner] >> _BITPOS_AUX);

    }



    /**

     * Sets the auxiliary data for `owner`. (e.g. number of whitelist mint slots used).

     * If there are multiple variables, please pack them into a uint64.

     */

    function _setAux(address owner, uint64 aux) internal virtual {

        uint256 packed = _packedAddressData[owner];

        uint256 auxCasted;

        // Cast `aux` with assembly to avoid redundant masking.

        assembly {

            auxCasted := aux

        }

        packed = (packed & _BITMASK_AUX_COMPLEMENT) | (auxCasted << _BITPOS_AUX);

        _packedAddressData[owner] = packed;

    }



    // =============================================================

    //                            IERC165

    // =============================================================



    /**

     * @dev Returns true if this contract implements the interface defined by

     * `interfaceId`. See the corresponding

     * [EIP section](https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified)

     * to learn more about how these ids are created.

     *

     * This function call must use less than 30000 gas.

     */

    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {

        // The interface IDs are constants representing the first 4 bytes

        // of the XOR of all function selectors in the interface.

        // See: [ERC165](https://eips.ethereum.org/EIPS/eip-165)

        // (e.g. `bytes4(i.functionA.selector ^ i.functionB.selector ^ ...)`)

        return

            interfaceId == 0x01ffc9a7 || // ERC165 interface ID for ERC165.

            interfaceId == 0x80ac58cd || // ERC165 interface ID for ERC721.

            interfaceId == 0x5b5e139f; // ERC165 interface ID for ERC721Metadata.

    }



    // =============================================================

    //                        IERC721Metadata

    // =============================================================



    /**

     * @dev Returns the token collection name.

     */

    function name() public view virtual override returns (string memory) {

        return _name;

    }



    /**

     * @dev Returns the token collection symbol.

     */

    function symbol() public view virtual override returns (string memory) {

        return _symbol;

    }



    /**

     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.

     */

    function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {

        if (!_exists(tokenId)) revert URIQueryForNonexistentToken();



        string memory baseURI = _baseURI();

        return bytes(baseURI).length != 0 ? string(abi.encodePacked(baseURI, _toString(tokenId))) : '';

    }



    /**

     * @dev Base URI for computing {tokenURI}. If set, the resulting URI for each

     * token will be the concatenation of the `baseURI` and the `tokenId`. Empty

     * by default, it can be overridden in child contracts.

     */

    function _baseURI() internal view virtual returns (string memory) {

        return '';

    }



    // =============================================================

    //                     OWNERSHIPS OPERATIONS

    // =============================================================



    /**

     * @dev Returns the owner of the `tokenId` token.

     *

     * Requirements:

     *

     * - `tokenId` must exist.

     */

    function ownerOf(uint256 tokenId) public view virtual override returns (address) {

        return address(uint160(_packedOwnershipOf(tokenId)));

    }



    /**

     * @dev Gas spent here starts off proportional to the maximum mint batch size.

     * It gradually moves to O(1) as tokens get transferred around over time.

     */

    function _ownershipOf(uint256 tokenId) internal view virtual returns (TokenOwnership memory) {

        return _unpackedOwnership(_packedOwnershipOf(tokenId));

    }



    /**

     * @dev Returns the unpacked `TokenOwnership` struct at `index`.

     */

    function _ownershipAt(uint256 index) internal view virtual returns (TokenOwnership memory) {

        return _unpackedOwnership(_packedOwnerships[index]);

    }



    /**

     * @dev Initializes the ownership slot minted at `index` for efficiency purposes.

     */

    function _initializeOwnershipAt(uint256 index) internal virtual {

        if (_packedOwnerships[index] == 0) {

            _packedOwnerships[index] = _packedOwnershipOf(index);

        }

    }



    /**

     * Returns the packed ownership data of `tokenId`.

     */

    function _packedOwnershipOf(uint256 tokenId) private view returns (uint256) {

        uint256 curr = tokenId;



        unchecked {

            if (_startTokenId() <= curr)

                if (curr < _currentIndex) {

                    uint256 packed = _packedOwnerships[curr];

                    // If not burned.

                    if (packed & _BITMASK_BURNED == 0) {

                        // Invariant:

                        // There will always be an initialized ownership slot

                        // (i.e. `ownership.addr != address(0) && ownership.burned == false`)

                        // before an unintialized ownership slot

                        // (i.e. `ownership.addr == address(0) && ownership.burned == false`)

                        // Hence, `curr` will not underflow.

                        //

                        // We can directly compare the packed value.

                        // If the address is zero, packed will be zero.

                        while (packed == 0) {

                            packed = _packedOwnerships[--curr];

                        }

                        return packed;

                    }

                }

        }

        revert OwnerQueryForNonexistentToken();

    }



    /**

     * @dev Returns the unpacked `TokenOwnership` struct from `packed`.

     */

    function _unpackedOwnership(uint256 packed) private pure returns (TokenOwnership memory ownership) {

        ownership.addr = address(uint160(packed));

        ownership.startTimestamp = uint64(packed >> _BITPOS_START_TIMESTAMP);

        ownership.burned = packed & _BITMASK_BURNED != 0;

        ownership.extraData = uint24(packed >> _BITPOS_EXTRA_DATA);

    }



    /**

     * @dev Packs ownership data into a single uint256.

     */

    function _packOwnershipData(address owner, uint256 flags) private view returns (uint256 result) {

        assembly {

            // Mask `owner` to the lower 160 bits, in case the upper bits somehow aren't clean.

            owner := and(owner, _BITMASK_ADDRESS)

            // `owner | (block.timestamp << _BITPOS_START_TIMESTAMP) | flags`.

            result := or(owner, or(shl(_BITPOS_START_TIMESTAMP, timestamp()), flags))

        }

    }



    /**

     * @dev Returns the `nextInitialized` flag set if `quantity` equals 1.

     */

    function _nextInitializedFlag(uint256 quantity) private pure returns (uint256 result) {

        // For branchless setting of the `nextInitialized` flag.

        assembly {

            // `(quantity == 1) << _BITPOS_NEXT_INITIALIZED`.

            result := shl(_BITPOS_NEXT_INITIALIZED, eq(quantity, 1))

        }

    }



    // =============================================================

    //                      APPROVAL OPERATIONS

    // =============================================================



    /**

     * @dev Gives permission to `to` to transfer `tokenId` token to another account.

     * The approval is cleared when the token is transferred.

     *

     * Only a single account can be approved at a time, so approving the

     * zero address clears previous approvals.

     *

     * Requirements:

     *

     * - The caller must own the token or be an approved operator.

     * - `tokenId` must exist.

     *

     * Emits an {Approval} event.

     */

    function approve(address to, uint256 tokenId) public payable virtual override {

        address owner = ownerOf(tokenId);



        if (_msgSenderERC721A() != owner)

            if (!isApprovedForAll(owner, _msgSenderERC721A())) {

                revert ApprovalCallerNotOwnerNorApproved();

            }



        _tokenApprovals[tokenId].value = to;

        emit Approval(owner, to, tokenId);

    }



    /**

     * @dev Returns the account approved for `tokenId` token.

     *

     * Requirements:

     *

     * - `tokenId` must exist.

     */

    function getApproved(uint256 tokenId) public view virtual override returns (address) {

        if (!_exists(tokenId)) revert ApprovalQueryForNonexistentToken();



        return _tokenApprovals[tokenId].value;

    }



    /**

     * @dev Approve or remove `operator` as an operator for the caller.

     * Operators can call {transferFrom} or {safeTransferFrom}

     * for any token owned by the caller.

     *

     * Requirements:

     *

     * - The `operator` cannot be the caller.

     *

     * Emits an {ApprovalForAll} event.

     */

    function setApprovalForAll(address operator, bool approved) public virtual override {

        _operatorApprovals[_msgSenderERC721A()][operator] = approved;

        emit ApprovalForAll(_msgSenderERC721A(), operator, approved);

    }



    /**

     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.

     *

     * See {setApprovalForAll}.

     */

    function isApprovedForAll(address owner, address operator) public view virtual override returns (bool) {

        return _operatorApprovals[owner][operator];

    }



    /**

     * @dev Returns whether `tokenId` exists.

     *

     * Tokens can be managed by their owner or approved accounts via {approve} or {setApprovalForAll}.

     *

     * Tokens start existing when they are minted. See {_mint}.

     */

    function _exists(uint256 tokenId) internal view virtual returns (bool) {

        return

            _startTokenId() <= tokenId &&

            tokenId < _currentIndex && // If within bounds,

            _packedOwnerships[tokenId] & _BITMASK_BURNED == 0; // and not burned.

    }



    /**

     * @dev Returns whether `msgSender` is equal to `approvedAddress` or `owner`.

     */

    function _isSenderApprovedOrOwner(

        address approvedAddress,

        address owner,

        address msgSender

    ) private pure returns (bool result) {

        assembly {

            // Mask `owner` to the lower 160 bits, in case the upper bits somehow aren't clean.

            owner := and(owner, _BITMASK_ADDRESS)

            // Mask `msgSender` to the lower 160 bits, in case the upper bits somehow aren't clean.

            msgSender := and(msgSender, _BITMASK_ADDRESS)

            // `msgSender == owner || msgSender == approvedAddress`.

            result := or(eq(msgSender, owner), eq(msgSender, approvedAddress))

        }

    }



    /**

     * @dev Returns the storage slot and value for the approved address of `tokenId`.

     */

    function _getApprovedSlotAndAddress(uint256 tokenId)

        private

        view

        returns (uint256 approvedAddressSlot, address approvedAddress)

    {

        TokenApprovalRef storage tokenApproval = _tokenApprovals[tokenId];

        // The following is equivalent to `approvedAddress = _tokenApprovals[tokenId].value`.

        assembly {

            approvedAddressSlot := tokenApproval.slot

            approvedAddress := sload(approvedAddressSlot)

        }

    }



    // =============================================================

    //                      TRANSFER OPERATIONS

    // =============================================================



    /**

     * @dev Transfers `tokenId` from `from` to `to`.

     *

     * Requirements:

     *

     * - `from` cannot be the zero address.

     * - `to` cannot be the zero address.

     * - `tokenId` token must be owned by `from`.

     * - If the caller is not `from`, it must be approved to move this token

     * by either {approve} or {setApprovalForAll}.

     *

     * Emits a {Transfer} event.

     */

    function transferFrom(

        address from,

        address to,

        uint256 tokenId

    ) public payable virtual override {

        uint256 prevOwnershipPacked = _packedOwnershipOf(tokenId);



        if (address(uint160(prevOwnershipPacked)) != from) revert TransferFromIncorrectOwner();



        (uint256 approvedAddressSlot, address approvedAddress) = _getApprovedSlotAndAddress(tokenId);



        // The nested ifs save around 20+ gas over a compound boolean condition.

        if (!_isSenderApprovedOrOwner(approvedAddress, from, _msgSenderERC721A()))

            if (!isApprovedForAll(from, _msgSenderERC721A())) revert TransferCallerNotOwnerNorApproved();



        if (to == address(0)) revert TransferToZeroAddress();



        _beforeTokenTransfers(from, to, tokenId, 1);



        // Clear approvals from the previous owner.

        assembly {

            if approvedAddress {

                // This is equivalent to `delete _tokenApprovals[tokenId]`.

                sstore(approvedAddressSlot, 0)

            }

        }



        // Underflow of the sender's balance is impossible because we check for

        // ownership above and the recipient's balance can't realistically overflow.

        // Counter overflow is incredibly unrealistic as `tokenId` would have to be 2**256.

        unchecked {

            // We can directly increment and decrement the balances.

            --_packedAddressData[from]; // Updates: `balance -= 1`.

            ++_packedAddressData[to]; // Updates: `balance += 1`.



            // Updates:

            // - `address` to the next owner.

            // - `startTimestamp` to the timestamp of transfering.

            // - `burned` to `false`.

            // - `nextInitialized` to `true`.

            _packedOwnerships[tokenId] = _packOwnershipData(

                to,

                _BITMASK_NEXT_INITIALIZED | _nextExtraData(from, to, prevOwnershipPacked)

            );



            // If the next slot may not have been initialized (i.e. `nextInitialized == false`) .

            if (prevOwnershipPacked & _BITMASK_NEXT_INITIALIZED == 0) {

                uint256 nextTokenId = tokenId + 1;

                // If the next slot's address is zero and not burned (i.e. packed value is zero).

                if (_packedOwnerships[nextTokenId] == 0) {

                    // If the next slot is within bounds.

                    if (nextTokenId != _currentIndex) {

                        // Initialize the next slot to maintain correctness for `ownerOf(tokenId + 1)`.

                        _packedOwnerships[nextTokenId] = prevOwnershipPacked;

                    }

                }

            }

        }



        emit Transfer(from, to, tokenId);

        _afterTokenTransfers(from, to, tokenId, 1);

    }



    /**

     * @dev Equivalent to `safeTransferFrom(from, to, tokenId, '')`.

     */

    function safeTransferFrom(

        address from,

        address to,

        uint256 tokenId

    ) public payable virtual override {

        safeTransferFrom(from, to, tokenId, '');

    }



    /**

     * @dev Safely transfers `tokenId` token from `from` to `to`.

     *

     * Requirements:

     *

     * - `from` cannot be the zero address.

     * - `to` cannot be the zero address.

     * - `tokenId` token must exist and be owned by `from`.

     * - If the caller is not `from`, it must be approved to move this token

     * by either {approve} or {setApprovalForAll}.

     * - If `to` refers to a smart contract, it must implement

     * {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.

     *

     * Emits a {Transfer} event.

     */

    function safeTransferFrom(

        address from,

        address to,

        uint256 tokenId,

        bytes memory _data

    ) public payable virtual override {

        transferFrom(from, to, tokenId);

        if (to.code.length != 0)

            if (!_checkContractOnERC721Received(from, to, tokenId, _data)) {

                revert TransferToNonERC721ReceiverImplementer();

            }

    }



    /**

     * @dev Hook that is called before a set of serially-ordered token IDs

     * are about to be transferred. This includes minting.

     * And also called before burning one token.

     *

     * `startTokenId` - the first token ID to be transferred.

     * `quantity` - the amount to be transferred.

     *

     * Calling conditions:

     *

     * - When `from` and `to` are both non-zero, `from`'s `tokenId` will be

     * transferred to `to`.

     * - When `from` is zero, `tokenId` will be minted for `to`.

     * - When `to` is zero, `tokenId` will be burned by `from`.

     * - `from` and `to` are never both zero.

     */

    function _beforeTokenTransfers(

        address from,

        address to,

        uint256 startTokenId,

        uint256 quantity

    ) internal virtual {}



    /**

     * @dev Hook that is called after a set of serially-ordered token IDs

     * have been transferred. This includes minting.

     * And also called after one token has been burned.

     *

     * `startTokenId` - the first token ID to be transferred.

     * `quantity` - the amount to be transferred.

     *

     * Calling conditions:

     *

     * - When `from` and `to` are both non-zero, `from`'s `tokenId` has been

     * transferred to `to`.

     * - When `from` is zero, `tokenId` has been minted for `to`.

     * - When `to` is zero, `tokenId` has been burned by `from`.

     * - `from` and `to` are never both zero.

     */

    function _afterTokenTransfers(

        address from,

        address to,

        uint256 startTokenId,

        uint256 quantity

    ) internal virtual {}



    /**

     * @dev Private function to invoke {IERC721Receiver-onERC721Received} on a target contract.

     *

     * `from` - Previous owner of the given token ID.

     * `to` - Target address that will receive the token.

     * `tokenId` - Token ID to be transferred.

     * `_data` - Optional data to send along with the call.

     *

     * Returns whether the call correctly returned the expected magic value.

     */

    function _checkContractOnERC721Received(

        address from,

        address to,

        uint256 tokenId,

        bytes memory _data

    ) private returns (bool) {

        try ERC721A__IERC721Receiver(to).onERC721Received(_msgSenderERC721A(), from, tokenId, _data) returns (

            bytes4 retval

        ) {

            return retval == ERC721A__IERC721Receiver(to).onERC721Received.selector;

        } catch (bytes memory reason) {

            if (reason.length == 0) {

                revert TransferToNonERC721ReceiverImplementer();

            } else {

                assembly {

                    revert(add(32, reason), mload(reason))

                }

            }

        }

    }



    // =============================================================

    //                        MINT OPERATIONS

    // =============================================================



    /**

     * @dev Mints `quantity` tokens and transfers them to `to`.

     *

     * Requirements:

     *

     * - `to` cannot be the zero address.

     * - `quantity` must be greater than 0.

     *

     * Emits a {Transfer} event for each mint.

     */

    function _mint(address to, uint256 quantity) internal virtual {

        uint256 startTokenId = _currentIndex;

        if (quantity == 0) revert MintZeroQuantity();



        _beforeTokenTransfers(address(0), to, startTokenId, quantity);



        // Overflows are incredibly unrealistic.

        // `balance` and `numberMinted` have a maximum limit of 2**64.

        // `tokenId` has a maximum limit of 2**256.

        unchecked {

            // Updates:

            // - `balance += quantity`.

            // - `numberMinted += quantity`.

            //

            // We can directly add to the `balance` and `numberMinted`.

            _packedAddressData[to] += quantity * ((1 << _BITPOS_NUMBER_MINTED) | 1);



            // Updates:

            // - `address` to the owner.

            // - `startTimestamp` to the timestamp of minting.

            // - `burned` to `false`.

            // - `nextInitialized` to `quantity == 1`.

            _packedOwnerships[startTokenId] = _packOwnershipData(

                to,

                _nextInitializedFlag(quantity) | _nextExtraData(address(0), to, 0)

            );



            uint256 toMasked;

            uint256 end = startTokenId + quantity;



            // Use assembly to loop and emit the `Transfer` event for gas savings.

            // The duplicated `log4` removes an extra check and reduces stack juggling.

            // The assembly, together with the surrounding Solidity code, have been

            // delicately arranged to nudge the compiler into producing optimized opcodes.

            assembly {

                // Mask `to` to the lower 160 bits, in case the upper bits somehow aren't clean.

                toMasked := and(to, _BITMASK_ADDRESS)

                // Emit the `Transfer` event.

                log4(

                    0, // Start of data (0, since no data).

                    0, // End of data (0, since no data).

                    _TRANSFER_EVENT_SIGNATURE, // Signature.

                    0, // `address(0)`.

                    toMasked, // `to`.

                    startTokenId // `tokenId`.

                )



                // The `iszero(eq(,))` check ensures that large values of `quantity`

                // that overflows uint256 will make the loop run out of gas.

                // The compiler will optimize the `iszero` away for performance.

                for {

                    let tokenId := add(startTokenId, 1)

                } iszero(eq(tokenId, end)) {

                    tokenId := add(tokenId, 1)

                } {

                    // Emit the `Transfer` event. Similar to above.

                    log4(0, 0, _TRANSFER_EVENT_SIGNATURE, 0, toMasked, tokenId)

                }

            }

            if (toMasked == 0) revert MintToZeroAddress();



            _currentIndex = end;

        }

        _afterTokenTransfers(address(0), to, startTokenId, quantity);

    }



    /**

     * @dev Mints `quantity` tokens and transfers them to `to`.

     *

     * This function is intended for efficient minting only during contract creation.

     *

     * It emits only one {ConsecutiveTransfer} as defined in

     * [ERC2309](https://eips.ethereum.org/EIPS/eip-2309),

     * instead of a sequence of {Transfer} event(s).

     *

     * Calling this function outside of contract creation WILL make your contract

     * non-compliant with the ERC721 standard.

     * For full ERC721 compliance, substituting ERC721 {Transfer} event(s) with the ERC2309

     * {ConsecutiveTransfer} event is only permissible during contract creation.

     *

     * Requirements:

     *

     * - `to` cannot be the zero address.

     * - `quantity` must be greater than 0.

     *

     * Emits a {ConsecutiveTransfer} event.

     */

    function _mintERC2309(address to, uint256 quantity) internal virtual {

        uint256 startTokenId = _currentIndex;

        if (to == address(0)) revert MintToZeroAddress();

        if (quantity == 0) revert MintZeroQuantity();

        if (quantity > _MAX_MINT_ERC2309_QUANTITY_LIMIT) revert MintERC2309QuantityExceedsLimit();



        _beforeTokenTransfers(address(0), to, startTokenId, quantity);



        // Overflows are unrealistic due to the above check for `quantity` to be below the limit.

        unchecked {

            // Updates:

            // - `balance += quantity`.

            // - `numberMinted += quantity`.

            //

            // We can directly add to the `balance` and `numberMinted`.

            _packedAddressData[to] += quantity * ((1 << _BITPOS_NUMBER_MINTED) | 1);



            // Updates:

            // - `address` to the owner.

            // - `startTimestamp` to the timestamp of minting.

            // - `burned` to `false`.

            // - `nextInitialized` to `quantity == 1`.

            _packedOwnerships[startTokenId] = _packOwnershipData(

                to,

                _nextInitializedFlag(quantity) | _nextExtraData(address(0), to, 0)

            );



            emit ConsecutiveTransfer(startTokenId, startTokenId + quantity - 1, address(0), to);



            _currentIndex = startTokenId + quantity;

        }

        _afterTokenTransfers(address(0), to, startTokenId, quantity);

    }



    /**

     * @dev Safely mints `quantity` tokens and transfers them to `to`.

     *

     * Requirements:

     *

     * - If `to` refers to a smart contract, it must implement

     * {IERC721Receiver-onERC721Received}, which is called for each safe transfer.

     * - `quantity` must be greater than 0.

     *

     * See {_mint}.

     *

     * Emits a {Transfer} event for each mint.

     */

    function _safeMint(

        address to,

        uint256 quantity,

        bytes memory _data

    ) internal virtual {

        _mint(to, quantity);



        unchecked {

            if (to.code.length != 0) {

                uint256 end = _currentIndex;

                uint256 index = end - quantity;

                do {

                    if (!_checkContractOnERC721Received(address(0), to, index++, _data)) {

                        revert TransferToNonERC721ReceiverImplementer();

                    }

                } while (index < end);

                // Reentrancy protection.

                if (_currentIndex != end) revert();

            }

        }

    }



    /**

     * @dev Equivalent to `_safeMint(to, quantity, '')`.

     */

    function _safeMint(address to, uint256 quantity) internal virtual {

        _safeMint(to, quantity, '');

    }



    // =============================================================

    //                        BURN OPERATIONS

    // =============================================================



    /**

     * @dev Equivalent to `_burn(tokenId, false)`.

     */

    function _burn(uint256 tokenId) internal virtual {

        _burn(tokenId, false);

    }



    /**

     * @dev Destroys `tokenId`.

     * The approval is cleared when the token is burned.

     *

     * Requirements:

     *

     * - `tokenId` must exist.

     *

     * Emits a {Transfer} event.

     */

    function _burn(uint256 tokenId, bool approvalCheck) internal virtual {

        uint256 prevOwnershipPacked = _packedOwnershipOf(tokenId);



        address from = address(uint160(prevOwnershipPacked));



        (uint256 approvedAddressSlot, address approvedAddress) = _getApprovedSlotAndAddress(tokenId);



        if (approvalCheck) {

            // The nested ifs save around 20+ gas over a compound boolean condition.

            if (!_isSenderApprovedOrOwner(approvedAddress, from, _msgSenderERC721A()))

                if (!isApprovedForAll(from, _msgSenderERC721A())) revert TransferCallerNotOwnerNorApproved();

        }



        _beforeTokenTransfers(from, address(0), tokenId, 1);



        // Clear approvals from the previous owner.

        assembly {

            if approvedAddress {

                // This is equivalent to `delete _tokenApprovals[tokenId]`.

                sstore(approvedAddressSlot, 0)

            }

        }



        // Underflow of the sender's balance is impossible because we check for

        // ownership above and the recipient's balance can't realistically overflow.

        // Counter overflow is incredibly unrealistic as `tokenId` would have to be 2**256.

        unchecked {

            // Updates:

            // - `balance -= 1`.

            // - `numberBurned += 1`.

            //

            // We can directly decrement the balance, and increment the number burned.

            // This is equivalent to `packed -= 1; packed += 1 << _BITPOS_NUMBER_BURNED;`.

            _packedAddressData[from] += (1 << _BITPOS_NUMBER_BURNED) - 1;



            // Updates:

            // - `address` to the last owner.

            // - `startTimestamp` to the timestamp of burning.

            // - `burned` to `true`.

            // - `nextInitialized` to `true`.

            _packedOwnerships[tokenId] = _packOwnershipData(

                from,

                (_BITMASK_BURNED | _BITMASK_NEXT_INITIALIZED) | _nextExtraData(from, address(0), prevOwnershipPacked)

            );



            // If the next slot may not have been initialized (i.e. `nextInitialized == false`) .

            if (prevOwnershipPacked & _BITMASK_NEXT_INITIALIZED == 0) {

                uint256 nextTokenId = tokenId + 1;

                // If the next slot's address is zero and not burned (i.e. packed value is zero).

                if (_packedOwnerships[nextTokenId] == 0) {

                    // If the next slot is within bounds.

                    if (nextTokenId != _currentIndex) {

                        // Initialize the next slot to maintain correctness for `ownerOf(tokenId + 1)`.

                        _packedOwnerships[nextTokenId] = prevOwnershipPacked;

                    }

                }

            }

        }



        emit Transfer(from, address(0), tokenId);

        _afterTokenTransfers(from, address(0), tokenId, 1);



        // Overflow not possible, as _burnCounter cannot be exceed _currentIndex times.

        unchecked {

            _burnCounter++;

        }

    }



    // =============================================================

    //                     EXTRA DATA OPERATIONS

    // =============================================================



    /**

     * @dev Directly sets the extra data for the ownership data `index`.

     */

    function _setExtraDataAt(uint256 index, uint24 extraData) internal virtual {

        uint256 packed = _packedOwnerships[index];

        if (packed == 0) revert OwnershipNotInitializedForExtraData();

        uint256 extraDataCasted;

        // Cast `extraData` with assembly to avoid redundant masking.

        assembly {

            extraDataCasted := extraData

        }

        packed = (packed & _BITMASK_EXTRA_DATA_COMPLEMENT) | (extraDataCasted << _BITPOS_EXTRA_DATA);

        _packedOwnerships[index] = packed;

    }



    /**

     * @dev Called during each token transfer to set the 24bit `extraData` field.

     * Intended to be overridden by the cosumer contract.

     *

     * `previousExtraData` - the value of `extraData` before transfer.

     *

     * Calling conditions:

     *

     * - When `from` and `to` are both non-zero, `from`'s `tokenId` will be

     * transferred to `to`.

     * - When `from` is zero, `tokenId` will be minted for `to`.

     * - When `to` is zero, `tokenId` will be burned by `from`.

     * - `from` and `to` are never both zero.

     */

    function _extraData(

        address from,

        address to,

        uint24 previousExtraData

    ) internal view virtual returns (uint24) {}



    /**

     * @dev Returns the next extra data for the packed ownership data.

     * The returned result is shifted into position.

     */

    function _nextExtraData(

        address from,

        address to,

        uint256 prevOwnershipPacked

    ) private view returns (uint256) {

        uint24 extraData = uint24(prevOwnershipPacked >> _BITPOS_EXTRA_DATA);

        return uint256(_extraData(from, to, extraData)) << _BITPOS_EXTRA_DATA;

    }



    // =============================================================

    //                       OTHER OPERATIONS

    // =============================================================



    /**

     * @dev Returns the message sender (defaults to `msg.sender`).

     *

     * If you are writing GSN compatible contracts, you need to override this function.

     */

    function _msgSenderERC721A() internal view virtual returns (address) {

        return msg.sender;

    }



    /**

     * @dev Converts a uint256 to its ASCII string decimal representation.

     */

    function _toString(uint256 value) internal pure virtual returns (string memory str) {

        assembly {

            // The maximum value of a uint256 contains 78 digits (1 byte per digit), but

            // we allocate 0xa0 bytes to keep the free memory pointer 32-byte word aligned.

            // We will need 1 word for the trailing zeros padding, 1 word for the length,

            // and 3 words for a maximum of 78 digits. Total: 5 * 0x20 = 0xa0.

            let m := add(mload(0x40), 0xa0)

            // Update the free memory pointer to allocate.

            mstore(0x40, m)

            // Assign the `str` to the end.

            str := sub(m, 0x20)

            // Zeroize the slot after the string.

            mstore(str, 0)



            // Cache the end of the memory to calculate the length later.

            let end := str



            // We write the string from rightmost digit to leftmost digit.

            // The following is essentially a do-while loop that also handles the zero case.

            // prettier-ignore

            for { let temp := value } 1 {} {

                str := sub(str, 1)

                // Write the character to the pointer.

                // The ASCII index of the '0' character is 48.

                mstore8(str, add(48, mod(temp, 10)))

                // Keep dividing `temp` until zero.

                temp := div(temp, 10)

                // prettier-ignore

                if iszero(temp) { break }

            }



            let length := sub(end, str)

            // Move the pointer 32 bytes leftwards to make room for the length.

            str := sub(str, 0x20)

            // Store the length.

            mstore(str, length)

        }

    }

}



// File: contracts/SnakeTest2.sol





pragma solidity ^0.8.18;



library ParameterSupplier{

    function getBaitPosString(uint256[] memory baitParameters) external pure returns(string memory)

{

    return string(abi.encodePacked

    (

        '<circle cx=', '"',Strings.toString(baitParameters[1]),'"', " ",'cy=', '"',Strings.toString(baitParameters[2]),'"', " ",'r=', '"',Strings.toString(baitParameters[4]), '"', " ", 'display="inline" fill="white"> <animate begin=', '"',getDecimalNum(baitParameters[0]),'"', " ",'attributeName="display" values="none" dur="infinite" repeatCount="1" /> </circle>'

        '<rect x="6" y="616" width="608" height="68" style= "stroke:black; stroke-width: 4;; fill: none"/>'

        '<rect x="10" y="620" width="600" height="60" style= "stroke:white; stroke-width: 4;; fill: black"/>'

        '<text x="60" y="666" textLength="200" style="fill:white; font-size:48px; font-family:Arial; ">Score:</text>'

        '<text x="350" y="666" textLength="220" style="fill:white; font-size:48px; font-family:Arial;">', '000', getScoreString(baitParameters[3]) ,

        '<set attributeName="visibility" begin=', '"',getDecimalNum(baitParameters[0]),'"', " ", 'dur="infinite" to="hidden"/>  </text>'

        '<text x="350" y="666" textLength="220" visibility="hidden" style="fill:white; font-size:48px; font-family:Arial;">', '000', getScoreString(baitParameters[3] + (baitParameters[4] * 3)) ,

        '<set attributeName="visibility" begin=', '"',getDecimalNum(baitParameters[0]),'"', " ", 'dur="infinite" to="visible"/> </text>'

        '<text x="150" y="330" textLength="300" visibility="hidden" style="fill:white; font-size:48px; font-family:Arial Black;">GAME OVER<set attributeName="visibility" begin=', '"',getDecimalNum(baitParameters[5]),'"', " ", 'dur="infinite" to="visible"/> </text>'

    ));

}



function generateAttributes(uint256 tokenId) external pure returns (string memory)

{



    string memory attributes = string( abi.encodePacked(

            '{"trait_type":"Total Score", "value":',

            Strings.toString((randHeight(tokenId) * 2) + (randBait(tokenId) * 3)),

            "},",

            '{"trait_type":"Length", "value":',

            Strings.toString(randHeight(tokenId)),

            "},",

            '{"trait_type":"Venomosity", "value":',

            Strings.toString(randTox(tokenId)),

            "},",

            '{"trait_type":"Bait Size", "value":',

            Strings.toString(randBait(tokenId)),

            "},",

            '{"trait_type":"Race ", "value":"',

            randTrait(tokenId, 1),

            '"},',

            '{"trait_type":"Snake Scale ", "value":"',

            randTrait(tokenId, 0),

            '"}'

        )

    );



    return attributes;

}



function randTrait(uint256 _tokenId, uint8 trait) internal pure returns(string memory)

{



    uint256 capValue = uint256(keccak256(abi.encodePacked("bait", _tokenId))) % 5;



    if(capValue == 0)

    {

        if(trait == 1){

              return "Eunectes";

        }else{return "Scute";}

        

    }else if(capValue  == 1)

    {

        if(trait == 1){

         return "Naja";

        }else{return "Ventral ";}

    }else if(capValue  == 2)

    {

        if(trait == 1){

         return "Viperidae";

        }else{return "Dorsal";}

    }else if(capValue  == 3)

    {

        if(trait == 1){

         return "Boa Constrictor";

        }else{return "Supralabial";}

    }else if(capValue  == 4)

    {

        if(trait == 1){

         return "Dendroaspis";

        }else{return "Infralabial";}

    }else

    {

        if(trait == 1){

         return "Hydrophiinae";

        }else{return "Subcaudal";}

    }

} 



function randHeight(uint256 _tokenId) internal pure returns(uint256 height)

{

    uint32 nonce = 1;

    uint256 capValue = uint256(keccak256(abi.encodePacked("height", _tokenId))) % 1000;

    uint256 retVal = 0;



    if(capValue < 500)

    {

        return uint256(keccak256(abi.encodePacked("height", _tokenId + nonce))) % 30;

    }else if(capValue < 800)

    {

        retVal = uint256(keccak256(abi.encodePacked("height", _tokenId + nonce))) % 30;

        return retVal + 30;

    }else if(capValue < 990)

    {

        retVal = uint256(keccak256(abi.encodePacked("height", _tokenId + nonce))) % 30;

        return retVal + 60;

    }else if(capValue < 1000)

    {

        retVal = uint256(keccak256(abi.encodePacked("height", _tokenId + nonce))) % 10;

        return retVal + 90;

    }

}





function randBait(uint256 _tokenId) internal pure returns(uint256 bait)

{

    uint32 nonce = 1;

    uint256 capValue = uint256(keccak256(abi.encodePacked("bait", _tokenId))) % 87;

    uint256 retVal = 0;



    if(capValue < 44)

    {

        return uint256(keccak256(abi.encodePacked("bait", _tokenId + nonce))) % 4;

    }else if(capValue < 80)

    {

        retVal = uint256(keccak256(abi.encodePacked("bait", _tokenId + nonce))) % 4;

        return retVal + 4;

    }else if(capValue < 87)

    {

        retVal = uint256(keccak256(abi.encodePacked("bait", _tokenId + nonce))) % 2;

        return retVal + 8;

    }

} 



function randTox(uint256 _tokenId) internal pure returns(uint256 tox)

{

    uint32 nonce = 1;

    uint256 capValue = uint256(keccak256(abi.encodePacked("tox", _tokenId))) % 87;

    uint256 retVal = 0;



    if(capValue < 44)

    {

        return uint256(keccak256(abi.encodePacked("tox", _tokenId + nonce))) % 4;

    }else if(capValue < 80)

    {

        retVal = uint256(keccak256(abi.encodePacked("tox", _tokenId + nonce))) % 4;

        return retVal + 4;

    }else if(capValue < 87)

    {

        retVal = uint256(keccak256(abi.encodePacked("tox", _tokenId + nonce))) % 2;

        return retVal + 8;

    }

} 





function getScoreString(uint256 score) internal pure returns(string memory)

{

    if(score < 10)

    {

        return string(abi.encodePacked

        (

            '00', Strings.toString(score)

        ));

    }else if(score < 100)

    {

        return string(abi.encodePacked

        (

            '0', Strings.toString(score)

        ));



    }else

    {

        return string(abi.encodePacked

        (

            Strings.toString(score)

        ));

    }



}







function getDecimalNum(uint256 number) internal pure returns(string memory)

{

    return string(abi.encodePacked

    (

        Strings.toString(number / 100),".", getSecondTwoNum(number)

    ));

}

function getSecondTwoNum(uint256 number) internal pure returns(string memory)

{

    uint256 retValue = number % 100;

    if(retValue < 10)

    {

        return string(abi.encodePacked('0', Strings.toString(retValue)));



    }else {

        return string(abi.encodePacked(Strings.toString(retValue)));

    }

}

}











contract Venomaze is ERC721A, Ownable {

uint256 public maxPerTx = 10;

uint256 public maxFree = 1;

uint256 public maxSupply = 2555;

uint256 public price = .0033 ether;

bool public paused = true;



mapping(address => uint256) private _totalFreeMint;



uint32 rectX1 = 20;

uint32 rectY1 = 20;

uint32 rectWidth = 600;

uint32 rectHeighth = 600;





constructor() ERC721A("Venomaze", "VNM") Ownable(msg.sender) {}



function mint(uint256 _numTokens) external payable {

    require(!paused, "Paused");



    uint256 _price = (msg.value == 0 &&

        (_totalFreeMint[msg.sender] + _numTokens <= maxFree))

        ? 0

        : price;



    require(_numTokens <= maxPerTx, "Max transaction exceed.");

    require(

        (totalSupply() + _numTokens) <= maxSupply,

        "Max supply exceed."

    );

    require(msg.value >= (_price * _numTokens), "Wrong price.");



    if (_price == 0) {

        _totalFreeMint[msg.sender] += _numTokens;

    }



    _safeMint(msg.sender, _numTokens);

    }

    



function randMod(uint256 _modulus, uint256 _tokenId, string memory randomizer) internal pure returns(uint256)

{

    return uint256(keccak256(abi.encodePacked(randomizer, _tokenId))) % _modulus;

} 













struct SnakeParametersFirstPart

{

    uint256 snakeLength;

    bool toRight;

    uint256[] durations;

    uint256[] baitPos;

    uint256[] snakeSpawnPos;

    uint256[] pathToFirstTarget;

    SnakeParametersSecondPart secondPart;

}



struct SnakeParametersSecondPart

{

    bool toUp;

    uint256[] secondDurations;

    uint256[] snakeSecondSpawnPos;

    uint256[] pathToSecondTarget;

    uint256[] baitDisTime;

}



function GenerateSnakeParameters(uint256 _tokenId) internal view returns (SnakeParametersFirstPart memory) 

{

    uint256 ranSnakeLength = ParameterSupplier.randHeight(_tokenId);



    if(ranSnakeLength<15) {ranSnakeLength=15;}



    uint256 x1 = randMod(rectWidth, _tokenId, "second");



    if(x1 < rectX1){ x1 += rectX1; }



    if(x1 > (rectWidth - rectX1)){ x1 -= rectX1; }



    bool toRight = x1 < (rectWidth/2);

    uint256 x2;



    if(toRight)

    {

        x2 = x1 + (ranSnakeLength * 2);

    }

    else

    {

        x2 = x1 - (ranSnakeLength * 2);



    }



    uint256 y1 = randMod(rectWidth, _tokenId, "third");



    if(y1 < rectY1){ y1 += rectY1; }



    if(y1 > (rectHeighth - rectY1)){ y1 -= rectY1; }



    uint256[] memory snakeSpawnPos = new uint256[](4);



    snakeSpawnPos[0] = x1;

    snakeSpawnPos[1] = y1;

    snakeSpawnPos[2] = x2;

    snakeSpawnPos[3] = y1;

    uint256 baitPosCX;



    if(toRight)

    {

        baitPosCX = randMod(x2, _tokenId, "fourth") + x2;

        if(baitPosCX < 318) {baitPosCX = 320;}

        else if(baitPosCX > 578) {baitPosCX = 575;}

    }else

    {

        baitPosCX = randMod(x2, _tokenId, "fifth");

        if(baitPosCX < 22) {baitPosCX = 25;}

        else if(baitPosCX > 282) {baitPosCX -= 20;}

    }

    uint256 baitPosCY = randMod(rectWidth - 210, _tokenId, "sixth");

    if(baitPosCY < 210) {baitPosCY = 215;}



    uint256[] memory baitPos = new uint256[](2);

    baitPos[0] = baitPosCX;

    baitPos[1] = baitPosCY;

    //bait



    uint256[] memory pathToFirstTarget = new uint256[](4);





    if(toRight) 

    {

        pathToFirstTarget[0] = baitPosCX- x2;

        pathToFirstTarget[1] = baitPosCX + 5;

        pathToFirstTarget[3] = baitPosCX + 5;

        pathToFirstTarget[2] = x1 + pathToFirstTarget[0];

    }

    else 

    {

        pathToFirstTarget[0] = x2 - baitPosCX;

        pathToFirstTarget[1] = baitPosCX - 5;

        pathToFirstTarget[3] = baitPosCX - 5;

        pathToFirstTarget[2] = x1 - pathToFirstTarget[0];

    } 







    



    uint256[] memory durations = new uint256[](3);



    durations[0] = pathToFirstTarget[0];

    durations[1] = get_durations(toRight, x1, x2); // checks snake disappear duration according to direction

    durations[2] = pathToFirstTarget[0] + durations[1];





    return 

    SnakeParametersFirstPart

    (

        ranSnakeLength,

        toRight,

        durations,

        baitPos,

        snakeSpawnPos,

        pathToFirstTarget,

        GenerateSecondParameters(_tokenId, ranSnakeLength, y1, baitPosCX, baitPosCY, pathToFirstTarget[0], durations[1])

    );

}



function GenerateSecondParameters(uint256 _tokenId, uint256 snakeLen,uint256 spawnPosY1, uint256 baitPosCX, uint256 baitPosCY, uint256 beginDur, uint256 disappearDur) internal pure returns (SnakeParametersSecondPart memory) 

{

    uint256 secondSpawnPosY1 = spawnPosY1;

    uint256 secondSpawnPosY2;

    bool isUp = secondSpawnPosY1 > baitPosCY;

    if(isUp)

    {

        secondSpawnPosY2 = spawnPosY1 - disappearDur;

    }else

    {

        secondSpawnPosY2 = spawnPosY1 + disappearDur;

    }



    uint256[] memory secondDurations = new uint256[](5);



    secondDurations[0] = beginDur;

    secondDurations[1] = disappearDur;

    secondDurations[2] = beginDur + disappearDur;



    uint256[] memory secondSpawnPos = new uint256[](4);



    secondSpawnPos[0] = baitPosCX;

    secondSpawnPos[1] = secondSpawnPosY1;

    secondSpawnPos[2] = baitPosCX;

    secondSpawnPos[3] = secondSpawnPosY2;

    

    uint256[] memory pathToSecondTarget = new uint256[](3);



    pathToSecondTarget[0] = secondSpawnPosY1;

    pathToSecondTarget[1] = secondSpawnPosY2;



    uint256[] memory baitDisTime = new uint256[](6);

    if(isUp)

    {

        secondDurations[3] = secondSpawnPosY2 - 10;

        pathToSecondTarget[2] = secondSpawnPosY2 - 10;

        baitDisTime[0] = (secondSpawnPosY1 - baitPosCY) +  beginDur;

        //pathToSecondTarget[2] = (secondSpawnPosY1 - baitPosCY) + (snakeLen * 2);

    }else

    {

        secondDurations[3] = 610 - secondSpawnPosY2;

        pathToSecondTarget[2] = 610 - secondSpawnPosY2;

        baitDisTime[0] = (baitPosCY - secondSpawnPosY1) +  beginDur;

        

        //pathToSecondTarget[2] = (baitPosCY - secondSpawnPosY1) + (snakeLen * 2);

    }

    baitDisTime[1] = baitPosCX;

    baitDisTime[2] = baitPosCY;

    baitDisTime[3] = snakeLen * 2;

    baitDisTime[4] = ParameterSupplier.randBait(_tokenId);

    if(baitDisTime[4] < 1){baitDisTime[4] = 1;}



    secondDurations[4] = secondDurations[3] + secondDurations[2]; //dis

    baitDisTime[5] =secondDurations[3] + secondDurations[2];



    return 

    SnakeParametersSecondPart

    (

        isUp,

        secondDurations,

        secondSpawnPos,

        pathToSecondTarget,

        baitDisTime

    );

}





function get_durations(bool _toRight, uint256 x1, uint256 x2) internal pure returns (uint256) {

    if(_toRight){

        return x2 - x1;

    }else{ return x1 - x2;}

}



function getFirstNum(uint256 number) internal pure returns(uint256)

{

    return number / 100;

}



function getDecimalNum(uint256 number) internal pure returns(string memory)

{

    return string(abi.encodePacked

    (

        Strings.toString(number / 100),".", getSecondTwoNum(number)

    ));

}



function getSecondTwoNum(uint256 number) internal pure returns(string memory)

{

    uint256 retValue = number % 100;

    if(retValue < 10)

    {

        return string(abi.encodePacked('0', Strings.toString(retValue)));



    }else {

        return string(abi.encodePacked(Strings.toString(retValue)));

    }

}



function createFirstSnakeString(uint256 tokenId) internal view returns (string memory)

{

    SnakeParametersFirstPart memory parameters = GenerateSnakeParameters(tokenId);

    bool toRight = parameters.toRight;

    uint256[] memory durations = parameters.durations;

    uint256[] memory snakeSpawnPos = parameters.snakeSpawnPos;

    uint256[] memory pathToFirstTarget = parameters.pathToFirstTarget;





    

    return

    string(abi.encodePacked

    (

        '<line x1=', getSpawnPosString(snakeSpawnPos),

        'display="inline" style="stroke:white; stroke-width: 10; stroke-dasharray: 10 2; ">',

        '<animateMotion dur=', '"', getDecimalNum(durations[0]), '"',

        " ",'repeatCount="1"', " ", getPathString(toRight, pathToFirstTarget[0]), 

        '<animate begin=', '"',getDecimalNum(durations[0]), '"', " ", 'attributeName="x2" values=', '"',Strings.toString(pathToFirstTarget[1]),'"', " ",'repeatCount="1" />',

        '<animate begin=', '"',getDecimalNum(durations[0]), '"', " ", 'attributeName="x1" values=', '"',Strings.toString(pathToFirstTarget[2]),";", Strings.toString(pathToFirstTarget[3]),'"', " ",'dur=', '"', getDecimalNum(durations[1]),'"', " ",'repeatCount="1" />',

        '<animate begin=', '"',getDecimalNum(durations[2]), '"', " ",'attributeName="display" values="none" dur="infinite" repeatCount="1" /> </line>',

        createSecondSnakeString(parameters.secondPart)

    ));

}



function createSecondSnakeString(SnakeParametersSecondPart memory snakeSecond) internal pure returns (string memory)

{

    return

    string(abi.encodePacked

    (

        '<line x1=', getSpawnPosString(snakeSecond.snakeSecondSpawnPos),

        'display="none" style="stroke:white; stroke-width: 10; stroke-dasharray: 10 2">',

        '<animate begin=', '"',getDecimalNum(snakeSecond.secondDurations[0]),'"', " ", 'attributeName="display" values="inline" dur="infinite" repeatCount="1" />'

        '<animate begin=', '"',getDecimalNum(snakeSecond.secondDurations[0]),'"', " ", 'attributeName="y2" values=', '"',Strings.toString(snakeSecond.pathToSecondTarget[0]),";", Strings.toString(snakeSecond.pathToSecondTarget[1]),'"', " ", 'dur=', '"', getDecimalNum(snakeSecond.secondDurations[1]),'"', " ", 'repeatCount="1" />',

        '<animateMotion begin=', '"',getDecimalNum(snakeSecond.secondDurations[2]),'"', " ", 'dur=', '"', getDecimalNum(snakeSecond.secondDurations[3]),'"', " ", 'repeatCount="1" ', getPathHeightString(snakeSecond.toUp, snakeSecond.pathToSecondTarget[2]),' />',

        '<animate begin=', '"',getDecimalNum(snakeSecond.secondDurations[4]),'"', " ", 'attributeName="display" values="none" dur="infinite" repeatCount="1" /> </line> ',

        ParameterSupplier.getBaitPosString(snakeSecond.baitDisTime)



    ));

}



function getPathString(bool toRight, uint256 pathToTarget) internal pure returns(string memory)

{

    if(!toRight)

    {

        return string(abi.encodePacked

        (

            'path="M0,0',

             ' -', Strings.toString(pathToTarget),

             ', 0" />'

        ));

    }else

    {

        return string(abi.encodePacked

        (

            'path="M0,0',

             ' ', Strings.toString(pathToTarget),

             ', 0" />'

        ));



    }

}



function getPathHeightString(bool toUp, uint256 pathToTarget) internal pure returns(string memory)

{

    if(toUp)

    {

        return string(abi.encodePacked

        (

            'path="M0,0 0',

             ' -', Strings.toString(pathToTarget),

             '," />'

        ));

    }else

    {

        return string(abi.encodePacked

        (

            'path="M0,0 0',

             ' ', Strings.toString(pathToTarget),

             '," />'

        ));



    }

}



function getSpawnPosString(uint256[] memory snakeSpawnPos) internal pure returns(string memory)

{

    return string(abi.encodePacked

    (

        '"',Strings.toString(snakeSpawnPos[0]), '" ' ,

        'y1=', '"',Strings.toString(snakeSpawnPos[1]), '" ' ,

        'x2=', '"',Strings.toString(snakeSpawnPos[2]), '" ' ,

        'y2=', '"',Strings.toString(snakeSpawnPos[3]), '" ' 

      

    ));

}









function getScoreString(uint256 score) internal pure returns(string memory)

{

    if(score < 10)

    {

        return string(abi.encodePacked

        (

            '00', Strings.toString(score)

        ));

    }else if(score < 100)

    {

        return string(abi.encodePacked

        (

            '0', Strings.toString(score)

        ));



    }else

    {

        return string(abi.encodePacked

        (

            Strings.toString(score)

        ));

    }



}





function printSVG(uint256 tokenId) public view returns (string memory)

{

            

    return string(

        abi.encodePacked(

            '<svg width="620" height="700" xmlns="http://www.w3.org/2000/svg"> '

            '<rect x="6" y="6" width="608" height="608" '

            'style="stroke:black; stroke-width: 4; fill: none"/> '

            '<rect x="10" y="10" width="600" height="600" '

            'style="stroke:white; stroke-width: 4; fill: black"/> ',

            createFirstSnakeString(tokenId), "</svg>"

        )

    );



}







function tokenURI(uint256 tokenId) public view override returns (string memory)

{

    require(

        _exists(tokenId),

        "ERC721Metadata: URI query for nonexistent token"

    );

    string memory svg = printSVG(tokenId);

    string memory attributes = ParameterSupplier.generateAttributes(tokenId);



    string memory json = Base64.encode(

        bytes(

            string(

                abi.encodePacked(

                    '{"name": "Venomaze #',

                    Strings.toString(tokenId),

                    '", "description": "Most iconic retro game, now in the Solidity.", "attributes":[',

                    attributes,

                    '], "image": "data:image/svg+xml;base64,',

                    Base64.encode(bytes(svg)),

                    '"}'

                )

            )

        )

    );



    return string(abi.encodePacked("data:application/json;base64,", json));

}



    function _startTokenId() internal view virtual override returns (uint256) {

        return 1;

    }



    function setPaused() external onlyOwner {

        paused = !paused;

    }



    function setMaxPerTx(uint256 _maxPerTx) external onlyOwner {

        maxPerTx = _maxPerTx;

    }



    function airdrop(address _to, uint256 _amount) external onlyOwner {

        require(totalSupply() + _amount < maxSupply, "Beyond max supply");

        _mint(_to, _amount);

    }



    function withdraw() external onlyOwner {

        (bool success, ) = msg.sender.call{value: address(this).balance}("");

        require(success, "Transfer failed");

    }

}