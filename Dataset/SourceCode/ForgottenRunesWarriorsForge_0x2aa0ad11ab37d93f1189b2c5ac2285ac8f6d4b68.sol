// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMKKMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNXccNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNo''cONMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNo',ldKMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWKk:;0MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMNNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMXOccXMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNNMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMKKMx:0MMMMMMMMMMMMMMMMMMMMMMMMMMMMXOccXMMMMMMMMMMMMMMMMMMMMMMMMMMMM0:xNOKMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMKkXM0lxx,,OMMMMMMMMMMMMMMMMMMMMMMMMMMMXOccXMMMMMMMMMMMMMMMMMMMMMMMMMMXd,,xl;0MXXMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMNKd,;:,.';''l0WMMMMMMMMMMMMMMMMMMMMMMMMXOccXMMMMMMMMMMMMMMMMMMMMMMMMW0;.,:,;l:,,xXNMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMXkOx;...  ..'l0KK0NMMMMMMMMMMMMMMMMMMMMXOccXMMMMMMMMMMMMMMMMMMMMN0000l...  ...;xooXMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMNOx0o;:,'.   .cl,....;dodkkXMMMMMMMMMMMMN0xo;;0XNMMMMMMMMMMMMXkdlod:....,lc.   .;;:lk0xONMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMWO'..,,.   ..lo............'oKMMMMMMMMMXl.....;lx0XMMMMMMMMKo'...........;kl..    ',..'OWMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMM0kKkc'.      .;:..............:kkkkkO0Od;.........l0OOkkkkkk:..............:;.      ':lkK0KMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMXko'...      .'..'.........  .:xo,,;'.ll..........cOl.','.:d:.. ..........','.     ..,''lONMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMNkoo,..        ...........  'dclOo:dx;...........cdl:x0c';'  ...........        ..,ookNMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMWNKkc:'          ...;,.....';',c,.:kl............,ok:'cc,';'.....,;'..          ':ckKNWMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMWXOddddo,.      ..';:l;....',ccc;.'..   ......  ..'.';cc,'....;l:;...      .;oddddOXWMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMNKOc.  .,,,ckl.  ,,.. .....;cld;...  .     ..:dl;...... .';,  .lkc,,,.  .cOKNMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMWWKkkkkKWMMMO,  ..... ',......cc.............cc......,' .. ... ,OMMMWKkkkkKWWMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMKc. .' ''  ...'',,cd:... ..... ...:dc''''...     '. .cKMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMX; ...    ..  ..;,;;....  ...  ....;; .... .. .. .'. :XMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMd,c.     ',..  ..'l,...  ...  ...,l'    ..,' ,'  ,,.dMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNNXc.'.  ''''    .',,..  ...  .,:,'...  ,''. ,c''l0KNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNXWx.'o...  ':..,xo'........cx,..;.  ,'.o'cWWXWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMW0KNooo,cOx' 'xXk'......;c:' 'xOc,oooNKKMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWWMWNl. .,cOWKkkkOk0Xx:,. .dNWWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMO'  ',,xKWMMMMMN0x,,'  'OMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMO'  ..ck0NWNWNXXOx;..  'OMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMO'   'O0dkO0XXkkxkx'   'OMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMO'   ;Oo,:clxOl::kK;   'OMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMO,  .kx''''.,;,,'cX0,  'OMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMNc  :Kx.''..''',..oMk..dNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMK,:NO;''.',,''''.'kNccNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMkdo'.''..','.....'d0KMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMXl..'.'..,,.... ..cKWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMk'...'...,,,'... ...lWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWc ...'...,,,' .,... lMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMk.    ....',,'.....  .,kMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMK,    ..   .,...  ..    ,KMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMK,     .    ..  .   .     ,KMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMK,     .    ..   ..  ..     lMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMl     ..    ..   ..   ..    .kMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMK,     ..    ..   ..   ..     .kMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMK,     ..     ..    .    .      lMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMl      .      ..    ..   ..     lMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMK,..   ..      .     ..    .   ..,KMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWWl ll  ..      ..      .        lK;lMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMXXl,Kl  .;.     ..      ..   ..  lMllNKNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWWl;O; .kMl     ..      .... lK, ,k,lWKXMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWNOl';'lMK; .,...       .,.  lMl'c.'ONNWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWNN0Kl;Kl ,KMd. .,,.  :XMl  lK,lMK0NWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWNWk..;.lMMl .kMMk. lMMk..,. lWNWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMKkKllMk. lMMMMl lMMd lKl,xXXMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWNNNWl.,...lMMMMl .ll.,KWWNNWWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWWW0dddKk;lkko':kxddONWWWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWNNNNO;.'l0XNNNNWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWWNKKXWWWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
// MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//
// Forgotten Runes Warriors Forge
// https://forgottenrunes.com
// Twitter: @forgottenrunes
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.12;

import "../interfaces/IBookOfLore.sol";

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC1155/IERC1155.sol";
import "@openzeppelin/contracts/utils/cryptography/MerkleProof.sol";
import "@openzeppelin/contracts/utils/Strings.sol";

interface IERC1155Burnable is IERC1155 {
    function burn(
        address account,
        uint256 id,
        uint256 value
    ) external;
}

contract ForgottenRunesWarriorsForge is Ownable {
    uint256 public startTimestamp = type(uint256).max;

    address public warriorsAddress;
    bytes32 public warriorsMerkleRoot;

    address public sacredFlameAddress;
    uint256 public sacredFlameTokenId = 0;

    address public bookOfLoreAddress;

    mapping(address => bytes32) public burnableToMerkleRoot;

    mapping(uint256 => bool) public warriorIdToForgedStatus;
    mapping(uint256 => mapping(address => uint256))
        public warriorIdToUsedBurnable;

    event WarriorWeaponForged(
        uint256 indexed warriorId,
        address burnableAddress,
        uint256 burnableTokenId
    );

    constructor(
        address _sacredFlameAddress,
        address _warriorsAddress,
        bytes32 _warriorsMerkleRoot,
        address _bookOfLoreAddress
    ) {
        setSacredFlameAddress(_sacredFlameAddress);
        setWarriorsAddress(_warriorsAddress);
        setWarriorsMerkleRoot(_warriorsMerkleRoot);
        setBookOfLoreAddress(_bookOfLoreAddress);
    }

    function forgeWeapon(
        address _burnableAddress,
        uint256 _burnableTokenId,
        bytes32[] calldata _burnableMerkleProof,
        uint256 _warriorTokenId,
        bytes32[] calldata _warriorsMerkleProof
    ) external {
        require(started(), "Blacksmith is not ready");

        require(
            IERC721(_burnableAddress).ownerOf(_burnableTokenId) == _msgSender(),
            "Not owner of burnable token"
        );

        bytes32 burnableMerkleRoot = burnableToMerkleRoot[_burnableAddress];

        require(
            burnableMerkleRoot != bytes32(0x0),
            "Burnable contract not supported"
        );

        if (burnableMerkleRoot != "Allow any") {
            // Need to verify that token ID supports melting
            require(
                MerkleProof.verify(
                    _burnableMerkleProof,
                    burnableMerkleRoot,
                    keccak256(abi.encodePacked(_burnableTokenId))
                ),
                "Token can't be burnt"
            );
        }

        require(
            IERC721(warriorsAddress).ownerOf(_warriorTokenId) == _msgSender(),
            "Not owner of warrior"
        );

        require(
            MerkleProof.verify(
                _warriorsMerkleProof,
                warriorsMerkleRoot,
                keccak256(abi.encodePacked(_warriorTokenId))
            ),
            "This warrior can't be forged"
        );

        require(
            IERC1155(sacredFlameAddress).balanceOf(
                _msgSender(),
                sacredFlameTokenId
            ) > 0,
            "No flames"
        );

        require(!warriorIdToForgedStatus[_warriorTokenId], "Already forged");

        warriorIdToForgedStatus[_warriorTokenId] = true;
        warriorIdToUsedBurnable[_warriorTokenId][
            _burnableAddress
        ] = _burnableTokenId;

        // burn the burnable (doing transfer as burn not implemented on some contracts :/)
        IERC721(_burnableAddress).transferFrom(
            _msgSender(),
            0x000000000000000000000000000000000000dEaD,
            _burnableTokenId
        );

        // "burn" the flame
        IERC1155Burnable(sacredFlameAddress).burn(
            _msgSender(),
            sacredFlameTokenId,
            1
        );

        // write the Lore
        if (bookOfLoreAddress != address(0)) {
            IBookOfLore(bookOfLoreAddress).addLoreWithScribe(
                warriorsAddress,
                _warriorTokenId,
                0,
                false,
                string.concat(
                    Strings.toHexString(_burnableAddress),
                    ";",
                    Strings.toString(_burnableTokenId)
                )
            );
        }

        emit WarriorWeaponForged(
            _warriorTokenId,
            _burnableAddress,
            _burnableTokenId
        );
    }

    function started() public view returns (bool) {
        return block.timestamp > startTimestamp;
    }

    function setBurnableMerkleRoot(
        address _burnableAddress,
        bytes32 _merkleRoot
    ) external onlyOwner {
        burnableToMerkleRoot[_burnableAddress] = _merkleRoot;
    }

    function setWarriorsMerkleRoot(bytes32 _warriorsMerkleRoot)
        public
        onlyOwner
    {
        warriorsMerkleRoot = _warriorsMerkleRoot;
    }

    function setWarriorsAddress(address _warriorsAddress) public onlyOwner {
        warriorsAddress = _warriorsAddress;
    }

    function setSacredFlameAddress(address _sacredFlameAddress)
        public
        onlyOwner
    {
        sacredFlameAddress = _sacredFlameAddress;
    }

    function setSacredFlameTokenId(uint256 _sacredFlameTokenId)
        public
        onlyOwner
    {
        sacredFlameTokenId = _sacredFlameTokenId;
    }

    function setStartTimestamp(uint256 _startTimestamp) public onlyOwner {
        startTimestamp = _startTimestamp;
    }

    function setBookOfLoreAddress(address _bookOfLoreAddress) public onlyOwner {
        bookOfLoreAddress = _bookOfLoreAddress;
    }

    // emergency exit functions
    function withdrawAll() public payable onlyOwner {
        require(payable(_msgSender()).send(address(this).balance));
    }

    function recoverERC721(
        address _nft,
        address _destination,
        uint256 _tokenId
    ) external onlyOwner {
        IERC721(_nft).safeTransferFrom(address(this), _destination, _tokenId);
    }

    function recoverERC20(address _token, address _destination)
        external
        onlyOwner
    {
        uint256 amount = IERC20(_token).balanceOf(address(this));
        IERC20(_token).transferFrom(address(this), _destination, amount);
    }
}