/**

 *Submitted for verification at Etherscan.io on 2023-12-09

*/



// __        __                       _        ____                      _

// \ \      / /__ _ __   ___ __ _ ___| |__    / ___| ___ _ __   ___  ___(_)___

//  \ \ /\ / / _ \ '_ \ / __/ _` / __| '_ \  | |  _ / _ \ '_ \ / _ \/ __| / __|

//   \ V  V /  __/ | | | (_| (_| \__ \ | | | | |_| |  __/ | | |  __/\__ \ \__ \

//    \_/\_/ \___|_| |_|\___\__,_|___/_| |_|  \____|\___|_| |_|\___||___/_|___/

//  ____                       _ _      ____            _                  _

// |  _ \  ___ _ __   ___  ___(_) |_   / ___|___  _ __ | |_ _ __ __ _  ___| |_

// | | | |/ _ \ '_ \ / _ \/ __| | __| | |   / _ \| '_ \| __| '__/ _` |/ __| __|

// | |_| |  __/ |_) | (_) \__ \ | |_  | |__| (_) | | | | |_| | | (_| | (__| |_

// |____/ \___| .__/ \___/|___/_|\__|  \____\___/|_| |_|\__|_|  \__,_|\___|\__|

//            |_|



// SPDX-License-Identifier: CC0-1.0

pragma solidity 0.8.22;



// Based on official specification in https://eips.ethereum.org/EIPS/eip-165

interface IERC165 {

    /// @notice Query if a contract implements an interface

    /// @param interfaceId The interface identifier, as specified in ERC-165

    /// @dev Interface identification is specified in ERC-165. This function

    ///  uses less than 30,000 gas.

    /// @return `true` if the contract implements `interfaceId` and

    ///  `interfaceId` is not 0xffffffff, `false` otherwise

    function supportsInterface(bytes4 interfaceId) external pure returns (bool);

}



interface IDepositContract {

    /**

     * @notice New WEN deposit made

     * @dev Emitted when an address made a deposit of 32 WEN to become a genesis validator on Wencash

     * @param pubkey the public key of the genesis validator

     * @param withdrawal_credentials the withdrawal credentials of the genesis validator

     * @param amount the amount of WEN deposited

     * @param signature the BLS signature of the genesis validator

     * @param index the deposit number for this deposit

     */

    event DepositEvent(

        bytes pubkey,

        bytes withdrawal_credentials,

        bytes amount,

        bytes signature,

        bytes index

    );



    /// @notice Submit a Phase 0 DepositData object.

    /// @param pubkey A BLS12-381 public key.

    /// @param withdrawal_credentials Commitment to a public key for withdrawals.

    /// @param signature A BLS12-381 signature.

    /// @param deposit_data_root The SHA-256 hash of the SSZ-encoded DepositData object.

    /// Used as a protection against malformed input.

    function deposit(

        bytes memory pubkey,

        bytes memory withdrawal_credentials,

        bytes memory signature,

        bytes32 deposit_data_root,

        uint256 stake_amount

    ) external;



    /// @notice Query the current deposit root hash.

    /// @return The deposit root hash.

    function get_deposit_root() external view returns (bytes32);



    /// @notice Query the current deposit count.

    /// @return The deposit count encoded as a little endian 64-bit number.

    function get_deposit_count() external view returns (bytes memory);

}



/**

 * @dev Interface of the ERC20 standard as defined in the EIP.

 */

interface IERC20 {

    /**

     * @dev Returns the amount of tokens in existence.

     */

    function totalSupply() external view returns (uint256);



    /**

     * @dev Returns the amount of tokens owned by `account`.

     */

    function balanceOf(address account) external view returns (uint256);



    /**

     * @dev Moves `amount` tokens from the caller's account to `recipient`.

     *

     * Returns a boolean value indicating whether the operation succeeded.

     *

     * Emits a {Transfer} event.

     */

    function transfer(

        address recipient,

        uint256 amount

    ) external returns (bool);



    /**

     * @dev Returns the remaining number of tokens that `spender` will be

     * allowed to spend on behalf of `owner` through {transferFrom}. This is

     * zero by default.

     *

     * This value changes when {approve} or {transferFrom} are called.

     */

    function allowance(

        address owner,

        address spender

    ) external view returns (uint256);



    /**

     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.

     *

     * Returns a boolean value indicating whether the operation succeeded.

     *

     * IMPORTANT: Beware that changing an allowance with this method brings the risk

     * that someone may use both the old and the new allowance by unfortunate

     * transaction ordering. One possible solution to mitigate this race

     * condition is to first reduce the spender's allowance to 0 and set the

     * desired value afterwards:

     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729

     *

     * Emits an {Approval} event.

     */

    function approve(address spender, uint256 amount) external returns (bool);



    /**

     * @dev Moves `amount` tokens from `sender` to `recipient` using the

     * allowance mechanism. `amount` is then deducted from the caller's

     * allowance.

     *

     * Returns a boolean value indicating whether the operation succeeded.

     *

     * Emits a {Transfer} event.

     */

    function transferFrom(

        address sender,

        address recipient,

        uint256 amount

    ) external returns (bool);



    /**

     * @dev Emitted when `value` tokens are moved from one account (`from`) to

     * another (`to`).

     *

     * Note that `value` may be zero.

     */

    event Transfer(address indexed from, address indexed to, uint256 value);



    /**

     * @dev Emitted when the allowance of a `spender` for an `owner` is set by

     * a call to {approve}. `value` is the new allowance.

     */

    event Approval(

        address indexed owner,

        address indexed spender,

        uint256 value

    );

}



/**

 * @title Wencash Genesis Validators Deposit Contract

 *

 * @notice This contract allows anyone to register as Genesis Validators for the Wencash Blockchain.

 * To become a Genesis Validator, a participant must send the deposit data:

 * public key, withdrawal credentials, signature, amount, deposit data root.

 *

 * This smart contract allows deposits until block #18_909_332 (Monday Jan 01 2024).

 * After this delay, this contract is locked, it will be only used as a historical reference and all WEN in it will be forever locked.

 * But the WEN deposit will be withdrawable on the Wencash Blockchain after its launch.

 *

 * The `genesis.szz` for the Wencash Consensus Layer will be generated out of this smart contract using the `get_deposit_root()` function and

 * Genesis Validators will have their WEN balance on the Wencash Consensus Layer after the network start.

 *

 * @dev The Wencash Genesis Validators Deposit Contract is deployed on the Ethereum network.

 * Once the contract is locked, no more deposits can be made.

 */

contract WencashGenesisDepositContract is IDepositContract, IERC165 {

    uint256 private constant DEPOSIT_CONTRACT_TREE_DEPTH = 32;

    // NOTE: this also ensures `deposit_count` will fit into 64-bits

    uint256 private constant MAX_DEPOSIT_COUNT = 2 ** DEPOSIT_CONTRACT_TREE_DEPTH - 1;



    bytes32[DEPOSIT_CONTRACT_TREE_DEPTH] private zero_hashes;



    bytes32[DEPOSIT_CONTRACT_TREE_DEPTH] private branch;

    uint256 private deposit_count;



    // The block number when the contract will be locked (Monday Jan 01 2024)

    uint256 public constant LOCK_BLOCK_NUMBER = 18_909_332;



    IERC20 public immutable stake_token;



    constructor() {

        // input set to the address of WEN ERC20 token contract.

        stake_token = IERC20(0xEBA6145367b33e9FB683358E0421E8b7337D435f);

    }



    function get_deposit_root() external view override returns (bytes32) {

        bytes32 node;

        uint256 size = deposit_count;

        for (uint256 height = 0; height < DEPOSIT_CONTRACT_TREE_DEPTH; height++) {

            if ((size & 1) == 1) {

                node = sha256(abi.encodePacked(branch[height], node));

            } else {

                node = sha256(abi.encodePacked(node, zero_hashes[height]));

            }

            size /= 2;

        }

        return sha256(abi.encodePacked(node, to_little_endian_64(uint64(deposit_count)), bytes24(0)));

    }



    function get_deposit_count() external view override returns (bytes memory) {

        return to_little_endian_64(uint64(deposit_count));

    }



    function deposit(

        bytes memory pubkey,

        bytes memory withdrawal_credentials,

        bytes memory signature,

        bytes32 deposit_data_root,

        uint256 stake_amount

    ) external override {

        stake_token.transferFrom(msg.sender, address(this), stake_amount);

        _deposit(pubkey, withdrawal_credentials, signature, deposit_data_root, stake_amount);

    }



    function batchDeposit(

        bytes calldata pubkeys,

        bytes calldata withdrawal_credentials,

        bytes calldata signatures,

        bytes32[] calldata deposit_data_roots

    ) external {

        uint256 count = deposit_data_roots.length;

        require(count > 0, "BatchDeposit: You should deposit at least one validator");

        require(count <= 128, "BatchDeposit: You can deposit max 128 validators at a time");



        require(pubkeys.length == count * 48, "BatchDeposit: Pubkey count don't match");

        require(signatures.length == count * 96, "BatchDeposit: Signatures count don't match");

        require(withdrawal_credentials.length == 32, "BatchDeposit: Withdrawal Credentials count don't match");



        uint256 stake_amount = 32 ether;

        stake_token.transferFrom(msg.sender, address(this), stake_amount * count);



        for (uint256 i = 0; i < count; ++i) {

            bytes memory pubkey = bytes(pubkeys[i * 48:(i + 1) * 48]);

            bytes memory signature = bytes(signatures[i * 96:(i + 1) * 96]);



            _deposit(pubkey, withdrawal_credentials, signature, deposit_data_roots[i], stake_amount);

        }

    }



    function _deposit(

        bytes memory pubkey,

        bytes memory withdrawal_credentials,

        bytes memory signature,

        bytes32 deposit_data_root,

        uint256 stake_amount

    ) internal {

        // Check the contract is not locked

        require(block.number < LOCK_BLOCK_NUMBER, "WencashGenesisDepositContract: Contract is locked");



        // Extended ABI length checks since dynamic types are used.

        require(pubkey.length == 48, "WencashGenesisDepositContract: invalid pubkey length");

        require(withdrawal_credentials.length == 32, "WencashGenesisDepositContract: invalid withdrawal_credentials length");

        require(signature.length == 96, "WencashGenesisDepositContract: invalid signature length");



        // Check deposit amount is exactly 32 WEN

        require(stake_amount == 32 ether, "WencashGenesisDepositContract: Cannot send an amount different from 32 WEN");

        uint256 deposit_amount = stake_amount / 1 gwei;



        // Emit `DepositEvent` log

        bytes memory amount = to_little_endian_64(uint64(deposit_amount));

        emit DepositEvent(

            pubkey,

            withdrawal_credentials,

            amount,

            signature,

            to_little_endian_64(uint64(deposit_count))

        );



        // Compute deposit data root (`DepositData` hash tree root)

        bytes32 pubkey_root = sha256(abi.encodePacked(pubkey, bytes16(0)));

        bytes32[3] memory sig_parts = abi.decode(signature, (bytes32[3]));

        bytes32 signature_root = sha256(

            abi.encodePacked(

                sha256(abi.encodePacked(sig_parts[0], sig_parts[1])),

                sha256(abi.encodePacked(sig_parts[2], bytes32(0)))

            )

        );

        bytes32 node = sha256(

            abi.encodePacked(

                sha256(abi.encodePacked(pubkey_root, withdrawal_credentials)),

                sha256(abi.encodePacked(amount, bytes24(0), signature_root))

            )

        );



        // Verify computed and expected deposit data roots match

        require(

            node == deposit_data_root,

            "DepositContract: reconstructed DepositData does not match supplied deposit_data_root"

        );



        // Avoid overflowing the Merkle tree (and prevent edge case in computing `branch`)

        require(deposit_count < MAX_DEPOSIT_COUNT, "DepositContract: merkle tree full");



        // Add deposit data root to Merkle tree (update a single `branch` node)

        deposit_count += 1;

        uint256 size = deposit_count;

        for (uint256 height = 0; height < DEPOSIT_CONTRACT_TREE_DEPTH; height++) {

            if ((size & 1) == 1) {

                branch[height] = node;

                return;

            }

            node = sha256(abi.encodePacked(branch[height], node));

            size /= 2;

        }

        // As the loop should always end prematurely with the `return` statement,

        // this code should be unreachable. We assert `false` just to be safe.

        assert(false);

    }



    function supportsInterface(bytes4 interfaceId) external pure override returns (bool) {

        return

            interfaceId == type(IERC165).interfaceId ||

            interfaceId == type(IDepositContract).interfaceId;

    }



    function to_little_endian_64(uint64 value) internal pure returns (bytes memory ret) {

        ret = new bytes(8);

        bytes8 bytesValue = bytes8(value);

        // Byteswapping during copying to bytes.

        ret[0] = bytesValue[7];

        ret[1] = bytesValue[6];

        ret[2] = bytesValue[5];

        ret[3] = bytesValue[4];

        ret[4] = bytesValue[3];

        ret[5] = bytesValue[2];

        ret[6] = bytesValue[1];

        ret[7] = bytesValue[0];

    }

}