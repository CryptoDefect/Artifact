/**

 *Submitted for verification at Etherscan.io on 2023-12-13

*/



pragma solidity ^0.8.20;



library GenLib {

    string internal constant _TABLE =

        "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/";



        function encode(bytes memory data) internal pure returns (string memory) {

        if (data.length == 0) return "";



        string memory table = _TABLE;



        string memory result = new string(4 * ((data.length + 2) / 3));



        /// @solidity memory-safe-assembly

        assembly {

            let tablePtr := add(table, 1)



            let resultPtr := add(result, 32)



            for {

                let dataPtr := data

                let endPtr := add(data, mload(data))

            } lt(dataPtr, endPtr) {



            } {

                dataPtr := add(dataPtr, 3)

                let input := mload(dataPtr)



                mstore8(resultPtr, mload(add(tablePtr, and(shr(18, input), 0x3F))))

                resultPtr := add(resultPtr, 1) // Advance



                mstore8(resultPtr, mload(add(tablePtr, and(shr(12, input), 0x3F))))

                resultPtr := add(resultPtr, 1) // Advance



                mstore8(resultPtr, mload(add(tablePtr, and(shr(6, input), 0x3F))))

                resultPtr := add(resultPtr, 1) // Advance



                mstore8(resultPtr, mload(add(tablePtr, and(input, 0x3F))))

                resultPtr := add(resultPtr, 1) // Advance

            }



            switch mod(mload(data), 3)

            case 1 {

                mstore8(sub(resultPtr, 1), 0x3d)

                mstore8(sub(resultPtr, 2), 0x3d)

            }

            case 2 {

                mstore8(sub(resultPtr, 1), 0x3d)

            }

        }



        return result;

    }



    /**

     * @dev Converts a `uint256` to its ASCII `string` decimal representation.

     */

    function toString(uint256 value) internal pure returns (string memory) {

        if (value == 0) {

            return "0";

        }

        uint256 temp = value;

        uint256 digits;

        while (temp != 0) {

            digits++;

            temp /= 10;

        }

        bytes memory buffer = new bytes(digits);

        while (value != 0) {

            digits -= 1;

            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));

            value /= 10;

        }

        return string(buffer);

    }



    function parseInt(string memory _a)

        internal

        pure

        returns (uint8 _parsedInt)

    {

        bytes memory bresult = bytes(_a);

        uint8 mint = 0;

        for (uint8 i = 0; i < bresult.length; i++) {

            if (

                (uint8(uint8(bresult[i])) >= 48) &&

                (uint8(uint8(bresult[i])) <= 57)

            ) {

                mint *= 10;

                mint += uint8(bresult[i]) - 48;

            }

        }

        return mint;

    }



    function parseInt256(string memory _a) internal pure returns (uint256) {

    bytes memory bresult = bytes(_a);

    uint256 mint = 0;

    for (uint256 i = 0; i < bresult.length; i++) {

        if (bresult[i] >= "0" && bresult[i] <= "9") {

            mint *= 10;

            mint += uint256(uint8(bresult[i]) - 48); // ASCII code for '0' is 48

        }

    }

    return mint;

    }



    function substring(

        string memory str,

        uint256 startIndex,

        uint256 endIndex

    ) internal pure returns (string memory) {

        bytes memory strBytes = bytes(str);

        bytes memory result = new bytes(endIndex - startIndex);

        for (uint256 i = startIndex; i < endIndex; i++) {

            result[i - startIndex] = strBytes[i];

        }

        return string(result);

    }



}

// File: @openzeppelin/contracts/security/ReentrancyGuard.sol





// OpenZeppelin Contracts (last updated v4.9.0) (security/ReentrancyGuard.sol)



pragma solidity ^0.8.0;



/**

 * @dev Contract module that helps prevent reentrant calls to a function.

 *

 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier

 * available, which can be applied to functions to make sure there are no nested

 * (reentrant) calls to them.

 *

 * Note that because there is a single `nonReentrant` guard, functions marked as

 * `nonReentrant` may not call one another. This can be worked around by making

 * those functions `private`, and then adding `external` `nonReentrant` entry

 * points to them.

 *

 * TIP: If you would like to learn more about reentrancy and alternative ways

 * to protect against it, check out our blog post

 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].

 */

abstract contract ReentrancyGuard {

    // Booleans are more expensive than uint256 or any type that takes up a full

    // word because each write operation emits an extra SLOAD to first read the

    // slot's contents, replace the bits taken up by the boolean, and then write

    // back. This is the compiler's defense against contract upgrades and

    // pointer aliasing, and it cannot be disabled.



    // The values being non-zero value makes deployment a bit more expensive,

    // but in exchange the refund on every call to nonReentrant will be lower in

    // amount. Since refunds are capped to a percentage of the total

    // transaction's gas, it is best to keep them low in cases like this one, to

    // increase the likelihood of the full refund coming into effect.

    uint256 private constant _NOT_ENTERED = 1;

    uint256 private constant _ENTERED = 2;



    uint256 private _status;



    constructor() {

        _status = _NOT_ENTERED;

    }



    /**

     * @dev Prevents a contract from calling itself, directly or indirectly.

     * Calling a `nonReentrant` function from another `nonReentrant`

     * function is not supported. It is possible to prevent this from happening

     * by making the `nonReentrant` function external, and making it call a

     * `private` function that does the actual work.

     */

    modifier nonReentrant() {

        _nonReentrantBefore();

        _;

        _nonReentrantAfter();

    }



    function _nonReentrantBefore() private {

        // On the first call to nonReentrant, _status will be _NOT_ENTERED

        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");



        // Any calls to nonReentrant after this point will fail

        _status = _ENTERED;

    }



    function _nonReentrantAfter() private {

        // By storing the original value once again, a refund is triggered (see

        // https://eips.ethereum.org/EIPS/eip-2200)

        _status = _NOT_ENTERED;

    }



    /**

     * @dev Returns true if the reentrancy guard is currently set to "entered", which indicates there is a

     * `nonReentrant` function in the call stack.

     */

    function _reentrancyGuardEntered() internal view returns (bool) {

        return _status == _ENTERED;

    }

}



// File: @openzeppelin/contracts/token/ERC20/IERC20.sol





// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC20/IERC20.sol)



pragma solidity ^0.8.20;



/**

 * @dev Interface of the ERC20 standard as defined in the EIP.

 */

interface IERC20 {

    /**

     * @dev Emitted when `value` tokens are moved from one account (`from`) to

     * another (`to`).

     *

     * Note that `value` may be zero.

     */

    event Transfer(address indexed from, address indexed to, uint256 value);



    /**

     * @dev Emitted when the allowance of a `spender` for an `owner` is set by

     * a call to {approve}. `value` is the new allowance.

     */

    event Approval(address indexed owner, address indexed spender, uint256 value);



    /**

     * @dev Returns the value of tokens in existence.

     */

    function totalSupply() external view returns (uint256);



    /**

     * @dev Returns the value of tokens owned by `account`.

     */

    function balanceOf(address account) external view returns (uint256);



    /**

     * @dev Moves a `value` amount of tokens from the caller's account to `to`.

     *

     * Returns a boolean value indicating whether the operation succeeded.

     *

     * Emits a {Transfer} event.

     */

    function transfer(address to, uint256 value) external returns (bool);



    /**

     * @dev Returns the remaining number of tokens that `spender` will be

     * allowed to spend on behalf of `owner` through {transferFrom}. This is

     * zero by default.

     *

     * This value changes when {approve} or {transferFrom} are called.

     */

    function allowance(address owner, address spender) external view returns (uint256);



    /**

     * @dev Sets a `value` amount of tokens as the allowance of `spender` over the

     * caller's tokens.

     *

     * Returns a boolean value indicating whether the operation succeeded.

     *

     * IMPORTANT: Beware that changing an allowance with this method brings the risk

     * that someone may use both the old and the new allowance by unfortunate

     * transaction ordering. One possible solution to mitigate this race

     * condition is to first reduce the spender's allowance to 0 and set the

     * desired value afterwards:

     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729

     *

     * Emits an {Approval} event.

     */

    function approve(address spender, uint256 value) external returns (bool);



    /**

     * @dev Moves a `value` amount of tokens from `from` to `to` using the

     * allowance mechanism. `value` is then deducted from the caller's

     * allowance.

     *

     * Returns a boolean value indicating whether the operation succeeded.

     *

     * Emits a {Transfer} event.

     */

    function transferFrom(address from, address to, uint256 value) external returns (bool);

}



// File: @openzeppelin/contracts/interfaces/draft-IERC6093.sol





// OpenZeppelin Contracts (last updated v5.0.0) (interfaces/draft-IERC6093.sol)

pragma solidity ^0.8.20;



/**

 * @dev Standard ERC20 Errors

 * Interface of the https://eips.ethereum.org/EIPS/eip-6093[ERC-6093] custom errors for ERC20 tokens.

 */

interface IERC20Errors {

    /**

     * @dev Indicates an error related to the current `balance` of a `sender`. Used in transfers.

     * @param sender Address whose tokens are being transferred.

     * @param balance Current balance for the interacting account.

     * @param needed Minimum amount required to perform a transfer.

     */

    error ERC20InsufficientBalance(address sender, uint256 balance, uint256 needed);



    /**

     * @dev Indicates a failure with the token `sender`. Used in transfers.

     * @param sender Address whose tokens are being transferred.

     */

    error ERC20InvalidSender(address sender);



    /**

     * @dev Indicates a failure with the token `receiver`. Used in transfers.

     * @param receiver Address to which tokens are being transferred.

     */

    error ERC20InvalidReceiver(address receiver);



    /**

     * @dev Indicates a failure with the `spender`’s `allowance`. Used in transfers.

     * @param spender Address that may be allowed to operate on tokens without being their owner.

     * @param allowance Amount of tokens a `spender` is allowed to operate with.

     * @param needed Minimum amount required to perform a transfer.

     */

    error ERC20InsufficientAllowance(address spender, uint256 allowance, uint256 needed);



    /**

     * @dev Indicates a failure with the `approver` of a token to be approved. Used in approvals.

     * @param approver Address initiating an approval operation.

     */

    error ERC20InvalidApprover(address approver);



    /**

     * @dev Indicates a failure with the `spender` to be approved. Used in approvals.

     * @param spender Address that may be allowed to operate on tokens without being their owner.

     */

    error ERC20InvalidSpender(address spender);

}



/**

 * @dev Standard ERC721 Errors

 * Interface of the https://eips.ethereum.org/EIPS/eip-6093[ERC-6093] custom errors for ERC721 tokens.

 */

interface IERC721Errors {

    /**

     * @dev Indicates that an address can't be an owner. For example, `address(0)` is a forbidden owner in EIP-20.

     * Used in balance queries.

     * @param owner Address of the current owner of a token.

     */

    error ERC721InvalidOwner(address owner);



    /**

     * @dev Indicates a `tokenId` whose `owner` is the zero address.

     * @param tokenId Identifier number of a token.

     */

    error ERC721NonexistentToken(uint256 tokenId);



    /**

     * @dev Indicates an error related to the ownership over a particular token. Used in transfers.

     * @param sender Address whose tokens are being transferred.

     * @param tokenId Identifier number of a token.

     * @param owner Address of the current owner of a token.

     */

    error ERC721IncorrectOwner(address sender, uint256 tokenId, address owner);



    /**

     * @dev Indicates a failure with the token `sender`. Used in transfers.

     * @param sender Address whose tokens are being transferred.

     */

    error ERC721InvalidSender(address sender);



    /**

     * @dev Indicates a failure with the token `receiver`. Used in transfers.

     * @param receiver Address to which tokens are being transferred.

     */

    error ERC721InvalidReceiver(address receiver);



    /**

     * @dev Indicates a failure with the `operator`’s approval. Used in transfers.

     * @param operator Address that may be allowed to operate on tokens without being their owner.

     * @param tokenId Identifier number of a token.

     */

    error ERC721InsufficientApproval(address operator, uint256 tokenId);



    /**

     * @dev Indicates a failure with the `approver` of a token to be approved. Used in approvals.

     * @param approver Address initiating an approval operation.

     */

    error ERC721InvalidApprover(address approver);



    /**

     * @dev Indicates a failure with the `operator` to be approved. Used in approvals.

     * @param operator Address that may be allowed to operate on tokens without being their owner.

     */

    error ERC721InvalidOperator(address operator);

}



/**

 * @dev Standard ERC1155 Errors

 * Interface of the https://eips.ethereum.org/EIPS/eip-6093[ERC-6093] custom errors for ERC1155 tokens.

 */

interface IERC1155Errors {

    /**

     * @dev Indicates an error related to the current `balance` of a `sender`. Used in transfers.

     * @param sender Address whose tokens are being transferred.

     * @param balance Current balance for the interacting account.

     * @param needed Minimum amount required to perform a transfer.

     * @param tokenId Identifier number of a token.

     */

    error ERC1155InsufficientBalance(address sender, uint256 balance, uint256 needed, uint256 tokenId);



    /**

     * @dev Indicates a failure with the token `sender`. Used in transfers.

     * @param sender Address whose tokens are being transferred.

     */

    error ERC1155InvalidSender(address sender);



    /**

     * @dev Indicates a failure with the token `receiver`. Used in transfers.

     * @param receiver Address to which tokens are being transferred.

     */

    error ERC1155InvalidReceiver(address receiver);



    /**

     * @dev Indicates a failure with the `operator`’s approval. Used in transfers.

     * @param operator Address that may be allowed to operate on tokens without being their owner.

     * @param owner Address of the current owner of a token.

     */

    error ERC1155MissingApprovalForAll(address operator, address owner);



    /**

     * @dev Indicates a failure with the `approver` of a token to be approved. Used in approvals.

     * @param approver Address initiating an approval operation.

     */

    error ERC1155InvalidApprover(address approver);



    /**

     * @dev Indicates a failure with the `operator` to be approved. Used in approvals.

     * @param operator Address that may be allowed to operate on tokens without being their owner.

     */

    error ERC1155InvalidOperator(address operator);



    /**

     * @dev Indicates an array length mismatch between ids and values in a safeBatchTransferFrom operation.

     * Used in batch transfers.

     * @param idsLength Length of the array of token identifiers

     * @param valuesLength Length of the array of token amounts

     */

    error ERC1155InvalidArrayLength(uint256 idsLength, uint256 valuesLength);

}



// File: @openzeppelin/contracts/utils/math/SignedMath.sol





// OpenZeppelin Contracts (last updated v5.0.0) (utils/math/SignedMath.sol)



pragma solidity ^0.8.20;



/**

 * @dev Standard signed math utilities missing in the Solidity language.

 */

library SignedMath {

    /**

     * @dev Returns the largest of two signed numbers.

     */

    function max(int256 a, int256 b) internal pure returns (int256) {

        return a > b ? a : b;

    }



    /**

     * @dev Returns the smallest of two signed numbers.

     */

    function min(int256 a, int256 b) internal pure returns (int256) {

        return a < b ? a : b;

    }



    /**

     * @dev Returns the average of two signed numbers without overflow.

     * The result is rounded towards zero.

     */

    function average(int256 a, int256 b) internal pure returns (int256) {

        // Formula from the book "Hacker's Delight"

        int256 x = (a & b) + ((a ^ b) >> 1);

        return x + (int256(uint256(x) >> 255) & (a ^ b));

    }



    /**

     * @dev Returns the absolute unsigned value of a signed value.

     */

    function abs(int256 n) internal pure returns (uint256) {

        unchecked {

            // must be unchecked in order to support `n = type(int256).min`

            return uint256(n >= 0 ? n : -n);

        }

    }

}



// File: @openzeppelin/contracts/utils/math/Math.sol





// OpenZeppelin Contracts (last updated v5.0.0) (utils/math/Math.sol)



pragma solidity ^0.8.20;



/**

 * @dev Standard math utilities missing in the Solidity language.

 */

library Math {

    /**

     * @dev Muldiv operation overflow.

     */

    error MathOverflowedMulDiv();



    enum Rounding {

        Floor, // Toward negative infinity

        Ceil, // Toward positive infinity

        Trunc, // Toward zero

        Expand // Away from zero

    }



    /**

     * @dev Returns the addition of two unsigned integers, with an overflow flag.

     */

    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {

        unchecked {

            uint256 c = a + b;

            if (c < a) return (false, 0);

            return (true, c);

        }

    }



    /**

     * @dev Returns the subtraction of two unsigned integers, with an overflow flag.

     */

    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {

        unchecked {

            if (b > a) return (false, 0);

            return (true, a - b);

        }

    }



    /**

     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.

     */

    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {

        unchecked {

            // Gas optimization: this is cheaper than requiring 'a' not being zero, but the

            // benefit is lost if 'b' is also tested.

            // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522

            if (a == 0) return (true, 0);

            uint256 c = a * b;

            if (c / a != b) return (false, 0);

            return (true, c);

        }

    }



    /**

     * @dev Returns the division of two unsigned integers, with a division by zero flag.

     */

    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {

        unchecked {

            if (b == 0) return (false, 0);

            return (true, a / b);

        }

    }



    /**

     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.

     */

    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {

        unchecked {

            if (b == 0) return (false, 0);

            return (true, a % b);

        }

    }



    /**

     * @dev Returns the largest of two numbers.

     */

    function max(uint256 a, uint256 b) internal pure returns (uint256) {

        return a > b ? a : b;

    }



    /**

     * @dev Returns the smallest of two numbers.

     */

    function min(uint256 a, uint256 b) internal pure returns (uint256) {

        return a < b ? a : b;

    }



    /**

     * @dev Returns the average of two numbers. The result is rounded towards

     * zero.

     */

    function average(uint256 a, uint256 b) internal pure returns (uint256) {

        // (a + b) / 2 can overflow.

        return (a & b) + (a ^ b) / 2;

    }



    /**

     * @dev Returns the ceiling of the division of two numbers.

     *

     * This differs from standard division with `/` in that it rounds towards infinity instead

     * of rounding towards zero.

     */

    function ceilDiv(uint256 a, uint256 b) internal pure returns (uint256) {

        if (b == 0) {

            // Guarantee the same behavior as in a regular Solidity division.

            return a / b;

        }



        // (a + b - 1) / b can overflow on addition, so we distribute.

        return a == 0 ? 0 : (a - 1) / b + 1;

    }



    /**

     * @notice Calculates floor(x * y / denominator) with full precision. Throws if result overflows a uint256 or

     * denominator == 0.

     * @dev Original credit to Remco Bloemen under MIT license (https://xn--2-umb.com/21/muldiv) with further edits by

     * Uniswap Labs also under MIT license.

     */

    function mulDiv(uint256 x, uint256 y, uint256 denominator) internal pure returns (uint256 result) {

        unchecked {

            // 512-bit multiply [prod1 prod0] = x * y. Compute the product mod 2^256 and mod 2^256 - 1, then use

            // use the Chinese Remainder Theorem to reconstruct the 512 bit result. The result is stored in two 256

            // variables such that product = prod1 * 2^256 + prod0.

            uint256 prod0 = x * y; // Least significant 256 bits of the product

            uint256 prod1; // Most significant 256 bits of the product

            assembly {

                let mm := mulmod(x, y, not(0))

                prod1 := sub(sub(mm, prod0), lt(mm, prod0))

            }



            // Handle non-overflow cases, 256 by 256 division.

            if (prod1 == 0) {

                // Solidity will revert if denominator == 0, unlike the div opcode on its own.

                // The surrounding unchecked block does not change this fact.

                // See https://docs.soliditylang.org/en/latest/control-structures.html#checked-or-unchecked-arithmetic.

                return prod0 / denominator;

            }



            // Make sure the result is less than 2^256. Also prevents denominator == 0.

            if (denominator <= prod1) {

                revert MathOverflowedMulDiv();

            }



            ///////////////////////////////////////////////

            // 512 by 256 division.

            ///////////////////////////////////////////////



            // Make division exact by subtracting the remainder from [prod1 prod0].

            uint256 remainder;

            assembly {

                // Compute remainder using mulmod.

                remainder := mulmod(x, y, denominator)



                // Subtract 256 bit number from 512 bit number.

                prod1 := sub(prod1, gt(remainder, prod0))

                prod0 := sub(prod0, remainder)

            }



            // Factor powers of two out of denominator and compute largest power of two divisor of denominator.

            // Always >= 1. See https://cs.stackexchange.com/q/138556/92363.



            uint256 twos = denominator & (0 - denominator);

            assembly {

                // Divide denominator by twos.

                denominator := div(denominator, twos)



                // Divide [prod1 prod0] by twos.

                prod0 := div(prod0, twos)



                // Flip twos such that it is 2^256 / twos. If twos is zero, then it becomes one.

                twos := add(div(sub(0, twos), twos), 1)

            }



            // Shift in bits from prod1 into prod0.

            prod0 |= prod1 * twos;



            // Invert denominator mod 2^256. Now that denominator is an odd number, it has an inverse modulo 2^256 such

            // that denominator * inv = 1 mod 2^256. Compute the inverse by starting with a seed that is correct for

            // four bits. That is, denominator * inv = 1 mod 2^4.

            uint256 inverse = (3 * denominator) ^ 2;



            // Use the Newton-Raphson iteration to improve the precision. Thanks to Hensel's lifting lemma, this also

            // works in modular arithmetic, doubling the correct bits in each step.

            inverse *= 2 - denominator * inverse; // inverse mod 2^8

            inverse *= 2 - denominator * inverse; // inverse mod 2^16

            inverse *= 2 - denominator * inverse; // inverse mod 2^32

            inverse *= 2 - denominator * inverse; // inverse mod 2^64

            inverse *= 2 - denominator * inverse; // inverse mod 2^128

            inverse *= 2 - denominator * inverse; // inverse mod 2^256



            // Because the division is now exact we can divide by multiplying with the modular inverse of denominator.

            // This will give us the correct result modulo 2^256. Since the preconditions guarantee that the outcome is

            // less than 2^256, this is the final result. We don't need to compute the high bits of the result and prod1

            // is no longer required.

            result = prod0 * inverse;

            return result;

        }

    }



    /**

     * @notice Calculates x * y / denominator with full precision, following the selected rounding direction.

     */

    function mulDiv(uint256 x, uint256 y, uint256 denominator, Rounding rounding) internal pure returns (uint256) {

        uint256 result = mulDiv(x, y, denominator);

        if (unsignedRoundsUp(rounding) && mulmod(x, y, denominator) > 0) {

            result += 1;

        }

        return result;

    }



    /**

     * @dev Returns the square root of a number. If the number is not a perfect square, the value is rounded

     * towards zero.

     *

     * Inspired by Henry S. Warren, Jr.'s "Hacker's Delight" (Chapter 11).

     */

    function sqrt(uint256 a) internal pure returns (uint256) {

        if (a == 0) {

            return 0;

        }



        // For our first guess, we get the biggest power of 2 which is smaller than the square root of the target.

        //

        // We know that the "msb" (most significant bit) of our target number `a` is a power of 2 such that we have

        // `msb(a) <= a < 2*msb(a)`. This value can be written `msb(a)=2**k` with `k=log2(a)`.

        //

        // This can be rewritten `2**log2(a) <= a < 2**(log2(a) + 1)`

        // → `sqrt(2**k) <= sqrt(a) < sqrt(2**(k+1))`

        // → `2**(k/2) <= sqrt(a) < 2**((k+1)/2) <= 2**(k/2 + 1)`

        //

        // Consequently, `2**(log2(a) / 2)` is a good first approximation of `sqrt(a)` with at least 1 correct bit.

        uint256 result = 1 << (log2(a) >> 1);



        // At this point `result` is an estimation with one bit of precision. We know the true value is a uint128,

        // since it is the square root of a uint256. Newton's method converges quadratically (precision doubles at

        // every iteration). We thus need at most 7 iteration to turn our partial result with one bit of precision

        // into the expected uint128 result.

        unchecked {

            result = (result + a / result) >> 1;

            result = (result + a / result) >> 1;

            result = (result + a / result) >> 1;

            result = (result + a / result) >> 1;

            result = (result + a / result) >> 1;

            result = (result + a / result) >> 1;

            result = (result + a / result) >> 1;

            return min(result, a / result);

        }

    }



    /**

     * @notice Calculates sqrt(a), following the selected rounding direction.

     */

    function sqrt(uint256 a, Rounding rounding) internal pure returns (uint256) {

        unchecked {

            uint256 result = sqrt(a);

            return result + (unsignedRoundsUp(rounding) && result * result < a ? 1 : 0);

        }

    }



    /**

     * @dev Return the log in base 2 of a positive value rounded towards zero.

     * Returns 0 if given 0.

     */

    function log2(uint256 value) internal pure returns (uint256) {

        uint256 result = 0;

        unchecked {

            if (value >> 128 > 0) {

                value >>= 128;

                result += 128;

            }

            if (value >> 64 > 0) {

                value >>= 64;

                result += 64;

            }

            if (value >> 32 > 0) {

                value >>= 32;

                result += 32;

            }

            if (value >> 16 > 0) {

                value >>= 16;

                result += 16;

            }

            if (value >> 8 > 0) {

                value >>= 8;

                result += 8;

            }

            if (value >> 4 > 0) {

                value >>= 4;

                result += 4;

            }

            if (value >> 2 > 0) {

                value >>= 2;

                result += 2;

            }

            if (value >> 1 > 0) {

                result += 1;

            }

        }

        return result;

    }



    /**

     * @dev Return the log in base 2, following the selected rounding direction, of a positive value.

     * Returns 0 if given 0.

     */

    function log2(uint256 value, Rounding rounding) internal pure returns (uint256) {

        unchecked {

            uint256 result = log2(value);

            return result + (unsignedRoundsUp(rounding) && 1 << result < value ? 1 : 0);

        }

    }



    /**

     * @dev Return the log in base 10 of a positive value rounded towards zero.

     * Returns 0 if given 0.

     */

    function log10(uint256 value) internal pure returns (uint256) {

        uint256 result = 0;

        unchecked {

            if (value >= 10 ** 64) {

                value /= 10 ** 64;

                result += 64;

            }

            if (value >= 10 ** 32) {

                value /= 10 ** 32;

                result += 32;

            }

            if (value >= 10 ** 16) {

                value /= 10 ** 16;

                result += 16;

            }

            if (value >= 10 ** 8) {

                value /= 10 ** 8;

                result += 8;

            }

            if (value >= 10 ** 4) {

                value /= 10 ** 4;

                result += 4;

            }

            if (value >= 10 ** 2) {

                value /= 10 ** 2;

                result += 2;

            }

            if (value >= 10 ** 1) {

                result += 1;

            }

        }

        return result;

    }



    /**

     * @dev Return the log in base 10, following the selected rounding direction, of a positive value.

     * Returns 0 if given 0.

     */

    function log10(uint256 value, Rounding rounding) internal pure returns (uint256) {

        unchecked {

            uint256 result = log10(value);

            return result + (unsignedRoundsUp(rounding) && 10 ** result < value ? 1 : 0);

        }

    }



    /**

     * @dev Return the log in base 256 of a positive value rounded towards zero.

     * Returns 0 if given 0.

     *

     * Adding one to the result gives the number of pairs of hex symbols needed to represent `value` as a hex string.

     */

    function log256(uint256 value) internal pure returns (uint256) {

        uint256 result = 0;

        unchecked {

            if (value >> 128 > 0) {

                value >>= 128;

                result += 16;

            }

            if (value >> 64 > 0) {

                value >>= 64;

                result += 8;

            }

            if (value >> 32 > 0) {

                value >>= 32;

                result += 4;

            }

            if (value >> 16 > 0) {

                value >>= 16;

                result += 2;

            }

            if (value >> 8 > 0) {

                result += 1;

            }

        }

        return result;

    }



    /**

     * @dev Return the log in base 256, following the selected rounding direction, of a positive value.

     * Returns 0 if given 0.

     */

    function log256(uint256 value, Rounding rounding) internal pure returns (uint256) {

        unchecked {

            uint256 result = log256(value);

            return result + (unsignedRoundsUp(rounding) && 1 << (result << 3) < value ? 1 : 0);

        }

    }



    /**

     * @dev Returns whether a provided rounding mode is considered rounding up for unsigned integers.

     */

    function unsignedRoundsUp(Rounding rounding) internal pure returns (bool) {

        return uint8(rounding) % 2 == 1;

    }

}



// File: @openzeppelin/contracts/utils/Strings.sol





// OpenZeppelin Contracts (last updated v5.0.0) (utils/Strings.sol)



pragma solidity ^0.8.20;







/**

 * @dev String operations.

 */

library Strings {

    bytes16 private constant HEX_DIGITS = "0123456789abcdef";

    uint8 private constant ADDRESS_LENGTH = 20;



    /**

     * @dev The `value` string doesn't fit in the specified `length`.

     */

    error StringsInsufficientHexLength(uint256 value, uint256 length);



    /**

     * @dev Converts a `uint256` to its ASCII `string` decimal representation.

     */

    function toString(uint256 value) internal pure returns (string memory) {

        unchecked {

            uint256 length = Math.log10(value) + 1;

            string memory buffer = new string(length);

            uint256 ptr;

            /// @solidity memory-safe-assembly

            assembly {

                ptr := add(buffer, add(32, length))

            }

            while (true) {

                ptr--;

                /// @solidity memory-safe-assembly

                assembly {

                    mstore8(ptr, byte(mod(value, 10), HEX_DIGITS))

                }

                value /= 10;

                if (value == 0) break;

            }

            return buffer;

        }

    }



    /**

     * @dev Converts a `int256` to its ASCII `string` decimal representation.

     */

    function toStringSigned(int256 value) internal pure returns (string memory) {

        return string.concat(value < 0 ? "-" : "", toString(SignedMath.abs(value)));

    }



    /**

     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.

     */

    function toHexString(uint256 value) internal pure returns (string memory) {

        unchecked {

            return toHexString(value, Math.log256(value) + 1);

        }

    }



    /**

     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.

     */

    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {

        uint256 localValue = value;

        bytes memory buffer = new bytes(2 * length + 2);

        buffer[0] = "0";

        buffer[1] = "x";

        for (uint256 i = 2 * length + 1; i > 1; --i) {

            buffer[i] = HEX_DIGITS[localValue & 0xf];

            localValue >>= 4;

        }

        if (localValue != 0) {

            revert StringsInsufficientHexLength(value, length);

        }

        return string(buffer);

    }



    /**

     * @dev Converts an `address` with fixed length of 20 bytes to its not checksummed ASCII `string` hexadecimal

     * representation.

     */

    function toHexString(address addr) internal pure returns (string memory) {

        return toHexString(uint256(uint160(addr)), ADDRESS_LENGTH);

    }



    /**

     * @dev Returns true if the two strings are equal.

     */

    function equal(string memory a, string memory b) internal pure returns (bool) {

        return bytes(a).length == bytes(b).length && keccak256(bytes(a)) == keccak256(bytes(b));

    }

}



// File: @openzeppelin/contracts/utils/Context.sol





// OpenZeppelin Contracts (last updated v5.0.1) (utils/Context.sol)



pragma solidity ^0.8.20;



/**

 * @dev Provides information about the current execution context, including the

 * sender of the transaction and its data. While these are generally available

 * via msg.sender and msg.data, they should not be accessed in such a direct

 * manner, since when dealing with meta-transactions the account sending and

 * paying for execution may not be the actual sender (as far as an application

 * is concerned).

 *

 * This contract is only required for intermediate, library-like contracts.

 */

abstract contract Context {

    function _msgSender() internal view virtual returns (address) {

        return msg.sender;

    }



    function _msgData() internal view virtual returns (bytes calldata) {

        return msg.data;

    }



    function _contextSuffixLength() internal view virtual returns (uint256) {

        return 0;

    }

}



// File: @openzeppelin/contracts/token/ERC721/IERC721Receiver.sol





// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC721/IERC721Receiver.sol)



pragma solidity ^0.8.20;



/**

 * @title ERC721 token receiver interface

 * @dev Interface for any contract that wants to support safeTransfers

 * from ERC721 asset contracts.

 */

interface IERC721Receiver {

    /**

     * @dev Whenever an {IERC721} `tokenId` token is transferred to this contract via {IERC721-safeTransferFrom}

     * by `operator` from `from`, this function is called.

     *

     * It must return its Solidity selector to confirm the token transfer.

     * If any other value is returned or the interface is not implemented by the recipient, the transfer will be

     * reverted.

     *

     * The selector can be obtained in Solidity with `IERC721Receiver.onERC721Received.selector`.

     */

    function onERC721Received(

        address operator,

        address from,

        uint256 tokenId,

        bytes calldata data

    ) external returns (bytes4);

}



// File: @openzeppelin/contracts/utils/introspection/IERC165.sol





// OpenZeppelin Contracts (last updated v5.0.0) (utils/introspection/IERC165.sol)



pragma solidity ^0.8.20;



/**

 * @dev Interface of the ERC165 standard, as defined in the

 * https://eips.ethereum.org/EIPS/eip-165[EIP].

 *

 * Implementers can declare support of contract interfaces, which can then be

 * queried by others ({ERC165Checker}).

 *

 * For an implementation, see {ERC165}.

 */

interface IERC165 {

    /**

     * @dev Returns true if this contract implements the interface defined by

     * `interfaceId`. See the corresponding

     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]

     * to learn more about how these ids are created.

     *

     * This function call must use less than 30 000 gas.

     */

    function supportsInterface(bytes4 interfaceId) external view returns (bool);

}



// File: @openzeppelin/contracts/utils/introspection/ERC165.sol





// OpenZeppelin Contracts (last updated v5.0.0) (utils/introspection/ERC165.sol)



pragma solidity ^0.8.20;





/**

 * @dev Implementation of the {IERC165} interface.

 *

 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check

 * for the additional interface id that will be supported. For example:

 *

 * ```solidity

 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {

 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);

 * }

 * ```

 */

abstract contract ERC165 is IERC165 {

    /**

     * @dev See {IERC165-supportsInterface}.

     */

    function supportsInterface(bytes4 interfaceId) public view virtual returns (bool) {

        return interfaceId == type(IERC165).interfaceId;

    }

}



// File: @openzeppelin/contracts/token/ERC721/IERC721.sol





// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC721/IERC721.sol)



pragma solidity ^0.8.20;





/**

 * @dev Required interface of an ERC721 compliant contract.

 */

interface IERC721 is IERC165 {

    /**

     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.

     */

    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);



    /**

     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.

     */

    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);



    /**

     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.

     */

    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);



    /**

     * @dev Returns the number of tokens in ``owner``'s account.

     */

    function balanceOf(address owner) external view returns (uint256 balance);



    /**

     * @dev Returns the owner of the `tokenId` token.

     *

     * Requirements:

     *

     * - `tokenId` must exist.

     */

    function ownerOf(uint256 tokenId) external view returns (address owner);



    /**

     * @dev Safely transfers `tokenId` token from `from` to `to`.

     *

     * Requirements:

     *

     * - `from` cannot be the zero address.

     * - `to` cannot be the zero address.

     * - `tokenId` token must exist and be owned by `from`.

     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.

     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon

     *   a safe transfer.

     *

     * Emits a {Transfer} event.

     */

    function safeTransferFrom(address from, address to, uint256 tokenId, bytes calldata data) external;



    /**

     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients

     * are aware of the ERC721 protocol to prevent tokens from being forever locked.

     *

     * Requirements:

     *

     * - `from` cannot be the zero address.

     * - `to` cannot be the zero address.

     * - `tokenId` token must exist and be owned by `from`.

     * - If the caller is not `from`, it must have been allowed to move this token by either {approve} or

     *   {setApprovalForAll}.

     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon

     *   a safe transfer.

     *

     * Emits a {Transfer} event.

     */

    function safeTransferFrom(address from, address to, uint256 tokenId) external;



    /**

     * @dev Transfers `tokenId` token from `from` to `to`.

     *

     * WARNING: Note that the caller is responsible to confirm that the recipient is capable of receiving ERC721

     * or else they may be permanently lost. Usage of {safeTransferFrom} prevents loss, though the caller must

     * understand this adds an external call which potentially creates a reentrancy vulnerability.

     *

     * Requirements:

     *

     * - `from` cannot be the zero address.

     * - `to` cannot be the zero address.

     * - `tokenId` token must be owned by `from`.

     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.

     *

     * Emits a {Transfer} event.

     */

    function transferFrom(address from, address to, uint256 tokenId) external;



    /**

     * @dev Gives permission to `to` to transfer `tokenId` token to another account.

     * The approval is cleared when the token is transferred.

     *

     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.

     *

     * Requirements:

     *

     * - The caller must own the token or be an approved operator.

     * - `tokenId` must exist.

     *

     * Emits an {Approval} event.

     */

    function approve(address to, uint256 tokenId) external;



    /**

     * @dev Approve or remove `operator` as an operator for the caller.

     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.

     *

     * Requirements:

     *

     * - The `operator` cannot be the address zero.

     *

     * Emits an {ApprovalForAll} event.

     */

    function setApprovalForAll(address operator, bool approved) external;



    /**

     * @dev Returns the account approved for `tokenId` token.

     *

     * Requirements:

     *

     * - `tokenId` must exist.

     */

    function getApproved(uint256 tokenId) external view returns (address operator);



    /**

     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.

     *

     * See {setApprovalForAll}

     */

    function isApprovedForAll(address owner, address operator) external view returns (bool);

}



// File: @openzeppelin/contracts/token/ERC721/extensions/IERC721Enumerable.sol





// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC721/extensions/IERC721Enumerable.sol)



pragma solidity ^0.8.20;





/**

 * @title ERC-721 Non-Fungible Token Standard, optional enumeration extension

 * @dev See https://eips.ethereum.org/EIPS/eip-721

 */

interface IERC721Enumerable is IERC721 {

    /**

     * @dev Returns the total amount of tokens stored by the contract.

     */

    function totalSupply() external view returns (uint256);



    /**

     * @dev Returns a token ID owned by `owner` at a given `index` of its token list.

     * Use along with {balanceOf} to enumerate all of ``owner``'s tokens.

     */

    function tokenOfOwnerByIndex(address owner, uint256 index) external view returns (uint256);



    /**

     * @dev Returns a token ID at a given `index` of all the tokens stored by the contract.

     * Use along with {totalSupply} to enumerate all tokens.

     */

    function tokenByIndex(uint256 index) external view returns (uint256);

}



// File: @openzeppelin/contracts/token/ERC721/extensions/IERC721Metadata.sol





// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC721/extensions/IERC721Metadata.sol)



pragma solidity ^0.8.20;





/**

 * @title ERC-721 Non-Fungible Token Standard, optional metadata extension

 * @dev See https://eips.ethereum.org/EIPS/eip-721

 */

interface IERC721Metadata is IERC721 {

    /**

     * @dev Returns the token collection name.

     */

    function name() external view returns (string memory);



    /**

     * @dev Returns the token collection symbol.

     */

    function symbol() external view returns (string memory);



    /**

     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.

     */

    function tokenURI(uint256 tokenId) external view returns (string memory);

}



// File: @openzeppelin/contracts/token/ERC721/ERC721.sol





// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC721/ERC721.sol)



pragma solidity ^0.8.20;

















/**

 * @dev Implementation of https://eips.ethereum.org/EIPS/eip-721[ERC721] Non-Fungible Token Standard, including

 * the Metadata extension, but not including the Enumerable extension, which is available separately as

 * {ERC721Enumerable}.

 */

abstract contract ERC721 is Context, ERC165, IERC721, IERC721Metadata, IERC721Errors {

    using Strings for uint256;



    // Token name

    string private _name;



    // Token symbol

    string private _symbol;



    mapping(uint256 tokenId => address) private _owners;



    mapping(address owner => uint256) private _balances;



    mapping(uint256 tokenId => address) private _tokenApprovals;



    mapping(address owner => mapping(address operator => bool)) private _operatorApprovals;



    /**

     * @dev Initializes the contract by setting a `name` and a `symbol` to the token collection.

     */

    constructor(string memory name_, string memory symbol_) {

        _name = name_;

        _symbol = symbol_;

    }



    /**

     * @dev See {IERC165-supportsInterface}.

     */

    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {

        return

            interfaceId == type(IERC721).interfaceId ||

            interfaceId == type(IERC721Metadata).interfaceId ||

            super.supportsInterface(interfaceId);

    }



    /**

     * @dev See {IERC721-balanceOf}.

     */

    function balanceOf(address owner) public view virtual returns (uint256) {

        if (owner == address(0)) {

            revert ERC721InvalidOwner(address(0));

        }

        return _balances[owner];

    }



    /**

     * @dev See {IERC721-ownerOf}.

     */

    function ownerOf(uint256 tokenId) public view virtual returns (address) {

        return _requireOwned(tokenId);

    }



    /**

     * @dev See {IERC721Metadata-name}.

     */

    function name() public view virtual returns (string memory) {

        return _name;

    }



    /**

     * @dev See {IERC721Metadata-symbol}.

     */

    function symbol() public view virtual returns (string memory) {

        return _symbol;

    }



    /**

     * @dev See {IERC721Metadata-tokenURI}.

     */

    function tokenURI(uint256 tokenId) public view virtual returns (string memory) {

        _requireOwned(tokenId);



        string memory baseURI = _baseURI();

        return bytes(baseURI).length > 0 ? string.concat(baseURI, tokenId.toString()) : "";

    }



    /**

     * @dev Base URI for computing {tokenURI}. If set, the resulting URI for each

     * token will be the concatenation of the `baseURI` and the `tokenId`. Empty

     * by default, can be overridden in child contracts.

     */

    function _baseURI() internal view virtual returns (string memory) {

        return "";

    }



    /**

     * @dev See {IERC721-approve}.

     */

    function approve(address to, uint256 tokenId) public virtual {

        _approve(to, tokenId, _msgSender());

    }



    /**

     * @dev See {IERC721-getApproved}.

     */

    function getApproved(uint256 tokenId) public view virtual returns (address) {

        _requireOwned(tokenId);



        return _getApproved(tokenId);

    }



    /**

     * @dev See {IERC721-setApprovalForAll}.

     */

    function setApprovalForAll(address operator, bool approved) public virtual {

        _setApprovalForAll(_msgSender(), operator, approved);

    }



    /**

     * @dev See {IERC721-isApprovedForAll}.

     */

    function isApprovedForAll(address owner, address operator) public view virtual returns (bool) {

        return _operatorApprovals[owner][operator];

    }



    /**

     * @dev See {IERC721-transferFrom}.

     */

    function transferFrom(address from, address to, uint256 tokenId) public virtual {

        if (to == address(0)) {

            revert ERC721InvalidReceiver(address(0));

        }

        // Setting an "auth" arguments enables the `_isAuthorized` check which verifies that the token exists

        // (from != 0). Therefore, it is not needed to verify that the return value is not 0 here.

        address previousOwner = _update(to, tokenId, _msgSender());

        if (previousOwner != from) {

            revert ERC721IncorrectOwner(from, tokenId, previousOwner);

        }

    }



    /**

     * @dev See {IERC721-safeTransferFrom}.

     */

    function safeTransferFrom(address from, address to, uint256 tokenId) public {

        safeTransferFrom(from, to, tokenId, "");

    }



    /**

     * @dev See {IERC721-safeTransferFrom}.

     */

    function safeTransferFrom(address from, address to, uint256 tokenId, bytes memory data) public virtual {

        transferFrom(from, to, tokenId);

        _checkOnERC721Received(from, to, tokenId, data);

    }



    /**

     * @dev Returns the owner of the `tokenId`. Does NOT revert if token doesn't exist

     *

     * IMPORTANT: Any overrides to this function that add ownership of tokens not tracked by the

     * core ERC721 logic MUST be matched with the use of {_increaseBalance} to keep balances

     * consistent with ownership. The invariant to preserve is that for any address `a` the value returned by

     * `balanceOf(a)` must be equal to the number of tokens such that `_ownerOf(tokenId)` is `a`.

     */

    function _ownerOf(uint256 tokenId) internal view virtual returns (address) {

        return _owners[tokenId];

    }



    /**

     * @dev Returns the approved address for `tokenId`. Returns 0 if `tokenId` is not minted.

     */

    function _getApproved(uint256 tokenId) internal view virtual returns (address) {

        return _tokenApprovals[tokenId];

    }



    /**

     * @dev Returns whether `spender` is allowed to manage `owner`'s tokens, or `tokenId` in

     * particular (ignoring whether it is owned by `owner`).

     *

     * WARNING: This function assumes that `owner` is the actual owner of `tokenId` and does not verify this

     * assumption.

     */

    function _isAuthorized(address owner, address spender, uint256 tokenId) internal view virtual returns (bool) {

        return

            spender != address(0) &&

            (owner == spender || isApprovedForAll(owner, spender) || _getApproved(tokenId) == spender);

    }



    /**

     * @dev Checks if `spender` can operate on `tokenId`, assuming the provided `owner` is the actual owner.

     * Reverts if `spender` does not have approval from the provided `owner` for the given token or for all its assets

     * the `spender` for the specific `tokenId`.

     *

     * WARNING: This function assumes that `owner` is the actual owner of `tokenId` and does not verify this

     * assumption.

     */

    function _checkAuthorized(address owner, address spender, uint256 tokenId) internal view virtual {

        if (!_isAuthorized(owner, spender, tokenId)) {

            if (owner == address(0)) {

                revert ERC721NonexistentToken(tokenId);

            } else {

                revert ERC721InsufficientApproval(spender, tokenId);

            }

        }

    }



    /**

     * @dev Unsafe write access to the balances, used by extensions that "mint" tokens using an {ownerOf} override.

     *

     * NOTE: the value is limited to type(uint128).max. This protect against _balance overflow. It is unrealistic that

     * a uint256 would ever overflow from increments when these increments are bounded to uint128 values.

     *

     * WARNING: Increasing an account's balance using this function tends to be paired with an override of the

     * {_ownerOf} function to resolve the ownership of the corresponding tokens so that balances and ownership

     * remain consistent with one another.

     */

    function _increaseBalance(address account, uint128 value) internal virtual {

        unchecked {

            _balances[account] += value;

        }

    }



    /**

     * @dev Transfers `tokenId` from its current owner to `to`, or alternatively mints (or burns) if the current owner

     * (or `to`) is the zero address. Returns the owner of the `tokenId` before the update.

     *

     * The `auth` argument is optional. If the value passed is non 0, then this function will check that

     * `auth` is either the owner of the token, or approved to operate on the token (by the owner).

     *

     * Emits a {Transfer} event.

     *

     * NOTE: If overriding this function in a way that tracks balances, see also {_increaseBalance}.

     */

    function _update(address to, uint256 tokenId, address auth) internal virtual returns (address) {

        address from = _ownerOf(tokenId);



        // Perform (optional) operator check

        if (auth != address(0)) {

            _checkAuthorized(from, auth, tokenId);

        }



        // Execute the update

        if (from != address(0)) {

            // Clear approval. No need to re-authorize or emit the Approval event

            _approve(address(0), tokenId, address(0), false);



            unchecked {

                _balances[from] -= 1;

            }

        }



        if (to != address(0)) {

            unchecked {

                _balances[to] += 1;

            }

        }



        _owners[tokenId] = to;



        emit Transfer(from, to, tokenId);



        return from;

    }



    /**

     * @dev Mints `tokenId` and transfers it to `to`.

     *

     * WARNING: Usage of this method is discouraged, use {_safeMint} whenever possible

     *

     * Requirements:

     *

     * - `tokenId` must not exist.

     * - `to` cannot be the zero address.

     *

     * Emits a {Transfer} event.

     */

    function _mint(address to, uint256 tokenId) internal {

        if (to == address(0)) {

            revert ERC721InvalidReceiver(address(0));

        }

        address previousOwner = _update(to, tokenId, address(0));

        if (previousOwner != address(0)) {

            revert ERC721InvalidSender(address(0));

        }

    }



    /**

     * @dev Mints `tokenId`, transfers it to `to` and checks for `to` acceptance.

     *

     * Requirements:

     *

     * - `tokenId` must not exist.

     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.

     *

     * Emits a {Transfer} event.

     */

    function _safeMint(address to, uint256 tokenId) internal {

        _safeMint(to, tokenId, "");

    }



    /**

     * @dev Same as {xref-ERC721-_safeMint-address-uint256-}[`_safeMint`], with an additional `data` parameter which is

     * forwarded in {IERC721Receiver-onERC721Received} to contract recipients.

     */

    function _safeMint(address to, uint256 tokenId, bytes memory data) internal virtual {

        _mint(to, tokenId);

        _checkOnERC721Received(address(0), to, tokenId, data);

    }



    /**

     * @dev Destroys `tokenId`.

     * The approval is cleared when the token is burned.

     * This is an internal function that does not check if the sender is authorized to operate on the token.

     *

     * Requirements:

     *

     * - `tokenId` must exist.

     *

     * Emits a {Transfer} event.

     */

    function _burn(uint256 tokenId) internal {

        address previousOwner = _update(address(0), tokenId, address(0));

        if (previousOwner == address(0)) {

            revert ERC721NonexistentToken(tokenId);

        }

    }



    /**

     * @dev Transfers `tokenId` from `from` to `to`.

     *  As opposed to {transferFrom}, this imposes no restrictions on msg.sender.

     *

     * Requirements:

     *

     * - `to` cannot be the zero address.

     * - `tokenId` token must be owned by `from`.

     *

     * Emits a {Transfer} event.

     */

    function _transfer(address from, address to, uint256 tokenId) internal {

        if (to == address(0)) {

            revert ERC721InvalidReceiver(address(0));

        }

        address previousOwner = _update(to, tokenId, address(0));

        if (previousOwner == address(0)) {

            revert ERC721NonexistentToken(tokenId);

        } else if (previousOwner != from) {

            revert ERC721IncorrectOwner(from, tokenId, previousOwner);

        }

    }



    /**

     * @dev Safely transfers `tokenId` token from `from` to `to`, checking that contract recipients

     * are aware of the ERC721 standard to prevent tokens from being forever locked.

     *

     * `data` is additional data, it has no specified format and it is sent in call to `to`.

     *

     * This internal function is like {safeTransferFrom} in the sense that it invokes

     * {IERC721Receiver-onERC721Received} on the receiver, and can be used to e.g.

     * implement alternative mechanisms to perform token transfer, such as signature-based.

     *

     * Requirements:

     *

     * - `tokenId` token must exist and be owned by `from`.

     * - `to` cannot be the zero address.

     * - `from` cannot be the zero address.

     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.

     *

     * Emits a {Transfer} event.

     */

    function _safeTransfer(address from, address to, uint256 tokenId) internal {

        _safeTransfer(from, to, tokenId, "");

    }



    /**

     * @dev Same as {xref-ERC721-_safeTransfer-address-address-uint256-}[`_safeTransfer`], with an additional `data` parameter which is

     * forwarded in {IERC721Receiver-onERC721Received} to contract recipients.

     */

    function _safeTransfer(address from, address to, uint256 tokenId, bytes memory data) internal virtual {

        _transfer(from, to, tokenId);

        _checkOnERC721Received(from, to, tokenId, data);

    }



    /**

     * @dev Approve `to` to operate on `tokenId`

     *

     * The `auth` argument is optional. If the value passed is non 0, then this function will check that `auth` is

     * either the owner of the token, or approved to operate on all tokens held by this owner.

     *

     * Emits an {Approval} event.

     *

     * Overrides to this logic should be done to the variant with an additional `bool emitEvent` argument.

     */

    function _approve(address to, uint256 tokenId, address auth) internal {

        _approve(to, tokenId, auth, true);

    }



    /**

     * @dev Variant of `_approve` with an optional flag to enable or disable the {Approval} event. The event is not

     * emitted in the context of transfers.

     */

    function _approve(address to, uint256 tokenId, address auth, bool emitEvent) internal virtual {

        // Avoid reading the owner unless necessary

        if (emitEvent || auth != address(0)) {

            address owner = _requireOwned(tokenId);



            // We do not use _isAuthorized because single-token approvals should not be able to call approve

            if (auth != address(0) && owner != auth && !isApprovedForAll(owner, auth)) {

                revert ERC721InvalidApprover(auth);

            }



            if (emitEvent) {

                emit Approval(owner, to, tokenId);

            }

        }



        _tokenApprovals[tokenId] = to;

    }



    /**

     * @dev Approve `operator` to operate on all of `owner` tokens

     *

     * Requirements:

     * - operator can't be the address zero.

     *

     * Emits an {ApprovalForAll} event.

     */

    function _setApprovalForAll(address owner, address operator, bool approved) internal virtual {

        if (operator == address(0)) {

            revert ERC721InvalidOperator(operator);

        }

        _operatorApprovals[owner][operator] = approved;

        emit ApprovalForAll(owner, operator, approved);

    }



    /**

     * @dev Reverts if the `tokenId` doesn't have a current owner (it hasn't been minted, or it has been burned).

     * Returns the owner.

     *

     * Overrides to ownership logic should be done to {_ownerOf}.

     */

    function _requireOwned(uint256 tokenId) internal view returns (address) {

        address owner = _ownerOf(tokenId);

        if (owner == address(0)) {

            revert ERC721NonexistentToken(tokenId);

        }

        return owner;

    }



    /**

     * @dev Private function to invoke {IERC721Receiver-onERC721Received} on a target address. This will revert if the

     * recipient doesn't accept the token transfer. The call is not executed if the target address is not a contract.

     *

     * @param from address representing the previous owner of the given token ID

     * @param to target address that will receive the tokens

     * @param tokenId uint256 ID of the token to be transferred

     * @param data bytes optional data to send along with the call

     */

    function _checkOnERC721Received(address from, address to, uint256 tokenId, bytes memory data) private {

        if (to.code.length > 0) {

            try IERC721Receiver(to).onERC721Received(_msgSender(), from, tokenId, data) returns (bytes4 retval) {

                if (retval != IERC721Receiver.onERC721Received.selector) {

                    revert ERC721InvalidReceiver(to);

                }

            } catch (bytes memory reason) {

                if (reason.length == 0) {

                    revert ERC721InvalidReceiver(to);

                } else {

                    /// @solidity memory-safe-assembly

                    assembly {

                        revert(add(32, reason), mload(reason))

                    }

                }

            }

        }

    }

}



// File: @openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol





// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC721/extensions/ERC721Enumerable.sol)



pragma solidity ^0.8.20;









/**

 * @dev This implements an optional extension of {ERC721} defined in the EIP that adds enumerability

 * of all the token ids in the contract as well as all token ids owned by each account.

 *

 * CAUTION: `ERC721` extensions that implement custom `balanceOf` logic, such as `ERC721Consecutive`,

 * interfere with enumerability and should not be used together with `ERC721Enumerable`.

 */

abstract contract ERC721Enumerable is ERC721, IERC721Enumerable {

    mapping(address owner => mapping(uint256 index => uint256)) private _ownedTokens;

    mapping(uint256 tokenId => uint256) private _ownedTokensIndex;



    uint256[] private _allTokens;

    mapping(uint256 tokenId => uint256) private _allTokensIndex;



    /**

     * @dev An `owner`'s token query was out of bounds for `index`.

     *

     * NOTE: The owner being `address(0)` indicates a global out of bounds index.

     */

    error ERC721OutOfBoundsIndex(address owner, uint256 index);



    /**

     * @dev Batch mint is not allowed.

     */

    error ERC721EnumerableForbiddenBatchMint();



    /**

     * @dev See {IERC165-supportsInterface}.

     */

    function supportsInterface(bytes4 interfaceId) public view virtual override(IERC165, ERC721) returns (bool) {

        return interfaceId == type(IERC721Enumerable).interfaceId || super.supportsInterface(interfaceId);

    }



    /**

     * @dev See {IERC721Enumerable-tokenOfOwnerByIndex}.

     */

    function tokenOfOwnerByIndex(address owner, uint256 index) public view virtual returns (uint256) {

        if (index >= balanceOf(owner)) {

            revert ERC721OutOfBoundsIndex(owner, index);

        }

        return _ownedTokens[owner][index];

    }



    /**

     * @dev See {IERC721Enumerable-totalSupply}.

     */

    function totalSupply() public view virtual returns (uint256) {

        return _allTokens.length;

    }



    /**

     * @dev See {IERC721Enumerable-tokenByIndex}.

     */

    function tokenByIndex(uint256 index) public view virtual returns (uint256) {

        if (index >= totalSupply()) {

            revert ERC721OutOfBoundsIndex(address(0), index);

        }

        return _allTokens[index];

    }



    /**

     * @dev See {ERC721-_update}.

     */

    function _update(address to, uint256 tokenId, address auth) internal virtual override returns (address) {

        address previousOwner = super._update(to, tokenId, auth);



        if (previousOwner == address(0)) {

            _addTokenToAllTokensEnumeration(tokenId);

        } else if (previousOwner != to) {

            _removeTokenFromOwnerEnumeration(previousOwner, tokenId);

        }

        if (to == address(0)) {

            _removeTokenFromAllTokensEnumeration(tokenId);

        } else if (previousOwner != to) {

            _addTokenToOwnerEnumeration(to, tokenId);

        }



        return previousOwner;

    }



    /**

     * @dev Private function to add a token to this extension's ownership-tracking data structures.

     * @param to address representing the new owner of the given token ID

     * @param tokenId uint256 ID of the token to be added to the tokens list of the given address

     */

    function _addTokenToOwnerEnumeration(address to, uint256 tokenId) private {

        uint256 length = balanceOf(to) - 1;

        _ownedTokens[to][length] = tokenId;

        _ownedTokensIndex[tokenId] = length;

    }



    /**

     * @dev Private function to add a token to this extension's token tracking data structures.

     * @param tokenId uint256 ID of the token to be added to the tokens list

     */

    function _addTokenToAllTokensEnumeration(uint256 tokenId) private {

        _allTokensIndex[tokenId] = _allTokens.length;

        _allTokens.push(tokenId);

    }



    /**

     * @dev Private function to remove a token from this extension's ownership-tracking data structures. Note that

     * while the token is not assigned a new owner, the `_ownedTokensIndex` mapping is _not_ updated: this allows for

     * gas optimizations e.g. when performing a transfer operation (avoiding double writes).

     * This has O(1) time complexity, but alters the order of the _ownedTokens array.

     * @param from address representing the previous owner of the given token ID

     * @param tokenId uint256 ID of the token to be removed from the tokens list of the given address

     */

    function _removeTokenFromOwnerEnumeration(address from, uint256 tokenId) private {

        // To prevent a gap in from's tokens array, we store the last token in the index of the token to delete, and

        // then delete the last slot (swap and pop).



        uint256 lastTokenIndex = balanceOf(from);

        uint256 tokenIndex = _ownedTokensIndex[tokenId];



        // When the token to delete is the last token, the swap operation is unnecessary

        if (tokenIndex != lastTokenIndex) {

            uint256 lastTokenId = _ownedTokens[from][lastTokenIndex];



            _ownedTokens[from][tokenIndex] = lastTokenId; // Move the last token to the slot of the to-delete token

            _ownedTokensIndex[lastTokenId] = tokenIndex; // Update the moved token's index

        }



        // This also deletes the contents at the last position of the array

        delete _ownedTokensIndex[tokenId];

        delete _ownedTokens[from][lastTokenIndex];

    }



    /**

     * @dev Private function to remove a token from this extension's token tracking data structures.

     * This has O(1) time complexity, but alters the order of the _allTokens array.

     * @param tokenId uint256 ID of the token to be removed from the tokens list

     */

    function _removeTokenFromAllTokensEnumeration(uint256 tokenId) private {

        // To prevent a gap in the tokens array, we store the last token in the index of the token to delete, and

        // then delete the last slot (swap and pop).



        uint256 lastTokenIndex = _allTokens.length - 1;

        uint256 tokenIndex = _allTokensIndex[tokenId];



        // When the token to delete is the last token, the swap operation is unnecessary. However, since this occurs so

        // rarely (when the last minted token is burnt) that we still do the swap here to avoid the gas cost of adding

        // an 'if' statement (like in _removeTokenFromOwnerEnumeration)

        uint256 lastTokenId = _allTokens[lastTokenIndex];



        _allTokens[tokenIndex] = lastTokenId; // Move the last token to the slot of the to-delete token

        _allTokensIndex[lastTokenId] = tokenIndex; // Update the moved token's index



        // This also deletes the contents at the last position of the array

        delete _allTokensIndex[tokenId];

        _allTokens.pop();

    }



    /**

     * See {ERC721-_increaseBalance}. We need that to account tokens that were minted in batch

     */

    function _increaseBalance(address account, uint128 amount) internal virtual override {

        if (amount > 0) {

            revert ERC721EnumerableForbiddenBatchMint();

        }

        super._increaseBalance(account, amount);

    }

}



// File: CDG.sol





pragma solidity ^0.8.20;











contract ChateauDeGeneratif is ERC721Enumerable, ReentrancyGuard {

    /*

                                   

        .g8"""bgd `7MM"""Yb.     .g8"""bgd      

        .dP'     `M   MM    `Yb. .dP'     `M      

        dM'       `   MM     `Mb dM'       `      

        MM            MM      MM MM               

        MM.           MM     ,MP MM.    `7MMF'    

        `Mb.     ,'   MM    ,dP' `Mb.     MM      

        `"bmmmd'  .JMMmmmdP'     `"bmmmdPY     (2023)

    

    */

    using GenLib for uint8;



    struct TokenDetails {

        address tokenAddress;

        uint256 mintPrice;

        uint256 mintLimit;

        uint256 mintCount;

        bool isAccepted;

    }

    

    struct Trait {

        string traitName;

        string traitType;

        string pixels;

        uint256 pixelCount;

    }



    bool public mintingOpen = false;



    //Mappings

    mapping(address => uint256) public walletMintCount;

    mapping(uint256 => Trait[]) public traitTypes;

    mapping(string => bool) hashToMinted;

    mapping(uint256 => string) internal tokenIdToHash;

    mapping(uint8 => TokenDetails) public tokenDetails;

    mapping(uint8 => string[]) private secondTerms;



    //uint256s

    uint256 SEED_NONCE = 0;

    uint256 public maxSupply = 2800;

    uint256 public mintLimitPerWallet = 10;

    uint256 private constant splPer1 = 70;



    //ERC-20 Token Addresses

    address constant ETH_ADDRESS = 0x0000000000000000000000000000000000000000;

    

    address LINK_ADDRESS = 0x514910771AF9Ca656af840dff83E8264EcF986CA;

    address WBTC_ADDRESS = 0x2260FAC5E5542a773Aa44fBCfeDf7C193bc2C599;

    address RLB_ADDRESS = 0x046EeE2cc3188071C02BfC1745A6b17c656e3f3d;

    address SHIB_ADDRESS = 0x95aD61b0a150d79219dCF64E1E6Cc01f0B64C4cE;

    address PEPE_ADDRESS = 0x6982508145454Ce325dDbE47a25d4ec3d2311933;

    address BITCOIN_ADDRESS = 0x72e4f9F808C49A2a61dE9C5896298920Dc4EEEa9;

    address APE_ADDRESS = 0x4d224452801ACEd8B2F0aebE155379bb5D594381;

    address LADYS_ADDRESS = 0x12970E6868f88f6557B76120662c1B3E50A646bf;

    address GP_ADDRESS = 0x38Ec27c6F05a169e7eD03132bcA7d0cfeE93C2C5;

    

    //address

    address _owner;

    address payable private address1;

    address private constant address2 = 0xD1f785A1642c3310da57Bc966DB21328d96AC2c2;



    //string arrays

    string[] LETTERS = ["a", "b", "c", "d", "e", "f", "g", "h", "i", "j", "k", "l", "m", "n", "o", "p", "q", "r", "s", "t", "u", "v", "w", "x", "y", "z"];



    //uint arrays

    uint16[][8] TIERS;



    //This contract is a substantially modified fork of Anonymice, credit to the Anonymice devs for their work, which forms the basis of the hash derivation and SVG encoding!



constructor() ERC721("Chateau De Generatif", "CDG") {

        _owner = msg.sender;

        address1 = payable(_owner);



        // Populate second terms for each token type

        secondTerms[0] = ["Vitalique", "Gweigio", "DeFiasco", "De Generatif", "Hashianti", "Defignac", "Blockbelle"]; // ETH

        secondTerms[1] = ["Chainiot Noir", "D'Oracle", "Nodevignoble", "Linklanc", "Linklaret", "Oracelle", "Syncsirah"]; // LINK

        secondTerms[2] = ["Satoshiraz", "Wraprignon", "Ledgernet", "Bitbourg", "Hasherlot", "Ledgerlieu", "Bitbeau"]; // WBTC

        secondTerms[3] = ["Casinot", "Betblanc", "Rol' Brut", "Rollblanc", "Rouletteau", "Gambelais", "Casinac"]; // RLB

        secondTerms[4] = ["Le Dogelais", "Pawsecco", "Puppignon", "Shibernet", "Inuvignon", "Dogelato", "Puppoli"]; // SHIB

        secondTerms[5] = ["De Pepe", "Le Pepernay", "Greenogrigio", "Pays Du Pepe", "Ribbitret", "Froggiac", "Hopricot"]; // PEPE

        secondTerms[6] = ["Sonique", "Hedgehogue", "Barack", "Obamaroma", "Pottere", "Le Coin De Bite", "Hedgehaut"]; // BITCOIN

        secondTerms[7] = ["Boredaux", "Apevignon", "Apellation D'Ape", "Apertino", "Apeaujolais", "Yachtinac", "Primatage"]; // APE

        secondTerms[8] = ["Remiliot", "De La Rave", "Ladylicante", "Shizo", "Miladoret", "Du Frank", "Chibicru"]; // LADYS

        secondTerms[9] = ["D'Or", "Chainpain", "Du Dragon", "Goldpour", "Gold Cuvee", "Dragonello", "Dragonderne"]; // GP



        // TokenDetails Enumerate

        tokenDetails[0] = TokenDetails(ETH_ADDRESS, 40000000000000000, 2240, 0, true); // ETH

        tokenDetails[1] = TokenDetails(LINK_ADDRESS, 5430000000000000000, 336, 0, true); // LINK

        tokenDetails[2] = TokenDetails(WBTC_ADDRESS, 170000, 280, 0, true); // WBTC

        tokenDetails[3] = TokenDetails(RLB_ADDRESS, 461000000000000000000, 280, 0, true); // RLB

        tokenDetails[4] = TokenDetails(SHIB_ADDRESS, 7800000000000000000000000, 336, 0, true); // SHIB

        tokenDetails[5] = TokenDetails(PEPE_ADDRESS, 50200000000000000000000000, 336, 0, true); // PEPE

        tokenDetails[6] = TokenDetails(BITCOIN_ADDRESS, 64000000000, 168, 0, true); // BITCOIN

        tokenDetails[7] = TokenDetails(APE_ADDRESS, 37000000000000000000, 140, 0, true); // APE

        tokenDetails[8] = TokenDetails(LADYS_ADDRESS, 351000000000000000000000000, 140, 0, true); // LADYS

        tokenDetails[9] = TokenDetails(GP_ADDRESS, 400000000000000000000000, 49, 0, true); // GP



        // Type

        TIERS[0] = [5300, 100, 850, 500, 700, 900, 700, 350, 300, 250];

        // Domaine

        TIERS[1] = [3400, 1200, 1100, 1000, 900, 500, 500, 500, 500, 400];

        // Vintage

        TIERS[2] = [3100, 2100, 1475, 1150, 800, 600, 350, 200, 125, 100];

        // Accessory

        TIERS[3] = [8300, 400, 250, 150, 300, 600];

        // Top

        TIERS[4] = [6000, 1000, 150, 250, 500, 750, 300, 750, 300];

        // Label

        TIERS[5] = [1700, 1800, 600, 1800, 1400, 1400, 800, 100, 400];

        // Condition

        TIERS[6] = [9000, 750, 200, 50];

        // Bottle

        TIERS[7] = [3700, 500, 1000, 1900, 1000, 1900];



    }



    /*



    888b     d888 d8b          888    

    8888b   d8888 Y8P          888    

    88888b.d88888              888    

    888Y88888P888 888 88888b.  888888 

    888 Y888P 888 888 888 "88b 888    

    888  Y8P  888 888 888  888 888    

    888   "   888 888 888  888 Y88b.  

    888       888 888 888  888  "Y888 



   */



    /**

     * @dev Converts a digit from 0 - 10000 into its corresponding rarity based on the given rarity tier.

     * @param _randinput The input from 0 - 10000 to use for rarity gen.

     * @param _rarityTier The tier to use.

     */

    function rarityGen(uint256 _randinput, uint8 _rarityTier)

        internal

        view

        returns (string memory)

    {

        uint16 currentLowerBound = 0;

        for (uint8 i = 0; i < TIERS[_rarityTier].length; i++) {

            uint16 thisPercentage = TIERS[_rarityTier][i];

            if (

                _randinput >= currentLowerBound &&

                _randinput < currentLowerBound + thisPercentage

            ) return i.toString();

            currentLowerBound = currentLowerBound + thisPercentage;

        }



        revert();

    }



    /**

     * @dev Generates a 9 digit hash from a tokenId, address, and random number.

     * @param _t The token id to be used within the hash.

     * @param _a The address to be used within the hash.

     * @param _c The custom nonce to be used within the hash.

     */

    function hash(

        uint256 _t,

        address _a,

        uint256 _c,

        uint8 _tokenId

    ) internal returns (string memory) {

        require(_c < 10);



        // This will generate a 9 character string.

        // Initialize currentHash directly with the string representation of _tokenId

        

        string memory currentHash = GenLib.toString(_tokenId);



        for (uint8 i = 0; i < 8; i++) {

            SEED_NONCE++;

            uint16 _randinput = uint16(

                uint256(

                    keccak256(

                        abi.encodePacked(

                            block.timestamp,

                            block.prevrandao,

                            _t,

                            _a,

                            _c,

                            SEED_NONCE

                        )

                    )

                ) % 10000

            );



            currentHash = string(

                abi.encodePacked(currentHash, rarityGen(_randinput, i))

            );

        }



        if (hashToMinted[currentHash]) return hash(_t, _a, _c + 1, _tokenId);



        return currentHash;

    }



    /**

     * @notice Mints your NFTs with the specified token

     * @param _tokenId The ID of the token to use for minting

     *                 0 for ETH, 1 for LINK, 2 for WBTC, 3 for RLB (...)

     * @param _quantity The number of NFTs to mint in this transaction.

     *                  Must be a positive number and not exceed the set mint limit (5).

     */

    function mintCDG(uint8 _tokenId, uint8 _quantity) payable public nonReentrant {

        require(mintingOpen || msg.sender == _owner, "Minting is closed");

        require(walletMintCount[msg.sender] + _quantity <= mintLimitPerWallet || msg.sender == _owner, "Exceeds wallet mint limit");

        require(_quantity > 0 && (_quantity <= 5 || msg.sender == _owner), "1 to 5 NFTs allowed per transaction");



        TokenDetails storage details = tokenDetails[_tokenId];

        require(details.isAccepted, "Token not accepted");



        uint256 totalTokenAmount = details.mintPrice * _quantity;

        uint256 _totalSupply = totalSupply();

        require(_totalSupply + _quantity <= maxSupply, "Exceeds max supply");

        require(details.mintCount + _quantity <= details.mintLimit, "Exceeds mint limit for token");



        if (_tokenId == 0) {

            require(msg.value == totalTokenAmount, "Incorrect ETH value");

        } else {

            require(msg.value == 0, "ETH not required for ERC-20 minting");

            IERC20 tokenContract = IERC20(details.tokenAddress);

            require(tokenContract.transferFrom(msg.sender, address(this), totalTokenAmount), "ERC20 Transfer failed");

        }



        for (uint8 i = 0; i < _quantity; i++) {

            uint256 newTokenId = _totalSupply + i;

            tokenIdToHash[newTokenId] = hash(newTokenId, msg.sender, 0, _tokenId);

            hashToMinted[tokenIdToHash[newTokenId]] = true;

            _mint(msg.sender, newTokenId);

        }



        details.mintCount += _quantity;

        walletMintCount[msg.sender] += _quantity;

    }



    /*



    8888888b.                        888 

    888   Y88b                       888 

    888    888                       888 

    888   d88P .d88b.   8888b.   .d88888 

    8888888P" d8P  Y8b     "88b d88" 888 

    888 T88b  88888888 .d888888 888  888 

    888  T88b Y8b.     888  888 Y88b 888 

    888   T88b "Y8888  "Y888888  "Y88888  



    */



    /**

    * @dev Helper function to generate name

    */

    function generateName(string memory tokenHash) public view returns (string memory) {

        // Parse once and reuse

        uint8[4] memory indices = [

            GenLib.parseInt(GenLib.substring(tokenHash, 0, 1)), // tokenType

            GenLib.parseInt(GenLib.substring(tokenHash, 1, 2)), // suffixIndex

            GenLib.parseInt(GenLib.substring(tokenHash, 2, 3)), // prefixIndex

            GenLib.parseInt(GenLib.substring(tokenHash, 3, 4))  // vintageIndex

        ];



        // Hash the tokenHash once for reuse

        uint256 hashValue = uint256(keccak256(abi.encodePacked(tokenHash)));



        // Retrieve the prefix, suffix, and a term from secondTerms

        string memory suffix = traitTypes[1][indices[1]].traitName;

        string memory prefix = traitTypes[2][indices[2]].traitName;

        string memory secondTerm = secondTerms[indices[0]][hashValue % 7];



        // Year computation

        string memory yearStr = traitTypes[3][indices[3]].traitName;

        uint256 startYear = GenLib.parseInt256(GenLib.substring(yearStr, 0, 4));

        uint256 range = traitTypes[3][indices[3]].pixelCount;

        string memory year = GenLib.toString(startYear - (hashValue % (range + 1)));



        return string(abi.encodePacked(prefix, secondTerm, suffix, " ", year));

    }



    /**

     * @dev Helper function to reduce pixel size within contract

     */

    function letterToNumber(string memory _inputLetter)

        internal

        view

        returns (uint8)

    {

        for (uint8 i = 0; i < LETTERS.length; i++) {

            if (

                keccak256(abi.encodePacked((LETTERS[i]))) ==

                keccak256(abi.encodePacked((_inputLetter)))

            ) return (i);

        }

        revert();

    }



    /**

    * @dev Hash to SVG function

    */

    function hashToSVG(string memory _hash) public view returns (string memory) {

        string memory svgString;

        bool[26][26] memory placedPixels;

        bool shouldFlip = shouldFlipSVG(_hash);



        // Process trait-based pixels

        for (uint8 i = 4; i < 9; i++) { 

            uint8 thisTraitIndex = GenLib.parseInt(

                GenLib.substring(_hash, i, i + 1)

            );

            for (uint16 j = 0; j < traitTypes[i][thisTraitIndex].pixelCount; j++) {

                string memory thisPixel = GenLib.substring(

                    traitTypes[i][thisTraitIndex].pixels,

                    j * 4,

                    j * 4 + 4

                );

                svgString = processPixel(thisPixel, placedPixels, svgString, shouldFlip, 1, 2);

            }

        }



        // Process hardcoded pixels

        string memory hardcodedPixels = "ha11ia11ja11ka11hb11ib10jb10kb11hc11ic10jc10kc11hd11id10jd10kd11he11ie09je09ke11hf11kf11hg11kg11il00jl00im00jm00hn00in00jn00kn00ho00io00jo00ko00hp00ip00jp00kp00hq00iq00jq00kq00hr00ir00jr00kr00hs00is00js00ks00ht00it00jt00kt00hu00iu00ju00ku00hv00iv00jv00kv00hw00iw00jw00kw00ix00jx00iy00jy00hz11iz11jz11kz11";

        for (uint16 k = 0; k < bytes(hardcodedPixels).length / 4; k++) {

            string memory pixel = GenLib.substring(hardcodedPixels, k * 4, (k + 1) * 4);

            svgString = processPixel(pixel, placedPixels, svgString, shouldFlip, 1 , 2);

        }



        // Generate color styles and construct the final SVG string

        string memory colorStyles = generateColorStyles(_hash);



        svgString = string(

            abi.encodePacked(

                '<svg id="wine-svg" xmlns="http://www.w3.org/2000/svg" preserveAspectRatio="xMinYMin meet" viewBox="0 0 20 30">',

                svgString,

                "<style>rect{width:1px;height:1px;} #wine-svg{shape-rendering: crispedges;} ",

                colorStyles,

                "</style></svg>"

            )

        );



        return svgString;

    }



    function shouldFlipSVG(string memory _hash) internal pure returns (bool) {

        uint256 hashNumber = uint256(keccak256(abi.encodePacked(_hash)));

        return hashNumber % 10 % 2 == 0;

    }



    /**

    * @dev Helper function to process a pixel and update the SVG string

    */

    function processPixel(string memory thisPixel, bool[26][26] memory placedPixels, string memory svgString, bool shouldFlip, uint8 paddingLeft, uint8 paddingTop) internal view returns (string memory) {

        uint8 originalX = letterToNumber(

            GenLib.substring(thisPixel, 0, 1)

        );

        uint8 y = letterToNumber(

            GenLib.substring(thisPixel, 1, 2)

        );



        uint8 x = shouldFlip ? (17 - originalX) : originalX;



        if (!placedPixels[x][y]) {

            svgString = string(

                abi.encodePacked(

                    svgString,

                    "<rect class='c",

                    GenLib.substring(thisPixel, 2, 4),

                    "' x='",

                    (x + paddingLeft).toString(),

                    "' y='",

                    (y + paddingTop).toString(),

                    "'/>"

                )

            );

            placedPixels[x][y] = true;

        }

        return svgString;

    }



    /**

    * @dev Helper function to generate color styles

    */

    function generateColorStyles(string memory _hash) internal view returns (string memory) {

        string memory colorStyles = "";



        // Extract color codes for traits 0, 1, and 2

        string memory colorCodesTrait0 = traitTypes[0][GenLib.parseInt(GenLib.substring(_hash, 0, 1))].pixels;

        string memory colorCodesTrait1 = traitTypes[1][GenLib.parseInt(GenLib.substring(_hash, 1, 2))].pixels;

        string memory colorCodesTrait2 = traitTypes[2][GenLib.parseInt(GenLib.substring(_hash, 2, 3))].pixels;



        // c00-c02

        for (uint i = 0; i < 3; i++) {

            string memory colorCode = GenLib.substring(colorCodesTrait1, i * 3, (i + 1) * 3);

            colorStyles = string(abi.encodePacked(colorStyles, ".c0", GenLib.toString(i), "{fill:#", colorCode, "}"));

        }



        // c03-c05

        for (uint i = 0; i < 3; i++) {

            string memory colorCode = GenLib.substring(colorCodesTrait2, i * 3, (i + 1) * 3);

            colorStyles = string(abi.encodePacked(colorStyles, ".c0", GenLib.toString(i + 3), "{fill:#", colorCode, "}"));

        }



        // c06-c08

        colorStyles = string(abi.encodePacked(colorStyles, ".c06{fill:#e64539}.c07{fill:#f57}.c08{fill:#a34}"));



        // c09-c10

        for (uint i = 0; i < 2; i++) {

            string memory colorCode = GenLib.substring(colorCodesTrait0, i * 3, (i + 1) * 3);

            string memory className = i == 0 ? ".c09" : ".c10";

            colorStyles = string(abi.encodePacked(colorStyles, className, "{fill:#", colorCode, "}"));

        }



        // Append static color styles

        colorStyles = string(abi.encodePacked(colorStyles, ".c11{fill:#000}.c12{fill:#291d2b}.c13{fill:#ffc2a1}.c14{fill:#f0b541}.c15{fill:#3b2027}.c16{fill:#bd6a62}.c17{fill:#efe}.c18{fill:#cf7e2b}.c19{fill:#ab5130}.c20{fill:#3d2936}"));



        return colorStyles;

    }



    /**

     * @dev Hash to metadata function

     */

    function hashToMetadata(string memory _hash)

        public

        view

        returns (string memory)

    {

        string memory metadataString;



        for (uint8 i = 0; i < 9; i++) {

            uint8 thisTraitIndex = GenLib.parseInt(

                GenLib.substring(_hash, i, i + 1)

            );



            metadataString = string(

                abi.encodePacked(

                    metadataString,

                    '{"trait_type":"',

                    traitTypes[i][thisTraitIndex].traitType,

                    '","value":"',

                    traitTypes[i][thisTraitIndex].traitName,

                    '"}'

                )

            );



            if (i != 8)

                metadataString = string(abi.encodePacked(metadataString, ","));

        }



        return string(abi.encodePacked("[", metadataString, "]"));

    }



    /**

     * @dev Returns the SVG and metadata for a token Id

     * @param _tokenId The tokenId to return the SVG and metadata for.

     */

    function tokenURI(uint256 _tokenId)

        public

        view

        override

        returns (string memory) 

        {

        require(ownerOf(_tokenId) != address(0), "ERC721Metadata: URI query for nonexistent token");



        string memory tokenHash = _tokenIdToHash(_tokenId);

        string memory name = generateName(tokenHash);



        return

            string(

                abi.encodePacked(

                    "data:application/json;base64,",

                    GenLib.encode(

                        bytes(

                            string(

                                abi.encodePacked(

                                '{"name": "', name, '", "tokenId": "', 

                                GenLib.toString(_tokenId),

                                    '", "description": "A bottle of the finest ', name, '. Generated and stored fully on-chain, courtesy of CDG.", "image": "data:image/svg+xml;base64,',

                                    GenLib.encode(

                                        bytes(hashToSVG(tokenHash))

                                    ),

                                    '","attributes":',

                                    hashToMetadata(tokenHash),

                                    "}"

                                )

                            )

                        )

                    )

                )

            );

    }



    /**

     * @dev Returns a hash for a given tokenId

     * @param _tokenId The tokenId to return the hash for.

     */

    function _tokenIdToHash(uint256 _tokenId)

        public

        view

        returns (string memory)

    {

        string memory tokenHash = tokenIdToHash[_tokenId];

        return tokenHash;

    }



    /*



    .d88888b.                                          

    d88P" "Y88b                                         

    888     888                                         

    888     888 888  888  888 88888b.   .d88b.  888d888 

    888     888 888  888  888 888 "88b d8P  Y8b 888P"   

    888     888 888  888  888 888  888 88888888 888     

    Y88b. .d88P Y88b 888 d88P 888  888 Y8b.     888     

     "Y88888P"   "Y8888888P"  888  888  "Y8888  888     



    */



    /**

     * @dev Add a trait type

     * @param _traitTypeIndex The trait type index

     * @param traits Array of traits to add

     */



    function addTraitType(uint256 _traitTypeIndex, Trait[] memory traits)

        public

        onlyOwner

    {

        for (uint256 i = 0; i < traits.length; i++) {

            traitTypes[_traitTypeIndex].push(

                Trait(

                    traits[i].traitName,

                    traits[i].traitType,

                    traits[i].pixels,

                    traits[i].pixelCount

                )

            );

        }



        return;

    }



    /**

     * @dev Clears the traits.

     */

    function clearTraits() public onlyOwner {

        for (uint256 i = 0; i < 9; i++) {

            delete traitTypes[i];

        }

    }



    /**

    * @dev Ability to change token details if needed

    */

    function setTokenDetails(uint8 _id, address _tokenAddress, uint256 _mintPrice, uint256 _mintLimit, bool _isAccepted) external onlyOwner {

        tokenDetails[_id] = TokenDetails(_tokenAddress, _mintPrice, _mintLimit, tokenDetails[_id].mintCount, _isAccepted);

    }



    /**

    * @dev Allows the owner to reduce the max supply.

    * @param _newMaxSupply The new max supply, must be less than current max supply.

    */

    function reduceMaxSupply(uint256 _newMaxSupply) external onlyOwner {

        require(_newMaxSupply < maxSupply, "New max supply must be less than current max supply");

        maxSupply = _newMaxSupply;

    }



    /**

    * @dev Sets the minting status.

    * @param _mintingStatus Status of minting (open or closed).

    */

    function setMintingStatus(bool _mintingStatus) external onlyOwner {

        mintingOpen = _mintingStatus;

    }



    /**

    * @dev Sets mint limit per wallet and transfers ownership.

    * @param _mintLimitPerWallet Mint limit per wallet.

    * @param _newOwner Address of the new owner. Pass the current owner's address to leave unchanged.

    */

    function setMintLimitAndOwnership(uint256 _mintLimitPerWallet, address _newOwner) external onlyOwner {

        mintLimitPerWallet = _mintLimitPerWallet;



        if(_newOwner != address(0) && _newOwner != _owner) {

            _owner = _newOwner;

        }

    }



    /**

    * @dev Withdraw ETH to withdrawal addresses.

    */

    function withdrawETH() public onlyOwner {

        uint256 balance = address(this).balance;

        uint256 split1 = (balance * splPer1) / 100;

        uint256 split2 = balance - split1;



        address1.transfer(split1);

        address payable payableAddress2 = payable(address2);

        payableAddress2.transfer(split2);

    }



    /**

    * @dev Withdraw ETH to withdrawal addresses.

    * @param _tokenAddress Address of ERC-20 token to withdraw.

    */

    function withdrawERC20(address _tokenAddress) public onlyOwner {

        IERC20 token = IERC20(_tokenAddress);

        uint256 balance = token.balanceOf(address(this));

        uint256 split1 = (balance * splPer1) / 100;

        uint256 split2 = balance - split1;



        token.transfer(address1, split1);

        token.transfer(address2, split2);

    }



    /**

     * @dev Modifier to only allow owner to call functions

     */

    modifier onlyOwner() {

        require(_owner == msg.sender);

        _;

    }

}